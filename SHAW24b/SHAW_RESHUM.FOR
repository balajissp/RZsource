C
C***********************************************************************
C
      SUBROUTINE RESHUM(HUM,DHDW,dGMC,TR)
C
C     THIS SUBROUTINE DEFINES THE RELATION BETWEEN THE HUMIDITY AND THE
C     MOISTURE CONTENT OF THE RESIDUE.  GIVEN THE GRAVIMETRIC MOISTURE
C     CONTENT OF THE RESIDUE, THE HUMIDITY AND DERIVATIVE OF HUMIDITY
C     WITH RESPECT TO MOISTURE CONTENT ARE CALCULATED
C
C***********************************************************************
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (UGAS=8.3143D0,RESMA=-53.72D0,RESMB=1.32D0,g=9.81d0)
C
C     HUMIDITY WILL BE DETERMINED FROM THE COEFFICIENTS WHICH DESCRIBE
C     THE RELATION BETWEEN MOISTURE TENSION AND MOISTURE CONTENT AS
C     REPORTED BY MYROLD ET. AL (1981).  THE MOISTURE TENSION IS THEN
C     CONVERTED TO A HUMIDITY.
C                TENSION = A*W**B
C                H=EXP(M*TENSION/(UGAS*TEMP))
C     THEREFORE  H=EXP(M*A*W**B/(UGAS*TEMP))
C
      IF (dGMC .LE. 0.0) THEN
C        EQUATIONS CANNOT HANDLE CASE WHERE GMC=0.0 -- SET HUM=0.0
         HUM=0.0d0
         DHDW=0.0d0
        ELSE
C
C****    USE HUMDITITY-TENSION-MOISTURE CONTENT RELATIONS
         HUM=EXP(0.018d0*G/(UGAS*(TR+273.16d0))*RESMA*dGMC**(-RESMB))
         DHDW=-0.018d0*G*RESMA*RESMB*HUM*dGMC**(-RESMB-1.d0)
     >        /(UGAS*(TR+273.16d0))
      END IF
C
      RETURN
      END

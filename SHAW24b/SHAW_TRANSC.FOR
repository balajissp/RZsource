C
      SUBROUTINE TRANSC(NC,TDIRCC,TDIFFC,DIFKL,DIRKL,FBDU,FDDU,
     >         TOTLAI,SUNSLP,ALTITU,CANLAI,XANGLE,CLUMPNG,NPLANT)
C
C     THIS SUBROUTINE CALCULATES THE DIRECT AND DIFFUSE RADIATION
C     TRANSMISSION COEFFICIENTS AND THE SUM OF THE ATTENUATION FACTOR
C     TIMES LEAF AREA INDEX FOR EACH CANOPY LAYER. (THIS SUM IS USED IN
C     CALCULATING AN EFFECTIVE ALBEDO FOR THE CANOPY LAYER AND IN
C     PROPORTIONING ABSORBED RADIATION INTO EACH OF THE PLANT SPECIES.
C
C***********************************************************************
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (nodcan=11, MXSPEC=10)
C
      DOUBLE PRECISION TDIRCC(NODCAN),TDIFFC(NODCAN),
     +    DIRKL(MXSPEC+1,NODCAN),DIFKL(MXSPEC+1,NODCAN),
     +    FBDU(MXSPEC),FDDU(MXSPEC),TOTLAI(MXSPEC),
     +    CANLAI(MXSPEC,NODCAN),DIRCAN(MXSPEC),DIFCAN(MXSPEC),
     +    XANGLE(MXSPEC),CLUMPNG(MXSPEC)
C
      DATA AA/0.65D0/ BB/1.9D0/ CC/1.46D0/ DD/ 0.585D0/ EE/ 0.569D0/  
     >     FF/ 1.09D0/ GG/ 1.585D0/
C
C
      DO 5 J=1,NPLANT
C        COMUPUTE ATTENUATION FACTOR BASED ON SOLAR AND LEAF ANGLE
         IF (SUNSLP .GT. 0.0) THEN
C           SUN IS ABOVE HORIZON 
            IF (XANGLE(J) .LE. 0.0D0) THEN
C              LEAF ANGLE INCLINATION IS VERTICLE -- USE SOLAR ALTITUDE
C              INSTEAD OF ANGLE ON LOCAL SLOPE AND USE SIMPLIFIED 
C              EXPRESSION FOR K
               IF (ALTITU .GE. 1.57D0) THEN
                  DIRCAN(J) = 0.0D0
                 ELSE
                  DIRCAN(J)=1.D0/(1.5708D0*TAN(ALTITU))
               END IF
             ELSE
C            	 LEAF ORIENTATION OTHER THAN VERTICLE -- USE ANGLE ON 
C            	 LOCAL SLOPE -- COMPUTE ZENITH ANGLE
               ZEN = 1.571D0 - SUNSLP
               DIRCAN(J)=(XANGLE(J)*XANGLE(J) + (TAN(ZEN))**2)**0.5D0
     >            /(XANGLE(J) + 1.774D0*(XANGLE(J)+1.182D0)**(-0.733D0))
             END IF
C           AVOID UNREALISTICALLY LARGE Kd AT VERY LOW SUN ANGLES
            IF (DIRCAN(J) .GT. 10.D0) DIRCAN(J)=10.d0
          ELSE
C           SUN IS BELOW HORIZON - JUST SET DIRCAN(J) TO 1.0
            DIRCAN(J) = 1.0D0
         END IF
C        COMPUTE Kd AT INFINITE LEAF AREA
         IF (XANGLE(J) .LE. 1.0D0) THEN
            DIFINF = ATAN(XANGLE(J))/1.5708D0
          ELSE
            DIFINF = XANGLE(J)**CC/(XANGLE(J)**CC + 1.D0)
         END IF
         DIFCAN(J)= (DIFINF*CLUMPNG(J)*TOTLAI(J)**AA + BB)/
     >               (CLUMPNG(J)*TOTLAI(J)**AA + BB)
C        DEFINE SCATTERING COEFF. FOR INDIVIDUAL PLANT TYPES COMPUTED 
C        FROM FLERCHINGER & YU (2007, AG & FOREST MET).  FBDU IS     
C        FRACTION OF DOWNWARD REFLECTED BEAM RADIATION SCATTERED 
C        UPWARD. FDDU IS THE SAME FOR DIFFUSE RADIATION. 
         FBDU(J) = 0.5D0 + 0.5D0*(ATAN(XANGLE(J))/1.5708D0)**DD 
     >     *XANGLE(J)**((COS(ALTITU))**(EE+FF*XANGLE(J)))*SIN(ALTITU)
         FDDU(J) = 0.5D0 + 0.5D0*(ATAN(XANGLE(J))/1.5708D0)**GG
    5 CONTINUE
C
C**** DETERMINE THE TRANSMISSIVITY TO DIRECT AND DIFFUSE RADIATION
C     BETWEEN CANOPY NODES
      DO 20 I=1,NC
         DIRKL(NPLANT+1,I)=0.0D0
         DIFKL(NPLANT+1,I)=0.0D0
C        CALCULATE PRODUCT OF ATTENUATION COEFFICIENT AND LAI 
         DO 10 J=1,NPLANT
            DIRKL(J,I)=CLUMPNG(J)*CANLAI(J,I)*DIRCAN(J)
            DIFKL(J,I)=CLUMPNG(J)*CANLAI(J,I)*DIFCAN(J)
            DIRKL(NPLANT+1,I)=DIRKL(NPLANT+1,I)+DIRKL(J,I)
            DIFKL(NPLANT+1,I)=DIFKL(NPLANT+1,I)+DIFKL(J,I)
   10    CONTINUE
C        TRANSMISSIVITY OF LAYER IS EQUAL TO EXPONENT OF THE SUM OF
C        ATTENUATION FACTOR TIMES LEAF AREA INDEX
         IF (SUNSLP .LE. 0.0D0) THEN
C           SUN IS ON OR BELOW HORIZON OF LOCAL SLOPE -
C           SET DIRECT TRANSMISSIVITY TO ZERO.
            TDIRCC(I)=0.0D0
           ELSE
            TDIRCC(I)=EXP(-DIRKL(NPLANT+1,I))
         END IF
         TDIFFC(I)=EXP(-DIFKL(NPLANT+1,I))
   20 CONTINUE
      RETURN
      END
C***********************************************************************

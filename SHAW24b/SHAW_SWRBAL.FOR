c    last edited by RMA   9 Jul 96   4:15pm
c
C***********************************************************************
C
c rma\gf
      SUBROUTINE SWRBAL(NC,NSP,NR,SWCAN,SWSNOW,SWRES,SWSOIL,
     +  SUNHOR,HAFDAY,DECLIN,HOUR,DT,ALATUD,SLOPE,ASPECT,RCOVER,
     +  ALBRES,ALBSOI,CANLAI,CANALB,XANGLE,CLUMPNG,NPLANT,
     +  TDIFFC,DIFKL,TDIFFU,FDDU,RHOSP,DZSP,altitu,zsp,TOTLAI)
C
C     THIS SUBROUTINE DETERMINES THE SHORT-WAVE RADIATION BALANCE FOR
C     FOR EACH NODE ABOVE THE GROUND SURFACE.
C
C***********************************************************************
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (NODRES=10, nodcan=11, MXSPEC=10, NODSNO=100)
C
      DOUBLE PRECISION CANLAI(MXSPEC,NODCAN),CANALB(MXSPEC),
     +  TDIFFC(NODCAN),DIFKL(MXSPEC+1,NODCAN),TDIFFU(NODRES),
     +  TDIFFUswr(NODRES),
     +  RHOSP(NODSNO),DZSP(NODSNO),SWCAN(MXSPEC,NODCAN),SWSNOW(NODSNO),
     +    SWRES(NODRES),tdircc(nodcan),tdirec(nodres),zsp(nodsno),
     +    dirkl(MXSPEC+1,nodcan),XANGLE(MXSPEC),CLUMPNG(MXSPEC),
     +    FBDU(MXSPEC),FDDU(MXSPEC),TOTLAI(MXSPEC),albres(nodres)
      INTEGER HOUR
      common /shawp/ pplastic,emitc,emitr(10),emitsp,emits,emitf
     &      ,palbedo,ptransm
C
C**** SEPARATE THE TOTAL SOLAR RADIATION INTO DIRECT AND DIFFUSE
      CALL SOLAR_SHAW (DIRECT,DIFFUS,SUNSLP,ALTITU,SUNHOR,HAFDAY,
     >            DECLIN,HOUR,DT,ALATUD,SLOPE,ASPECT)
      IF ((DIRECT+DIFFUS) .LE. 0.0d0) GO TO 60
C
C**** DETERMINE THE SOIL AND SNOW ALBEDO
      IF (NSP.GT.0) THEN
         IF (NR .GT. 0) THEN
            ALBNXT=ALBRES(1)
           ELSE
            ALBNXT=ALBSOI
         END IF
         CALL SNOALB (NSP,ALBSNO,ALBNXT,RHOSP,ZSP)
      END IF
C
C             
C**** DETERMINE THE SOLAR RADIATION BALANCE FOR EACH MATERIAL, STARTING         
C     FROM THE SURFACE MATERIAL AND WORKING DOWNWARD.                           
C                                                                               
C**** SOLAR RADIATION BALANCE OF THE CANOPY
      IF (NC .GT. 0) THEN
         IF (NSP.EQ.0 .AND. NR.GT.0) THEN
C           RESIDUE LIES BENEATH CANOPY - DETERMINE SOLAR RADIATION
C           BALANCE THROUGH CANOPY AND RESIDUE TOGETHER
            MATERL=MATERL+1
            CALL SWRCAN (NC,NR,0,SWCAN,SWRES,DIRECT,DIFFUS,SUNSLP,
     +      ALTITU,ALBSOI,ALBRES,CANALB,XANGLE,CLUMPNG,NPLANT,CANLAI,
     +      RCOVER,TDIFFC,DIFKL,TDIFFUswr,TDIFFU,FDDU,TOTLAI)
           ELSE
C           CALCULATE RADIATION THROUGH CANOPY DOWN TO SNOW OR SOIL
            IF (NSP .GT. 0) THEN
               ALBNXT=ALBSNO
              ELSE
               ALBNXT=ALBSOI
            END IF
            CALL SWRCAN (NC,0,NSP,SWCAN,SWRES,DIRECT,DIFFUS,SUNSLP,
     +      ALTITU,ALBNXT,ALBRES,CANALB,XANGLE,CLUMPNG,NPLANT,CANLAI,
     +      RCOVER,TDIFFC,DIFKL,TDIFFUswr,TDIFFU,FDDU,TOTLAI)
         END IF
      END IF
C
C**** SOLAR RADIATION BALANCE OF THE SNOWPACK
      IF (NSP .GT. 0) CALL SWRSNO (NSP,SWSNOW,DIRECT,DIFFUS,ALBSNO,
     +                RHOSP,DZSP,SUNSLP)
C
C**** SOLAR RADIATION BALANCE OF THE RESIDUE IF NO CANOPY PRESENT OR IF
C     RESIDUE IS COVERED BY SNOW 
c rma\gf
   40 IF (NR .GT. 0) THEN 
         IF (NSP.GT.0 .OR. NC.EQ.0) THEN
            CALL SWRCAN (0,NR,NSP,SWCAN,SWRES,DIRECT,DIFFUS,
     +      SUNSLP,ALTITU,ALBsoi,ALBRES,CANALB,XANGLE,CLUMPNG,NPLANT,
     +      CANLAI,RCOVER,TDIFFC,DIFKL,TDIFFUswr,TDIFFU,FDDU,TOTLAI)
          END IF
      END IF
C
C**** SOLAR RADIATION BALANCE FOR SOIL SURFACE
   50 SWSOIL=(DIRECT+DIFFUS)*(1.0D0-ALBSOI)
C
      RETURN
C
C**** NO SOLAR RADIATION -- SET ABSORBED SOLAR RADIATION TO ZERO
C
C     CANOPY NODES
   60 DO 70 I=1,NC
         DO 65 J=1,NPLANT+1
            SWCAN(J,I)=0.0d0
   65    CONTINUE
   70 CONTINUE
C     DEFINE THE DIFFUSE RADIATION TRANSMISSION FOR LONG-WAVE BALANCE
      IF (NC .GT. 0) CALL TRANSC (NC,TDIRCC,TDIFFC,DIFKL,DIRKL,
     +  FBDU,FDDU,TOTLAI,0.0d0,0.0d0,CANLAI,XANGLE,CLUMPNG,NPLANT)
C
C     SNOWPACK NODES
      DO 80 I=1,NSP
         SWSNOW(I)=0.0d0
   80 CONTINUE
C
C     RESIDUE NODES
      DO 90 I=1,NR
         SWRES(I)=0.0d0
   90 CONTINUE
C     DEFINE THE DIFFUSE RADIATION TRANSMISSION FOR LONG-WAVE BALANCE
      IF (NR .GT. 0) 
     +  CALL TRANSR (NR,TDIREC,TDIFFUswr,TDIFFU,0.785d0,RCOVER)
C
C     SOIL SURFACE
      SWSOIL=0.0d0
C
      RETURN
      END
C
C***********************************************************************

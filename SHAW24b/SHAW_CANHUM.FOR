C***********************************************************************
C
      SUBROUTINE CANHUM(NCALC,HUM,DHDW,WCAN,TC,CANMA,CANMB)
C
C     THIS SUBROUTINE DEFINES THE RELATION BETWEEN THE HUMIDITY AND THE
C     MOISTURE CONTENT OF THE DEAD CANOPY MATERIAL USING THE CONVERSION
C     BETWEEN WATER POTENTIAL AND HUMIDITY.  DEPENDING ON THE VALUE OF
C     NCALC, THE SUBROUTINE WILL EITHER CALCULATE HUMIDITY AND THE
C     DERIVATIVE OF HUMIDTY WITH RESPECT TO WATER CONTENT FOR A GIVEN
C     WATER CONTENT, OR AN EQUILIBRIUM WATER CONTENT GIVEN THE HUMIDITY.
C     (NCALC = 1: CALCULATE HUMIDITY; OTHERWISE CALCULATE WATER CONTENT)
C***********************************************************************
C
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (G=9.81D0, UGAS=8.3143D0)
C
      IF (NCALC .EQ. 1) THEN
C        CALCULATE HUMIDITY AND SLOPE BASED ON WATER CONTENT
         IF (WCAN .LE. 0.0) THEN
C           EQUATIONS CANNOT HANDLE CASE WHERE WCAN=0.0 -- SET HUM=0.0
            HUM=0.0d0
            DHDW=0.0d0
          ELSE
C
           HUM=EXP(0.018d0*G/(UGAS*(TC+273.16d0))*CANMA*WCAN**(-CANMB))
           DHDW=-0.018d0*G*CANMA*CANMB*HUM*WCAN**(-CANMB-1.d0)
     >           /(UGAS*(TC+273.16d0))
         END IF
        ELSE
C        CALCULATE WATER CONTENT BASED ON HUMIDITY
         IF (HUM .LT. 0.999d0) THEN
            IF (HUM .LE. 0.0d0) THEN
               WCAN = 0.0d0
              ELSE
               WCAN=(LOG(HUM)*UGAS*(TC+273.16d0)/
     +             0.018d0/G/CANMA)**(-1.d0/CANMB)
            END IF
          ELSE
            WCAN=(LOG(0.999D0)*UGAS*(TC+273.16d0)/
     +          0.018d0/G/CANMA)**(-1.d0/CANMB)
         END IF
      END IF
      RETURN
      END

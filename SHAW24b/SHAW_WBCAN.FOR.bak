C***********************************************************************
C
      SUBROUTINE WBCAN(N,NC,NR,NSP,NS,TC,TCDT,TLC,TLCDT,TLCLWR,
     +  VAPC,VAPCDT,WCAN,WCANDT,PCAN,PCANDT,MAT,MATDT,SWCAN,LWCAN,
     +  TDIFFC,DIFKL,ITER,WT,WDT,DT,DCHAR,DRYCAN,
     +  CANLAI,TOTLAI,ZC,CANMA,CANMB,ITYPE,IEVAP,NPLANT,TRNSP,TPOTNL,
     +  XTRACT,ZMSUB,ZHSUB,ZERSUB,STABLE,WINDC,RSTOM0,RSTEXP,PLEAF0,
     +  RLEAF0,RROOT0,TOTROT,RLEAF,RROOT,ROOTDN,QVC,ATRANS,evapmx,
     +  A2,B2,C2,D2,ISTRESS)

C
C     THIS SUBROUTINE CALCULATES THE JACOBIAN MATRIX COEFFICIENTS FOR
C     THE CANOPY PORTION OF THE NEWTON-RAPHSON SOLUTION OF THE WATER
C     BALANCE
C
CnT   THIS SUBROUTINE HAS BEEN MODIFIED TO USE TRANSPIRATION SPECIFIED
CnT   FROM OUTSIDE GOSHAW.  LINES THAT ARE NO LONGER NEEDED OR CHANGED
CnT   HAVE BEEN COMMENTED OUT BY "CnT" 
C
C***********************************************************************
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (MXNOD=300, NODRES=10, nodcan=11, NODSNO=100, 
     + NODTOT=MXNOD+NODCAN+NODRES+NODSNO, MXSPEC=10)
C
      DOUBLE PRECISION DCHAR(MXSPEC,NODCAN),DRYCAN(MXSPEC,NODCAN),
     +  CANLAI(MXSPEC,NODCAN),TOTLAI(MXSPEC),ZC(NODCAN),TRNSP(MXSPEC),
     +  XTRACT(MXNOD),WINDC(NODCAN),RSTOM0(MXSPEC),RSTEXP(MXSPEC),
     +  PLEAF0(MXSPEC),RLEAF0(MXSPEC),RROOT0(MXSPEC),TOTROT(MXSPEC),
     +  RLEAF(MXSPEC,NODCAN),RROOT(MXSPEC,MXNOD),ROOTDN(MXSPEC,MXNOD),
     +  TPOTNL(MXSPEC),A2(NODTOT),B2(NODTOT),C2(NODTOT),D2(NODTOT)
C     STATE VARIABLES OF THE CANOPY
      DOUBLE PRECISION TC(NODCAN),TCDT(NODCAN),VAPC(NODCAN),
     +  VAPCDT(NODCAN),WCAN(NODCAN),WCANDT(NODCAN),PCAN(MXSPEC),
     +  PCANDT(MXSPEC),MAT(MXNOD),MATDT(MXNOD),SWCAN(MXSPEC,NODCAN),
     +  LWCAN(MXSPEC,NODCAN),TDIFFC(NODCAN),DIFKL(MXSPEC+1,NODCAN),
     +  QVC(NODCAN),TLC(MXSPEC,NODCAN),TLCDT(MXSPEC,NODCAN)
C     LOCAL VALIABLES
      DOUBLE PRECISION CON(NODCAN),CONT(NODCAN),
     +  CONDT(NODCAN),HEATC(NODCAN),TLCLWR(MXSPEC,NODCAN),
     +  ETLYR(NODCAN),DETLYR(NODCAN),DHEATC(NODCAN),DTLDTC(NODCAN)
c    +  CONDT(NODCAN),HEATC(NODCAN),ETLAY(NODCAN),RETLAY(NODCAN)
      INTEGER ITYPE(MXSPEC),IEVAP(MXSPEC)
C
      PARAMETER (RHOL=1.0D3, RHOA=1.25D0, CA=1006.D0)
      common /shawp/ pplastic,emitc,emitr(10),emitsp,emits,emitf
     &      ,palbedo,ptransm
C
c rma     + RSTEXP,PLEAF0,RLEAF0,RROOT0,TOTROT,RLEAF,RROOT,ROOTDN,

C**** CALCULATE TRANSPIRATION FROM CANOPY
CnT   CALL LEAFT ONLY TO DISTRIBUTE ATRANS THROUGH CANOPY LAYERS
CnT   TRANSPIRATION IS SUPPLIED FROM OUTSIDE GOSHAW
      CALL LEAFT (NC,NS,ITER,TC,TCDT,TLC,TLCDT,TLCLWR,VAPC,VAPCDT,
     + WCAN,WCANDT,PCAN,PCANDT,MAT,MATDT,TRNSP,TPOTNL,XTRACT,SWCAN,
     + LWCAN,TDIFFC,DIFKL,HEATC,ETLYR,DETLYR,DHEATC,DTLDTC,     
     + WT,WDT,DT,DCHAR,DRYCAN,CANLAI,TOTLAI,NPLANT,ITYPE,IEVAP,RSTOM0,
     + RSTEXP,PLEAF0,RLEAF0,TOTROT,RLEAF,RROOT,ROOTDN,
     + CANMA,CANMB,WINDC,ZC,ATRANS,ISTRESS)
C
C**** DETERMINE THE EDDY CONDUCTANCE TERM BETWEEN NODES
c rma/gf
      IF (ITER .EQ. 1)
     +     CALL CANTK (NC,CONT,TC,ZC,ZMSUB,ZHSUB,ZERSUB,STABLE,WINDC)
      CALL CANTK (NC,CONDT,TCDT,ZC,ZMSUB,ZHSUB,ZERSUB,STABLE,WINDC)
C
C**** CALCULATE THE AVERAGE CONDUCTANCE TERM OVER THE TIME STEP
      CALL WEIGHT (NC,CON,CONT,CONDT,WT,WDT)
C
C**** CONVERT THERMAL EDDY CONDUCTANCE TO VAPOR CONDUCTANCE
C     AND CALCULATE LIQUID FLUX BETWEEN LAYERS
      DO 5 I=1,NC
         CON(I)=CON(I)/RHOA/CA
         IF (ISTRESS.EQ.3) THEN
         QVC(I)=CON(I)*(WDT*(VAPCDT(I)-VAPCDT(I+1))
     >                 + WT*(VAPC(I)-VAPC(I+1)))
         ELSE
         QVC(I)=CON(I)*(VAPCDT(I)-VAPCDT(I+1))
         ENDIF
    5 CONTINUE
c
c gnf check if soil can supply water for evaporation
c      if (-qvc(nc).gt.evapmx*rhol) then
CnT
c      if (nr .eq. 0) then
c         qvc(nc)=-evapmx*rhol
c         con(nc)=0.0d0
c      end if         
c      end if
      if (istress.ne.3) then
      if (nr.eq.0) then
             qvc(nc)=-evapmx*rhol
             con(nc)=0.0
      endif
      else
C             qvc(nc)=qvc(nc)*(1.0d0-pplastic)   !LIWANG 2017
C             con(nc)=con(nc)*(1.0d0-pplastic)
      endif
C
C**** DETERMINE THE MATRIX COEFFICIENTS FOR THE TOP LAYER
C
       IF (ISTRESS.EQ.3) THEN
       A2(N+1)=WDT*CON(1)
       B2(N)=B2(N) - WDT*(CON(1) + DETLYR(1)) - (ZC(2)-ZC(1))/2/DT
       C2(N)=WDT*CON(1)
       D2(N)=D2(N)-QVC(1)+ETLYR(1)-(ZC(2)-ZC(1))
     >      *(VAPCDT(1)-VAPC(1))/2/DT
       ELSE
       A2(N+1)=CON(1)
       B2(N)=B2(N) - (CON(1) + DETLYR(1))
       C2(N)=CON(1)
       D2(N)=D2(N)-QVC(1)+ETLYR(1)
       ENDIF        
C
C**** DETERMINE THE COEFFICIENTS FOR THE REST OF THE LAYERS
      DO 10 I=N+1,N+NC-1
         J=I-N+1
         IF (ISTRESS.EQ.3) THEN
         A2(I+1)=WDT*CON(J)
         B2(I)=-WDT*(CON(J-1) + CON(J) + DETLYR(J))
     >         - (ZC(J+1)-ZC(J-1))/2.d0/DT
         C2(I)=WDT*CON(J)
         D2(I)=QVC(J-1) - QVC(J) + ETLYR(J)
     >        - (ZC(J+1)-ZC(J-1))*(VAPCDT(J)-VAPC(J))/2.d0/DT
         ELSE
         A2(I+1)=CON(J)
         B2(I)=-(CON(J-1) + CON(J) + DETLYR(J))
         C2(I)=CON(J)
         D2(I)=QVC(J-1) - QVC(J) + ETLYR(J)
         ENDIF
   10 CONTINUE
      N=N+NC
C
C**** DETERMINE THE COEFFICIENTS FOR THE TOP LAYER OF THE NEXT MATERIAL
C
      IF (NSP.GT.0 .OR. NR.EQ.0) THEN
C****    NEXT MATERIAL IS SNOW OR SOIL
C        -- NO WATER BALANCE MATRIX FOR SNOW OR SOIL
         A2(N) = 0.0d0
         B2(N) = 0.0d0
         C2(N-1)=0.0d0
         D2(N) = 0.0d0
        ELSE
C
C****    NEXT MATERIAL IS RESIDUE
         IF (ISTRESS.EQ.3) THEN
           B2(N) = -WDT*CON(NC)
         ELSE
           B2(N) = -CON(NC)
         ENDIF
           D2(N) = QVC(NC)
      END IF
C
      RETURN
      END

C
      SUBROUTINE LWRBAL(NC,NSP,NR,NPLANT,TA,TLC,TLCDT,TLCLWR,TSP,TSPDT,
     + TR,TRDT,TS,TSDT,CLOUDS,LWCAN,LWSNOW,LWRES,LWSOIL,
     + TDIFFU,TDIFFC,DIFKL,FDDU,WDT,VAPA)
C
C     THIS SUBROUTINE CALLS SUBROUTINES TO SET UP THE LONG-WAVE
C     RADIATION BALANCE FOR SYSTEM.
C
C***********************************************************************
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (MXNOD=300, NODRES=10, nodcan=11, MXSPEC=10, NODSNO=100)
      common /shawp/ pplastic,emitc,emitr(10),emitsp,emits,emitf
     &      ,palbedo,ptransm
C
      DOUBLE PRECISION TLC(MXSPEC,NODCAN),TLCDT(MXSPEC,NODCAN),
     +  TSP(NODSNO),
     +  TSPDT(NODSNO),TR(NODRES),TRDT(NODRES),TS(MXNOD),TSDT(MXNOD),
     +  LWCAN(MXSPEC,NODCAN),LWSNOW,LWRES(NODRES),LWSOIL,TDIFFU(NODRES),
     +  TDIFFC(NODCAN),DIFKL(MXSPEC+1,NODCAN),FDDU(MXSPEC),
     +  TLCLWR(MXSPEC,NODCAN)
C
      PARAMETER (STEFAN=5.6697D-08)
C
C**** DETERMINE THE LONGWAVE RADIATION FLUX ABOVE EACH NODE.  START WITH
C     ATMOSPHERE AND WORK DOWNWARDS.
C
      CALL LWRATM (ABOVE,CLOUDS,TA,VAPA)
C
C
      IF (NSP .GT. 0) THEN
C        SURFACE IS SNOW
         TK = TSP(1)+273.16
         EMITSFC=EMITSP*STEFAN *(TK**4 + 4.*WDT*(TK**3)
     >                          *(TSPDT(1)-TSP(1)))
        ELSE 
C        SURFACE IS SOIL
         TK=TS(1) + 273.16                                                        
         EMITSFC=EMITS*STEFAN*((TK**4)+4.*WDT*(TK**3)*(TSDT(1)-TS(1)))
      END IF
C
       IF (NC .GT. 0) THEN
C        RADIATION BALANCE FOR CANOPY NODES
   20    DO 25 I=1,NC
           DO 24 J=1,NPLANT+1
              tlclwr(j,i) = tlcdt(j,i)
   24      CONTINUE
   25    CONTINUE
         IF (NSP.EQ.0 .AND. NR.GT.0) THEN
C           RESIDUE LIES BENEATH CANOPY - DETERMINE LONGWAVE RADIATION 
C           BALANCE THROUGH CANOPY AND RESIDUE TOGETHER
            CALL LWRCAN (LWCAN,LWRES,NPLANT,NC,NSP,NR,TLC,TLCDT,TR,TRDT,
     +                ABOVE,EMITSFC,TDIFFU,TDIFFC,DIFKL,FDDU,WDT)
           ELSE
C           CALCULATE RADIATION THROUGH CANOPY DOWN TO SNOW OR SOIL
            CALL LWRCAN (LWCAN,LWRES,NPLANT,NC,NSP,0,TLC,TLCDT,TR,TRDT,
     +                ABOVE,EMITSFC,TDIFFU,TDIFFC,DIFKL,FDDU,WDT)
         END IF
           
      END IF
C
      IF (NSP .GT. 0) THEN
C        RADIATION BALANCE FOR SNOW PACK
         LWSNOW = EMITSP*ABOVE - EMITSFC
C        NO LWR BENEATH THE SNOW
         LWSOIL = 0.0d0
         DO 35 I=1,NR
            LWRES(I) = 0.0d0
   35    CONTINUE
        ELSE
C
C        RADIATION BALANCE OF RESIDUE AND SOIL
         IF (NR .GT. 0) THEN
C           CHECK IF ALREADY COMPUTED WITH CANOPY RADIATION BALANCE
            IF (NC.EQ.0) THEN
C              RADIATION BALANCE FOR THE RESIDUE LAYERS WITH NO CANOPY
               CALL LWRCAN (LWCAN,LWRES,NPLANT,NC,NSP,NR,TLC,TLCDT,
     +               TR,TRDT,ABOVE,EMITSFC,TDIFFU,TDIFFC,DIFKL,FDDU,WDT)
            END IF
           ELSE
C           BARE SOIL; ADJUST LONG-WAVE ABOVE SOIL FOR PLASTIC FILM
C            ABOVE = (1.d0-PPLASTIC)*ABOVE 
C     +              + PPLASTIC*(EMITF*ABOVE + (1.d0-EMITF)*EMITSFC)   !NOT NEEDED ANY MORE? LIWANG 2017
         END IF
C        RADIATION BALANCE OF SOIL
         LWSOIL = EMITS*ABOVE - EMITSFC
      END IF
C
      RETURN
      END
C
C***********************************************************************

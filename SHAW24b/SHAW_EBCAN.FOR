C***********************************************************************
C
      SUBROUTINE EBCAN(N,NC,NR,NSP,NS,TC,TCDT,TLC,TLCDT,TLCLWR,
     +  VAPC,VAPCDT,WCAN,WCANDT,PCAN,PCANDT,MAT,MATDT,SWCAN,LWCAN,
     +  TDIFFC,DIFKL,ITER,WT,WDT,DT,DCHAR,DRYCAN,
     +  CANLAI,TOTLAI,ZC,CANMA,CANMB,ITYPE,IEVAP,NPLANT,TRNSP,TPOTNL,
     +  XTRACT,ZMSUB,ZHSUB,ZERSUB,STABLE,WINDC,RSTOM0,RSTEXP,PLEAF0,
     +  RLEAF0,TOTROT,RLEAF,RROOT,ROOTDN,ATRANS,evapmx,A1,B1,C1,D1,
     +  istress,TLCSHAW,TLEAFU,TLEAFL,HOUR)
C
C     THIS SUBROUTINE CALCULATES THE JACOBIAN MATRIX COEFFICIENTS FOR
C     THE CANOPY PORTION OF THE NEWTON-RAPHSON SOLUTION OF THE ENERGY
C     BALANCE
C
C***********************************************************************
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (MXNOD=300, NODRES=10, nodcan=11,
     +  MXSPEC=10, NODSNO=100, NODTOT=MXNOD+NODCAN+NODRES+NODSNO)
C
      DOUBLE PRECISION DCHAR(MXSPEC,NODCAN),DRYCAN(MXSPEC,NODCAN),
     +  CANLAI(MXSPEC,NODCAN),TOTLAI(MXSPEC),ZC(NODCAN),TRNSP(MXSPEC),
     +  XTRACT(MXNOD),WINDC(NODCAN),RSTOM0(MXSPEC),RSTEXP(MXSPEC),
     +  PLEAF0(MXSPEC),RLEAF0(MXSPEC),TOTROT(MXSPEC),
     +  RLEAF(MXSPEC,NODCAN),RROOT(MXSPEC,MXNOD),ROOTDN(MXSPEC,MXNOD),
     +  TPOTNL(MXSPEC),A1(NODTOT),B1(NODTOT),C1(NODTOT),D1(NODTOT)
C     STATE VARIABLES OF THE CANOPY
      DOUBLE PRECISION TC(NODCAN),TCDT(NODCAN),TLC(MXSPEC,NODCAN),
     +  TLCDT(MXSPEC,NODCAN),TLCLWR(MXSPEC,NODCAN),VAPC(NODCAN),
     +  VAPCDT(NODCAN),WCAN(NODCAN),WCANDT(NODCAN),PCAN(MXSPEC),
     +  PCANDT(MXSPEC),MAT(MXNOD),MATDT(MXNOD),SWCAN(MXSPEC,NODCAN),
     +  LWCAN(MXSPEC,NODCAN),TDIFFC(NODCAN),DIFKL(MXSPEC+1,NODCAN)
C     LOCAL VALIABLES
      DOUBLE PRECISION CON(NODCAN),CONT(NODCAN),
     +  CONDT(NODCAN),HEATC(NODCAN),LS,LV,
     +  ETLYR(NODCAN),DETLYR(NODCAN),DHEATC(NODCAN),DTLDTC(NODCAN)
c    +  CONDT(NODCAN),HEATC(NODCAN),ETLAY(NODCAN),RETLAY(NODCAN),LS,LV
      INTEGER ITYPE(MXSPEC),IEVAP(MXSPEC),HOUR
C
      PARAMETER(LV=2.5D06,LS=2.835D06,RHOL=1.0D3,RHOA=1.25D0,CA=1006.D0)
      common /shawp/ pplastic,emitc,emitr(10),emitsp,emits,emitf
     &      ,palbedo,ptransm
C
C**** CALCULATE LEAF TEMPERATURE AND HEAT TRANSFER FROM CANOPY
      CALL LEAFT (NC,NS,ITER,TC,TCDT,TLC,TLCDT,TLCLWR,VAPC,VAPCDT,
     + WCAN,WCANDT,PCAN,PCANDT,MAT,MATDT,TRNSP,TPOTNL,XTRACT,SWCAN,
     + LWCAN,TDIFFC,DIFKL,HEATC,ETLYR,DETLYR,DHEATC,DTLDTC,
     + WT,WDT,DT,DCHAR,DRYCAN,CANLAI,TOTLAI,NPLANT,ITYPE,IEVAP,RSTOM0,
     + RSTEXP,PLEAF0,RLEAF0,TOTROT,RLEAF,RROOT,ROOTDN,
     + CANMA,CANMB,WINDC,ZC,ATRANS,ISTRESS,TLCSHAW,TLEAFU,TLEAFL,HOUR)
C
C**** DETERMINE THE EDDY CONDUCTANCE TERM BETWEEN NODES
      IF (ITER .EQ. 1)
     +     CALL CANTK (NC,CONT,TC,ZC,ZMSUB,ZHSUB,ZERSUB,STABLE,WINDC)
      CALL CANTK (NC,CONDT,TCDT,ZC,ZMSUB,ZHSUB,ZERSUB,STABLE,WINDC)
C
C**** CALCULATE THE AVERAGE CONDUCTANCE TERM OVER THE TIME STEP
      CALL WEIGHT (NC,CON,CONT,CONDT,WT,WDT)
C
C**** DETERMINE THE MATRIX COEFFICIENTS FOR THE TOP LAYER
C
      A1(N+1)=A1(N+1) + WDT*CON(1)
      B1(N)=B1(N) - WDT*(CON(1)+DHEATC(1)*(1.d0-DTLDTC(1)))
     >            - (ZC(2)-ZC(1))/(2.d0*DT)*RHOA*CA
      C1(N)=C1(N) + WDT*CON(1)
      D1(N)=D1(N) - CON(1)*(WT*(TC(1)-TC(2))+WDT*(TCDT(1)-TCDT(2)))
     >    + HEATC(1) -(ZC(2)-ZC(1))/(2.d0*DT)*RHOA*CA*(TCDT(1)-TC(1))
C
C**** DETERMINE THE COEFFICIENTS FOR THE REST OF THE LAYERS
      DO 10 I=N+1,N+NC-1
         J=I-N+1
         A1(I+1)=A1(I+1) + WDT*CON(J)
         B1(I)=B1(I) -WDT*(CON(J-1) +CON(J) +DHEATC(J)*(1.d0-DTLDTC(J)))
     >               - (ZC(J+1)-ZC(J-1))/(2.d0*DT)*RHOA*CA
         C1(I)=C1(I) + WDT*CON(J)
         D1(I)=CON(J-1)*(WDT*(TCDT(J-1)-TCDT(J)) + WT*(TC(J-1)-TC(J)))
     >        -CON(J)*(WDT*(TCDT(J)-TCDT(J+1)) + WT*(TC(J)-TC(J+1)))
     >        -(ZC(J+1)-ZC(J-1))/(2.d0*DT)*RHOA*CA*(TCDT(J)-TC(J))
     >        +HEATC(J)
   10 CONTINUE
      N=N+NC
C
C**** DETERMINE THE COEFFICIENTS FOR THE TOP LAYER OF THE NEXT MATERIAL
      IF (NSP.GT.0 .OR. NR.LE.0) THEN
C        NEXT MATERIAL IS SNOW OR SOIL -- NEED TO CONSIDER LATENT HEAT
C        TRANSFER TO THE NEXT NODE.
C        DETERMINE THE CONVECTIVE VAPOR TRANSPORT FROM EDDY CONDUCTANCE
         CONV=CON(NC)/RHOA/CA
C        CALCULATE THE VAPOR TRANSFER BETWEEN NODES
         QVCAN=CONV*(WDT*(VAPCDT(NC)-VAPCDT(NC+1))
     >                 + WT*(VAPC(NC)-VAPC(NC+1)))  !*(1.0d0-pplastic)   !Liwang to account for plastic cover, should conv be modified too? Per Flerchinger
C        CALCULATE HUMIDITY AND SLOPE OF SAT. VAPOR CURVE
         CALL VSLOPE (SLPNC,SATV,TC(NC))
         HUMNC=VAPCDT(NC)
         CALL VSLOPE (SLPNC1,SATV,TC(NC+1))
         HUMNC1=VAPCDT(NC+1)/SATV
      END IF
C
         IF (NSP .GT. 0) THEN
C****       NEXT MATERIAL IS SNOW
            A1(N) = A1(N) + WDT*CONV*LS*SLPNC*HUMNC
            B1(N) = B1(N) - WDT*(CON(NC) + CONV*LS*SLPNC1*HUMNC1)
         D1(N) =CON(NC)*(WT*(TC(NC)-TC(NC+1))+WDT*(TCDT(NC)-TCDT(NC+1)))
     >          + LS*QVCAN
        ELSE
C
      IF (NR.GT.0) THEN
C****    NEXT MATERIAL IS RESIDUE
         B1(N) = B1(N) - WDT*CON(NC)
         D1(N) =CON(NC)*(WT*(TC(NC)-TC(NC+1))+WDT*(TCDT(NC)-TCDT(NC+1)))
          ELSE
C
C****       NEXT MATERIAL IS SOIL
c gnf       check if soil can supply water for evaporation
c            if (-qvcan.gt.evapmx*rhol) then
             if (istress.lt.3) then   !ISTRESS=0
               qvcan=-evapmx*rhol
               conv=0.0d0
               else
               qvcan=qvcan  !*(1.0d0-pplastic)
               conv=conv    !*(1.0d0-pplastic)
               endif
c            end if
            A1(N) = A1(N) + WDT*CONV*LV*SLPNC*HUMNC
            B1(N) = B1(N) - WDT*(CON(NC) + CONV*LV*SLPNC1*HUMNC1)
      D1(N) = CON(NC)*(WT*(TC(NC)-TC(NC+1)) + WDT*(TCDT(NC)-TCDT(NC+1)))
     >       + LV*QVCAN
C
      END IF
      END IF
C
      RETURN
      END
C

C
      SUBROUTINE CLOUDY (CLOUDS,ALATUD,DECLIN,HAFDAY,SUNHOR,JULIAN,
     >  NHRPDT)
C
C     THIS SUBROUTINE TOTALS THE SOLAR RADIATION FOR THE DAY.  IT THEN
C     ESTIMATES THE CLOUD COVER, WHICH WILL BE USED IN THE CALCULATION
C     FOR THE EMMISSIVITY OF THE ATMOSPHERE.  (DECLINATION AND HALF-DAY
C     LENGTH FOR THE CURRENT JULIAN DAY ARE ALSO CALCULATED.)
C
C***********************************************************************
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER (SOLCON=1360.D0, DIFATM=0.76D0)
C
      INTEGER THOUR,HOUR
      DIMENSION SUNHOR(24)
C
      TOTSUN=0.0
      DO 10, I=NHRPDT,24
         IF (SUNHOR(I) .LT. 0.0) SUNHOR(I)=0.0
         TOTSUN=TOTSUN + SUNHOR(I)
   10 CONTINUE
      DECLIN=0.4102D0*SIN(2*3.14159D0*(JULIAN-80)/365.0D0)
      COSHAF=-TAN(ALATUD)*TAN(DECLIN)
      IF (ABS(COSHAF) .GE. 1.0) THEN
         IF (COSHAF .GE. 1.0) THEN
C           SUN DOES NOT COME UP ON THIS DAY (WINTER IN ARCTIC CIRCLE)
            HAFDAY=0.0D0
          ELSE
C           SUN DOES NOT SET ON THIS DAY (SUMMER IN THE ARCTIC CIRCLE)
            HAFDAY=3.14159D0
         END IF
       ELSE
         HAFDAY=ACOS(COSHAF)
      END IF
      SUNMAX=24.*SOLCON*(HAFDAY*SIN(ALATUD)*SIN(DECLIN)
     >                  +COS(ALATUD)*COS(DECLIN)*SIN(HAFDAY))/3.14159D0
      TTOTAL=TOTSUN/SUNMAX
C     USE CLOUD EQUATION BASED FLERCHINGER & YU (2007), AG & FOREST MET
      CLOUDS=1.333 - 1.666*TTOTAL
c      CLOUDS=2.4d0 - 4.0d0*TTOTAL    !old one
      IF (CLOUDS .GT. 1.0) CLOUDS=1.0
      IF (CLOUDS .LT. 0.0) CLOUDS=0.0
C
      RETURN
      END
C***********************************************************************

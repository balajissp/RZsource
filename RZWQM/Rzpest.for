C $Id: Rzpest.for,v 1.1 2002/08/28 00:00:48 rojas Exp $
C
      SUBROUTINE PEMAIN(IP,COPLNT,CORES,CC,NN,T,THETA,TH3,ZN,BD,FPW,
     +    SLKS,TL)
C
C======================================================================
C
C       PURPOSE: THIS IS THE MAIN DRIVER ROUTINE FOR DISSIPATION OF A
C              PESTICIDE IN ALL POOLS.
C
C       REF:    R. DON WAUCHOPE
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       CC         I  SOLUTION CONCENTRATION IN THE SOIL (MG/L)
C       COPLNT    I/O INITIAL CONCENTRATION ON PLANT (UG/CM^2)
C       CORES I/0 INITIAL CONCENTRATION ON CROP RESIDUE (UG/CM^2)
C       DYIELD     C  DAUGHTER FORMATION PERCENTAGE (0..1)
C       EA         C  AEROBIC ARRHENIUS EQUATION ACTIVATION ENERGY (KJ/MOL)
C       EN         C  ANEROBIC ARRHENIUS EQUATION ACTIVATION ENERGY (KJ/MOL)
C       FIRST  L  LOGICAL FLAG TO SIGNAL FIRST INITIALIZATION.
C       FPW        I  FRACTION WATER FILLED PORE SPACE [0..100]
C       HALFBD     C  IRREVERSIBLE BINDING HALFLIFE (DAYS)
C       HALFF  C  FOLIAR LUMPED HALFLIFE (DAYS)
C       HALFFB     C  FOLIAR ABIOTIC HALFLIFE (DAYS)
C       HALFFP     C  FOLIAR PHOTODEGRADATION HALFLIFE (DAYS)
C       HALFR  C  RESIDUE LAYER LUMPED HALFLIFE (DAYS)
C       HALFRB     C  RESIDUE LAYER ABIOTIC HALFLIFE (DAYS)
C       HALFRP     C  RESIDUE LAYER PHOTODEGRADATION HALFLIFE (DAYS)
C       HALFSA     C  SOIL AEROBIC BIODEGRADATION HALFLIFE (DAYS)
C       HALFSB     C  SOIL ABIOTIC HALFLIFE (DAYS)
C       HALFSN     C  SOIL ANEROBIC BIODEGRADATION HALFLIFE (DAYS)
C       HALFSP     C  SOIL PHOTODEGRADATION HALFLIFE (DAYS)
C       HALFSV     C  SOIL VOLATILIZATION HALFLIFE (DAYS)
C       IC         L  NUMBER OF DAYS SINCE START OF DEGRADATION USED FOR
C                 MOVING AVERAGE CALCULATIONS
C       ID1        L  INDEX TO DAUGHTER #1 CHEMICAL
C       ID2        L  INDEX TO DAUGHTER #2 CHEMICAL
C       IGD        L  INDEX TO GRANDDAUGHTER CHEMICAL
C       IP         I  INDEX TO CURRENT PESTICIDE
C       IPANC  C  ANCESTORY CODE (SEE DOCUMENTATION)
C       IPCODE     C  DAUGHTER FORMATION DEGRADATION PROCESS INDEX,
C                 TELLS WHICH PROCESS IS RESPONSIBLE FOR DAUGHTER
C       IPDEP  C  FLAG FOR DEPTH ADJUSTMENT, ON IF = 1.
C       IPP        L  INDEX TO PARENT CHEMICAL
C       MXCHEM     P  MAXIMUM NUMBER OF CHEMICALS IN TRANSPORT MATRIX
C       MXNOD  P  MAXIMUM NUMBER OF NUMERICAL NODES
C       MXPEST     P  MAXIMUM NUMBER OF PESTICIDE TO SIMULATE
C       NN         I  NUMBER OF ACTUAL NUMERICAL NODES
C       NPEST  C  ACTUAL NUMBER OF PESTICIDES TO SIMULATE
C       ORGP   L  INITIAL PEST MASS ON FOLIAGE
C       ORGR   L  INITIAL PEST MASS ON RESIDUE
C       ORGSL  L  INITIAL PEST MASS IN SOIL LAYERS
C       RFT        L  ADJUSTED REFERENCE TEMPERATURE
C       RFTHETA    L  ADJUSTED REFERENCE THETA
C       T      I  SOIL TEMPERATURE IN LAYERS (C)
C       T3F        L  TEMP ARRAY FOR CURRENT PEST- 3 DAY MOVING AVE.
C       T3FWF  L  ARRAY FOR 3 DAY MOVING AVERAGE
C       T6F        L  TEMP ARRAY FOR CURRENT PEST- 6 DAY MOVING AVE.
C       T6FWF  L  ARRAY FOR 6 DAY MOVING AVERAGE
C       TH3        I
C       THETA  I  SOIL VOLUMETRIC WATER CONTENT (CC/CC)
C       THETAR     C  REFERENCE SOIL VOLUMETRIC WATER CONTENT (CC/CC)
C       TR         C  REFERENCE SOIL TEMPERATURE (C)
C       VMAX   C  MAXIMUM DEPTH ADJUSTMENT TO RATE CONSTANTS
C       WALKERB    C  WALKER SOIL MOISTURE ADJUSTMENT CONSTANT
C       XKBIND     L  RATE CONSTANT FOR IRREVERSIBLE BINDING
C       XKFAB  L  RATE CONSTANT FOR FOLIAR ABIOTIC
C       XKFL   L  RATE CONSTANT FOR FOLIAR LUMPED
C       XKFP   L  RATE CONSTANT FOR FOLIAR PHOTODEGRADATION
C       XKH        C  HENRY'S LAW CONSTANT
C       XKOC   C  SOIL ORGANIC SORPTION CONSTANT
C       XKRAB  L  RATE CONSTANT FOR RESIDUE LAYER ABIOTIC
C       XKRL   L  RATE CONSTANT FOR RESIDUE LAYER LUMPED
C       XKRP   L  RATE CONSTANT FOR RESIDUE LAYER PHOTODEGRADATION
C       XKS1AB     L  RATE CONSTANT FOR SOIL SURFACE ABIOTIC
C       XKS1AE     L  RATE CONSTANT FOR SOIL SURFACE AEROBIC
C       XKS1P  L  RATE CONSTANT FOR SOIL SURFACE PHOTODEGRADATION
C       XKS1V  L  RATE CONSTANT FOR SOIL SURFCE VOLATIZATION
C       XKSNAB     L  RATE CONSTANT FOR SOIL LAYER ABIOTIC
C       XKSNAE     L  RATE CONSTANT FOR SOIL LAYER AEROBIC
C       XKSNAN     L  RATE CONSTANT FOR SOIL LAYER ANEROBIC
C       XMW        C  MOLECULAR WEIGHT OF THE PESTICIDE (G/MOLE)
C       ZERO   P  = 0.0
C       ZN         I  DEPTH OF SOIL LAYERS (CM)
C
C       COMMENTS:  THIS ROUTINE IS CALLED ONCE FOR EVERY PESTICIDE
C
C       EXTERNAL REFERENCES:
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   1.1.27.2004
C-----------------------------------------------------------------------
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER(ZERO=0.0D0)
      PARAMETER(MXNOD=300,MXCHEM=15,MXPEST=3)
C     PARAMETER (NT3F = MXNOD*MXPEST*3, NT6F = MXNOD*MXPEST*6)
C
      COMMON /PINFO/ TSOTP0(MXPEST),TAPAP(MXPEST),TLPRS(MXPEST),
     +    TLPCN(MXPEST),TLPSL(MXPEST),TLPRO(MXPEST),TLPLC(MXPEST),
     +    TDPPLNT(MXPEST),TLPDT(MXPEST),TLPDTD(MXPEST),
     +    TLPDTL(MXPEST),TLPDMA(MXPEST)
C
      COMMON /RZPEST/ HALFF(MXPEST),HALFFP(MXPEST),HALFFB(MXPEST),
     +    HALFR(MXPEST),HALFRP(MXPEST),HALFRB(MXPEST),HALFSSA(MXPEST),
     +    HALFSSV(MXPEST),HALFSSP(MXPEST),HALFSSB(MXPEST),
     +    HALFSA(MXPEST),HALFSN(MXPEST),HALFSB(MXPEST),HALFBD(MXPEST),
     +    XKH(MXPEST),XKOC(MXPEST),XMW(MXPEST),THETAR(MXPEST),
     +    TMPR(MXPEST),EA(MXPEST),EN(MXPEST),WALKERB(MXPEST),
     +    VMAX(MXPEST),DYIELD(4,MXPEST),FREUND(MXPEST),XPKA(MXPEST),
     +    XPKB(MXPEST),REFPH(MXPEST),CATKOC(MXPEST),ANKOC(MXPEST),
     +    PCOEFF(MXPEST),PPOWER(MXPEST),RCOEFF(MXPEST),RPOWER(MXPEST),
     +    XKOW(MXPEST),PBIND(MXPEST),IPCODE(4,MXPEST),IPANC(MXPEST),
     +    IPDEP(MXPEST),IPACTV(MXPEST),NPEST
C
      DIMENSION CC(MXNOD,MXCHEM),COPLNT(MXPEST),CORES(MXPEST),
     +    THETA(NN),T(NN),TH3(NN),ZN(NN),TL(MXNOD),
C     +    T3FWF(3,MXPEST,MXNOD), T6FWF(6,MXPEST,MXNOD), T3F(3), T6F(6),
     +    BD(NN),FPW(NN),SLKS(MXNOD,MXPEST,2)
C
C     .. FIND REFERENCE TEMPERATURE
      IF(TMPR(IP).EQ.0.0D0) THEN
        RFT=25.0D0
      ELSE
        RFT=TMPR(IP)
      ENDIF
C
C     ..CALCULATE DISSIPATION FROM PLANT FOLIAGE
      CONVD=0.1D0
      CONVP=0.1D0
      IF(COPLNT(IP).NE.0.0D0) THEN
C
C       ..DETERMINE THE CORRECTED K VALUES FOR PLANTS
        CALL KSBIO(XKFP,XKFL,XKFAB,HALFFP(IP),HALFF(IP),HALFFB(IP))
C
C           .. DETERMINE METABOLITE FORMATION
        DO 10 ID=1,NPEST
          IF(IPANC(ID).EQ.IP) THEN
            IF(IPCODE(1,ID).GE.1.AND.IPCODE(1,ID).LE.3) THEN
              CALL DAUG(XKFL,XKFP,XKFAB,ZERO,COPLNT(IP),COPLNT(ID),
     +        DYIELD(1,ID),IPCODE(1,ID),XMW(IP),XMW(ID),TAPAP(ID),CONVP,
     +            CONVD)
            ENDIF
          ENDIF
   10   CONTINUE
C
C       ..CALCULATE plant canopy DEGRADATION
        COPLNT(IP)=COPLNT(IP)*EXP(-(XKFP+XKFL+XKFAB))
      ENDIF
C
C     ..CALCULATE DISSIPATION FROM RESIDUE
      IF(CORES(IP).NE.0.0D0) THEN
C
C       ..DETERMINE THE CORRECTED K VALUES FOR RESIDUE COVER
        CALL KSBIO(XKRP,XKRL,XKRAB,HALFRP(IP),HALFR(IP),HALFRB(IP))
C
C       .. DETERMINE METABOLITE FORMATION
        DO 20 ID=1,NPEST
          IF(IPANC(ID).EQ.IP) THEN
            IF(IPCODE(2,ID).GE.4.AND.IPCODE(2,ID).LE.6) THEN
              CALL DAUG(XKRL,XKRP,XKRAB,ZERO,CORES(IP),CORES(ID),
     +          DYIELD(2,ID),IPCODE(2,ID)-3,XMW(IP),XMW(ID),TAPAP(ID),
     +          CONVP,CONVD)
            ENDIF
          ENDIF
   20   CONTINUE
C
C       ..CALCULATE mulch layer DEGRADATION
        CORES(IP)=CORES(IP)*EXP(-(XKRP+XKRL+XKRAB))
      ENDIF
C
C     ..CALCULATE DISSIPATION FROM SOIL SURFACE
      IF(CC(1,IP+12).gt.0.0D0) THEN
        IF(THETAR(IP).EQ.0.0D0) THEN
          RFTHETA=TH3(1)
        ELSE
          RFTHETA=THETAR(IP)
        ENDIF
        FWFPS=FPW(1)*1.0D-2     !CONVERT TO FRACTION FROM %
C
C       ..DETERMINE THE CORRECTED K VALUES FOR SOIL SURFACE DISSIPATION
        CALL KSSURF(XKS1P,XKS1V,XKS1AB,XKS1AE,HALFSSP(IP),HALFSSV(IP),
     +      HALFSSB(IP),HALFSSA(IP),T(1),THETA(1),RFTHETA,RFT,XMW(IP),
     +      XKH(IP),XKOC(IP),EA(IP),WALKERB(IP),FWFPS)
C
C       .. DETERMINE METABOLITE FORMATION
        DO 30 ID=1,NPEST
          IF(IPANC(ID).EQ.IP) THEN
            IF(IPCODE(3,ID).GE.7.AND.IPCODE(3,ID).LE.10) THEN
C LIWANG MA, 7-3-2006
              CONVP=(THETA(1)+FSLKS(SLKS(1,IP,1),CC(1,IP+12),FREUND(IP),
     +            theta(1),bd(1))*BD(1))*0.1D0*TL(1)
              CONVD=(THETA(1)+FSLKS(SLKS(1,ID,1),CC(1,ID+12),FREUND(ID),
     +            theta(1),bd(1))*BD(1))*0.1D0*TL(1)
c              CONVD=(THETA(1)+SLKS(1,ID,1)*BD(1))*0.1D0*TL(1)
              CALL DAUG(XKS1AE,XKS1V,XKS1P,XKS1AB,CC(1,IP+12),
     +          CC(1,ID+12),DYIELD(3,ID),IPCODE(3,ID)-6,XMW(IP),XMW(ID),
     +            TAPAP(ID),CONVP,CONVD)
            ENDIF
          ENDIF
   30   CONTINUE
C
C       ..CALCULATE DEGRADATION
        CC(1,IP+12)=CC(1,IP+12)*EXP(-(XKS1P+XKS1V+XKS1AB+XKS1AE))
      ENDIF
CC
C     ..CALCULATE DISSIPATION FOR EACH SOIL LAYER
      DO 50 I=2,NN
        IF(CC(I,IP+12).gt.0.0D0) THEN
          FWFPS=FPW(I)*1.0D-2   !CONVERT TO FRACTION FROM %
          CALL FPMOVAV(FWFPS,TF3,TF6,IP,I)
C
          IF(THETAR(IP).EQ.0.0D0) THEN
            RFTHETA=TH3(I)
          ELSE
            RFTHETA=THETAR(IP)
          ENDIF
C
C         ..DETERMINE CORRECTED K VALUES FOR THE ROOT ZONE
          CALL KSSOIL(XKSNAE,XKSNAN,XKSNAB,HALFSA(IP),HALFSN(IP),
     +        HALFSB(IP),T(I),THETA(I),RFTHETA,RFT,EA(IP),EN(IP),
     +        WALKERB(IP),ZN(I),IPDEP(IP),TF3,TF6,VMAX(IP),FWFPS)
C
C         .. DETERMINE METABOLITE FORMATION
          DO 40 ID=1,NPEST
            IF(IPANC(ID).EQ.IP) THEN
              IF(IPCODE(4,ID).GE.10) THEN
                IF(IPCODE(4,ID).EQ.10) THEN
                  IPC=4  !THIS IS A LUMPED VALUE for both surface and subsurface, Liwang Ma, 3-5-2009
                ELSE
                  IPC=IPCODE(4,ID)-10       !REGULAR DIS CODE
                ENDIF
C LIWANG MA, 7-3-2006
              CONVP=(THETA(i)+FSLKS(SLKS(I,IP,1),CC(I,IP+12),FREUND(IP),
     +               theta(i),bd(i))*BD(I))*0.1D0*TL(I)
              CONVD=(THETA(i)+FSLKS(SLKS(I,ID,1),CC(I,ID+12),FREUND(ID),
     +               theta(i),bd(i))*BD(I))*0.1D0*TL(I)
c                CONVD=(THETA(I)+SLKS(I,ID,1)*BD(I))*0.1D0*TL(I)
                CALL DAUG(XKSNAE,XKSNAN,XKSNAB,XKS1AB,CC(I,IP+12),    !use ipcode=10 to make a uniform daughter product formation, Liwang Ma, 3-6-2009.
     +              CC(I,ID+12),DYIELD(4,ID),IPC,XMW(IP),XMW(ID),
     +              TAPAP(ID),CONVP,CONVD)
              ENDIF
            ENDIF
   40     CONTINUE
C
C         .. DEGRADE SOIL LAYER PESTICIDE
                IF(IPC.EQ.4) THEN
          CC(I,IP+12)=CC(I,IP+12)*EXP(-(XKSNAE+XKSNAN+XKSNAB+XKS1AB))
                else
          CC(I,IP+12)=CC(I,IP+12)*EXP(-(XKSNAE+XKSNAN+XKSNAB))
                endif
        ENDIF
   50 CONTINUE
C
      RETURN
      END
C
      SUBROUTINE KSBIO(XKP,XK,XKB,HALFP,HALF,HALFB)
C
C======================================================================
C
C       PURPOSE: THIS ROUTINE CALCULATES THE CORRECTED KS VALUES FOR
C              PLANT CANOPY AND RESIDUE LAYER.
C
C       REF: R. DON WAUCHOPE, TIFTON, GA, USA
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       HALF   I  LUMPED HALFLIFE (DAYS)
C       HALFB  I  ABIOTIC HALFLIFE (DAYS)
C       HALFP  I  PHOTODEGRADATION HALFLIFE (DAYS)
C       XK         I  LUMPED RATE CONSTANT
C       XKB        I  ABIOTIC RATE CONSTANT
C       XKP        I  PHOTODEGRADATION RATE CONSTANT
C
C       CALLED FROM:    PEMAIN
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   97
C
C-----------------------------------------------------------------------
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     ..FIND UNCORRECTED KS VALUES
      XK=0.0D0
      IF(HALF.GT.0.0D0) XK=LOG(2.0D0)/HALF
      XKP=0.0D0
      IF(HALFP.GT.0.0D0) XKP=LOG(2.0D0)/HALFP
      XKB=0.0D0
      IF(HALFB.GT.0.0D0) XKB=LOG(2.0D0)/HALFB
C
      RETURN
      END
C
      SUBROUTINE KSSURF(XKP,XKV,XKB,XKA,HALFSP,HALFSV,HALFSB,HALFSA,T,
     +    THETA,RFTHETA,RFT,XMW,XKH,XKOC,EA,WALKERB,FWFPS)
C
C======================================================================
C
C       PURPOSE: THIS ROUTINE CALCULATES THE CORRECTED KS VALUES FOR
C  *           SOIL SURFACE.
C
C       REF: R. DON WAUCHOPE, TIFTON, GA, USA
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       B      L  ACTUAL WALKER B USED DEFAULT=0.8
C       EA         I  AEROBIC ARRHENIUS EQUATION ACTIVATION ENERGY (KJ/MOL)
C       ENERGY     L  ACTUAL ACTIVATION ENERGY USED (KJ/MOL)
C       F1         L  ACTUAL ADJUSTMENT FOR TEMPERATURE
C       F2         L  ACTUAL ADJUSTMENT FOR LOW WATER CONTENTS
C       F4         L  ACTUAL ADJUSTMENT FOR HIGH WATER CONTENTS
C       FWFPS  L  FRACTION WATER FILLED PORE SPACE [0..1]
C       HALFSA     I  SOIL AEROBIC BIODEGRADATION HALFLIFE (DAYS)
C       HALFSB     I  SOIL ABIOTIC HALFLIFE (DAYS)
C       HALFSP     I  SOIL PHOTODEGRADATION HALFLIFE (DAYS)
C       HALFSV     I  SOIL ANEROBIC VOLATILIZATION HALFLIFE (DAYS)
C       HSA        L  ADJUSTED HALFLIFE
C       R      P  UNIVERSAL GAS CONSTANT
C       RFT        I  ADJUSTED REFERENCE TEMPERATURE (C)
C       RFTHETA    I  ADJUSTED REFERENCE THETA
C       T      I  ACTUAL SOIL TEMPERATURE (C)
C       THETA  I  ACTUAL VOLUMETRIC SOIL WATER CONTENT (CC/CC)
C       TK         P  CONVERSION FOR DEGREES C TO DEGRESS K.
C       WALKERB    I  WALKER SOIL MOISTURE ADJUSTMENT CONSTANT
C       XKA        I  RATE CONSTANT FOR AEROBIC
C       XKB        I  RATE CONSTANT FOR ABIOTIC
C       XKH        I  HENRY'S LAW CONSTANT
C       XKOC   I  SOIL ORGANIC SORPTION CONSTANT
C       XKP        I  RATE CONSTANT FOR PHOTODEGRADATION
C       XKV        I  RATE CONSTANT FOR VOLATIZATION
C       XMW        I  PESTICIDE MOLECULAR WEIGHT (G/MOLE)
C
C       CALLED FROM:  PEMAIN
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   97
C
C-----------------------------------------------------------------------
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER(TK=273.15D0,R=8.314D0)
C
C     ..VOLATIZATION K (DEFAULT)
      IF(HALFSV.GT.0.0D0) THEN
        XKV=LOG(2.0D0)/HALFSV
      ELSEIF(HALFSV.EQ.0.0D0) THEN
        XKV=105.7D0*(XKH/XKOC*SQRT(XMW))**0.453D0
C     XKV = 3.07D0 * (XKH / XKOC * SQRT(XMW))**0.453D0
C------------- THIS CONSTANT SHOULD BE 3.07?
      ELSE
        XKV=0.0D0
      ENDIF
C
C     .. PHOTOLYSIS K
      XKP=0.0D0
      IF(HALFSP.GT.0.0D0) XKP=LOG(2.0D0)/HALFSP
C
C     .. ABIOTIC K
      XKB=0.0D0
      IF(HALFSB.GT.0.0D0) XKB=LOG(2.0D0)/HALFSB
C
C     .. AEROBIC HALFLIFE
      XKA=0.0D0
      IF(HALFSA.GT.0.D00) THEN
C
C       .. ADJUST AEROBIC HALFLIFE FOR TEMPERATURE
        IF(EA.NE.0.0D0) THEN
          ENERGY=EA
        ELSE
          ENERGY=54.0D3
        ENDIF
        F1=EXP((ENERGY/R)*(1.0D0/(T+TK)-1.0D0/(RFT+TK)))
C
C       .. ADJUST AEROBIC HALFLIFE FOR LOW WATER CONTENTS
        IF(WALKERB.NE.0.0D0) THEN
          B=-WALKERB
        ELSE
          B=-0.8D0
        ENDIF
        F2=(THETA/RFTHETA)**B
C
C       .. ADJUST AEROBIC HALFLIFE FOR HIGH WATER CONTENTS
        IF(FWFPS.GT.0.6D0) THEN
          F4=2.5D0*(1.0D0-FWFPS)
        ELSE
          F4=1.0D0
        ENDIF
C
        HSA=HALFSA*F1*F2
        XKA=LOG(2.0D0)/HSA*F4
      ENDIF
C
      RETURN
      END
C
      SUBROUTINE KSSOIL(XKA,XKN,XKB,HALFSA,HALFSN,HALFSB,T,THETA,
     +    RFTHETA,RFT,EA,EN,WALKERB,ZN,IPDEP,TF3,TF6,VMAX,FWFPS)
C
C======================================================================
C
C       PURPOSE: THIS ROUTINE CALCULATES THE CORRECTED KS VALUES FOR
C  *           SOIL SURFACE.
C
C       REF: R. DON WAUCHOPE, TIFTON, GA, USA
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       B      L  ACTUAL WALKER B USED DEFAULT=0.8
C       EA         I  AEROBIC ARRHENIUS EQUATION ACTIVATION ENERGY (KJ/MOL)
C       EN         I  ANEROBIC ARRHENIUS EQUATION ACTIVATION ENERGY (KJ/MOL)
C       ENERGY     L  ACTUAL ACTIVATION ENERGY USED (KJ/MOL)
C----------------------------------------------------- (J/MOLE)
C       F1         L  ACTUAL ADJUSTMENT FOR TEMPERATURE
C       F2         L  ACTUAL ADJUSTMENT FOR LOW WATER CONTENTS
C       F3         L  ACTUAL ADJUSTMENT FOR DEPTH EFFECTS
C       F4         L  ACTUAL ADJUSTMENT FOR HIGH WATER CONTENTS
C       FWFPS  L  FRACTION WATER FILLED PORE SPACE [0..1]
C       HALFSA     I  SOIL AEROBIC BIODEGRADATION HALFLIFE (DAYS)
C       HALFSB     I  SOIL ABIOTIC HALFLIFE (DAYS)
C       HALFSN     I  SOIL ANEROBIC BIODEGRADATION HALFLIFE (DAYS)
C       HSA        L  ADJUSTED HALFLIFE
C       IC         I  NUMBER OF DAYS SINCE START OF DEGRADATION USED FOR
C                 MOVING AVERAGE CALCULATIONS
C       IPDEP  I  FLAG FOR DEPTH ADJUSTMENT, ON IF = 1.
C       R      P  UNIVERSAL GAS CONSTANT
C       RFT        I  ADJUSTED REFERENCE TEMPERATURE (C)
C       RFTHETA    I  ADJUSTED REFERENCE THETA
C       T      I  ACTUAL SOIL TEMPERATURE (C)
C       TF3        L  3 DAY MOVING AVE of fraction water filled pore spce
C       TF6        L  6 DAY MOVING AVE of fraction water filled pore spce
C       THETA  I  ACTUAL VOLUMETRIC SOIL WATER CONTENT (CC/CC)
C       TK         P  CONVERSION FOR DEGREES C TO DEGRESS K.
C       VMAX   I  MAXIMUM DEPTH ADJUSTMENT TO RATE CONSTANTS
C       WALKERB    I  WALKER SOIL MOISTURE ADJUSTMENT CONSTANT
C       XKA        I  RATE CONSTANT FOR AEROBIC
C       XKB        I  RATE CONSTANT FOR ABIOTIC
C       XKN        I  RATE CONSTANT FOR ANEROBIC
C       ZN         I  DEPTH IN SOIL LAYER (CM)
C
C       CALLED FROM:  PEMAIN
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   97
C
C-----------------------------------------------------------------------
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER(TK=273.15D0,R=8.314D0)
C
C     .. ABIOTIC K
      XKB=0.0D0
      IF(HALFSB.GT.0.0D0) XKB=LOG(2.0D0)/HALFSB
C
C     .. AEROBIC HALFLIFE
      XKA=0.0D0
      IF(HALFSA.GT.0.D00) THEN
C
C       .. ADJUST AEROBIC HALFLIFE FOR TEMPERATURE
        IF(EA.NE.0.0D0) THEN
          ENERGY=EA
        ELSE
          ENERGY=54.0D3
        ENDIF
        F1=EXP((ENERGY/R)*(1.0D0/(T+TK)-1.0D0/(RFT+TK)))
C
C       .. ADJUST AEROBIC HALFLIFE FOR LOW WATER WATER CONTENTS
        IF(WALKERB.NE.0.0D0) THEN
          B=-WALKERB
        ELSE
          B=-0.8D0
        ENDIF
        F2=(THETA/RFTHETA)**B
C
C       .. ADJUST AEROBIC HALFLIFE FOR DEPTH
        IF(IPDEP.EQ.1.AND.VMAX.GT.0.0D0) THEN
          IF(ZN.LT.25.0D0) THEN
            F3=1.0D0
          ELSEIF(ZN.GE.25.0D0.AND.ZN.LE.100.0D0) THEN
C           F3 = 1.0D0 + (VMAX-1.0D0) * (ZN-25.0D0) / 75.0D0
            F3=1.0D0+VMAX*(ZN-25.0D0)/75.0D0
C--------------------------------  VMAX?
          ELSE
            F3=1.0D0+VMAX
          ENDIF
        ELSE
          F3=1.0D0
        ENDIF
C
C       .. ADJUST AEROBIC HALFLIFE FOR HIGH WATER CONTENTS
        IF(FWFPS.GT.0.6D0) THEN
          F4=2.5D0*(1.0D0-FWFPS)
        ELSE
          F4=1.0D0
        ENDIF
C
        HSA=HALFSA*F1*F2*F3
        XKA=LOG(2.0D0)/HSA*F4
      ENDIF
C
C     .. ANEROBIC HALFLIFE
      XKN=0.0D0
      IF(HALFSN.GT.0.D00) THEN
        IF((TF3.GT.0.9D0.AND.ZN.LT.50.0D0).OR.(TF6.EQ.1.0D0.AND.ZN.GE.
     +      50.0D0)) THEN
          IF(EN.NE.0.0D0) THEN
            ENERGY=EN
          ELSE
            ENERGY=54.0D3
          ENDIF
          F1=EXP((ENERGY/R)*(1.0D0/(T+TK)-1.0D0/(RFT+TK)))
          XKN=LOG(2.0D0)/(HALFSN*F1)
          XKA=0.0D0
        ELSE
          XKN=0.0D0
        ENDIF
      ENDIF
C
      RETURN
      END
C
      SUBROUTINE DAUG(XK1,XK2,XK3,XK4,PARENT,DAUGH,DYIELD,IPCODE,PARMW,
     +    DAUGMW,ADDMB,CONVP,CONVD)
C
C======================================================================
C
C       PURPOSE: THIS ROUTINE CALCULATES THE CORRECTED KS VALUES FOR
C              SOIL SURFACE.
C
C       REF: R. DON WAUCHOPE, TIFTON, GA, USA
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       DAUGH      I  DAUGHTER PRODUCT MASS (BASE UNIT)
C       DAUGMW     I  MOLECULAR WEIGHT OF DAUGHTER (G/MOLES)
C       DK         L  ACTUAL PROCESS RATE FOR DAUGHTER.
C------------------------------------------ OF PARENT, NOT OF DAUGHTER
C       DYIELD     I  DAUGHTER FORMATION PERCENTAGE (0..100)
C       GAIN       L  AMOUNT OF PESTICIDE GAINED FROM PARENT DEG (KG/HA)
C       IPCODE     I  DAUGHTER FORMATION DEGRADATION PROCESS INDEX,
C                     TELLS WHICH PROCESS IS RESPONSIBLE FOR DAUGHTER
C       PARENT     I  ORIGINAL MASS OF PARENT (KG/HA)
C       PARMW      I  MOLECULAR WEIGHT OF PARENT (G/MOLES)
C       TDAU       L  DAUGHTER PRODUCT MASS (KG/HA)
C       WR         L  DAUGHTER WEIGHT / PARENT WEIGHT
C       XK1        I  1ST POSSIBLE PROCESS FOR DAUGHTER FORMULATION
C----------------------- THESE RATES ARE DISSIPATION (TRANSFORMATION) RATES,
C----------------------- NOT FORMATION RATES
C       XK2        I  2ND POSSIBLE PROCESS FOR DAUGHTER FORMULATION
C       XK3        I  3RD POSSIBLE PROCESS FOR DAUGHTER FORMULATION
C       XK4        I  4TH POSSIBLE PROCESS FOR DAUGHTER FORMULATION
C
C       CALLED FROM:  PEMAIN
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   97
C
C-----------------------------------------------------------------------
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      IF(IPCODE.EQ.1) DK=XK1
      IF(IPCODE.EQ.2) DK=XK2
      IF(IPCODE.EQ.3) DK=XK3
      IF(IPCODE.EQ.4) DK=XK4
      WR=DAUGMW/PARMW
C
      GAIN=(PARENT*CONVP)*(1.0D0-EXP(-DK))*WR*(DYIELD*0.01D0)
      DAUGH=(DAUGH*CONVD+GAIN)/CONVD
C
      if (dyield.gt.0.0d0.and.dk.le.0.0d0) then      
          Print*, 'warning: one of the degradation constant becomes 0'
c          stop
      endif
c
C     ACCUMULATE GAIN FOR MASS BALANCE CALCS (KG/HA)
      ADDMB=ADDMB+GAIN
      RETURN
      END
C
      SUBROUTINE KDSTAR(SLKS,CC,NN,FRACOM,THETA,BD)
C
C======================================================================
C
C       PURPOSE:  CALCULATES THE MODIFIED KD FOR KINETIC DESORPTION OF
C             PESTICIDES DURING THE SLOW MOVEMENT FROM MICROPORES TO
C             THE QUICKER MESOPORES
C
C       REF:  DON WAUCHOPE, TIFTON, GA
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       ANKOC  C  ANIONIC SOIL SORPTION CONSTANT
C       BD         I  SOIL BULK DENSITY (G/CM^3)
C       CATKOC     C  CATIONIC SOIL SORPTION CONSTANT.
C       CC         I  SOLUTION CONCENTRATION IN THE SOIL (MG/L)
C       CNSR   L
C       CNSS   L
C       COC        P
C       EK2        C  EQUIL.CONST. FOR ADSORPTION [CM3 H2O/G SOIL]
C       F      C  THE FRACTION OF SORPTION SITE THAT IS IN KINETIC
C       FIRST  L
C       FRACOM     I  FRACTION ORGANIC MATTER IN SOIL
C       FREUND     C  FREUNDLICH NUMBER
C       IEK2   C  EK2 THAT IS READIN FROM THE DATAFILE
C       IP         L  PESTICIDE INDEX - CURRENT PEST
C       MXCHEM     P  MAXIMUM NUMBER OF CHEMICALS IN TRANSPORT MATRIX
C       MXNOD  P  MAXIMUM NUMBER OF NUMERICAL NODES
C       MXPEST     P  MAXIMUM NUMBER OF PESTICIDE TO SIMULATE
C       NN         I  NUMBER OF ACTUAL NUMERICAL NODES
C       NPEST  C  ACTUAL NUMBER OF PESTICIDES TO SIMULATE
C       OKD        L  KD AT T-1
C       REFPH  C  REFERENCE PH FOR KOC
C       RK2        C  ADSORPTION RELEASE CONST.
C       SLKS   I  CORRECTED KD VALUES FOR EACH SOIL LAYER
C       TEN18  P  TEMP OF 10^1.8
C       THETA  I  SOIL VOLUMETRIC WATER CONTENT (CC/CC)
C       TKA        L  TEMP OF 10^PKA
C       TKB        L  TEMP OF 10^PKB
C       TKD        L  UNADJUSTED KD
C       TKDM   P  DEFAULT ANIONIC SOIL SORPTION CONSTANT
C       TKDP   P  DEFAULT CATIONIC SOIL SORPTION CONSTANT
C       TKOC0  L  NEUTRAL SPECIES SOIL SORPTION CONSTANT
C       TKOCL  L  ADJUST (+/-) KOC (CC/G)
C       TKOCM  L  WORKING COPY OF ANIONIC KOC (CC/G)
C       TKOCP  L  WORKING COPY OF CATIONIC KOC (CC/G)
C       TKW        P
C       XKOC   C  SOIL ORGANIC SORPTION CONSTANT
C       XPKA   C
C       XPKB   C
C
C       COMMENTS: MULTIPLY FRACTION OM BY 0.58 TO CONVERT TO FRACTION OC
C
C       CALLED FROM:  MAIN
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   97
C
C-----------------------------------------------------------------------
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER(MXNOD=300,MXCHEM=15,MXPEST=3,TKDM=10.0D0,TKDP=1.0D5,TKW=
     +    1.0D-14,TEN18=63.09573444802D0,COC=0.58D0)
C
C     PESTICIDE COMMON VARIABLES
C
      COMMON /KNTCS/ CONCX2(MXNOD,MXPEST),EK2(MXNOD,MXPEST),
     +    RK2(MXPEST),RDIF(MXPEST),FEK2(MXPEST),IEK2(MXPEST)
C
      LOGICAL FIRSTD
C
      COMMON /RZPEST/ HALFF(MXPEST),HALFFP(MXPEST),HALFFB(MXPEST),
     +    HALFR(MXPEST),HALFRP(MXPEST),HALFRB(MXPEST),HALFSSA(MXPEST),
     +    HALFSSV(MXPEST),HALFSSP(MXPEST),HALFSSB(MXPEST),
     +    HALFSA(MXPEST),HALFSN(MXPEST),HALFSB(MXPEST),HALFBD(MXPEST),
     +    XKH(MXPEST),XKOC(MXPEST),XMW(MXPEST),THETAR(MXPEST),
     +    TMPR(MXPEST),EA(MXPEST),EN(MXPEST),WALKERB(MXPEST),
     +    VMAX(MXPEST),DYIELD(4,MXPEST),FREUND(MXPEST),XPKA(MXPEST),
     +    XPKB(MXPEST),REFPH(MXPEST),CATKOC(MXPEST),ANKOC(MXPEST),
     +    PCOEFF(MXPEST),PPOWER(MXPEST),RCOEFF(MXPEST),RPOWER(MXPEST),
     +    XKOW(MXPEST),PBIND(MXPEST),IPCODE(4,MXPEST),IPANC(MXPEST),
     +    IPDEP(MXPEST),IPACTV(MXPEST),NPEST
C
      DIMENSION FRACOM(MXNOD),SLKS(MXNOD,MXPEST,2),CC(MXNOD,MXCHEM),
     +    THETA(NN),BD(NN),FRACON(MXNOD)
      SAVE FIRSTD
      DATA FIRSTD /.TRUE./
C
      DO 20 IP=1,NPEST
C
C       ..GET CORRECTED ACID/BASE KOC
        IF(CATKOC(IP).GT.0.0D0) THEN
          TKOCP=CATKOC(IP)
        ELSE
          TKOCP=TKDP
        ENDIF
        IF(ANKOC(IP).GT.0.0D0) THEN
          TKOCM=ANKOC(IP)
        ELSE
          TKOCM=TKDM
        ENDIF
        TKA=10.0D0**(-XPKA(IP))
        TKB=10.0D0**(-XPKB(IP))
        CNSR=10.0D0**(-REFPH(IP)+1.8D0)
        DO 10 I=1,NN
C--------------
          TKD=COC*FRACOM(I)*XKOC(IP)
          IF(FIRSTD) SLKS(I,IP,1)=TKD
c          OKD=THETA(I)+BD(I)*SLKS(I,IP,1)
          OKD=THETA(I)+BD(I)*
     +          FSLKS(SLKS(I,IP,1),CC(I,IP+12),FREUND(IP),
     +          theta(i),bd(i))
C---------------
          CNSS=CC(I,1)*TEN18
C
C         .. CALC NEW KD FOR LAYER
          IF(XPKA(IP).GT.0.0D0) THEN
C
C           .. SOIL SORPTION EQUILIBRIUM FOR ACID
            TKOC0=(XKOC(IP)*(CNSR+TKA)-TKOCM*TKA)/CNSR
            TKOCL=(CNSS*TKOC0+TKOCM*TKA)/(CNSS+TKA)
            SLKS(I,IP,1)=TKOCL*FRACOM(I)*COC
          ELSEIF(XPKB(IP).GT.0.0D0) THEN
C
C           .. SOIL SORPTION EQUILIBRIUM FOR BASE
            TKOC0=(XKOC(IP)*(TKW+TKB*CNSR)-TKB*CNSR*TKOCP)/TKW
            TKOCL=(TKW*TKOC0+TKB*CNSS*TKOCP)/(TKW+TKB*CNSS)
            SLKS(I,IP,1)=TKOCL*FRACOM(I)*COC
          ELSE
C
C           .. SOIL SORPTION EQUILIBRIUM FOR NONIONIC
            SLKS(I,IP,1)=TKD
          ENDIF
          SLKS(I,IP,1)=MAX(SLKS(I,IP,1),0.0D0)
C
C         .. IF HAVE FREUNDLICH NUMBER THEN UPDATE KD
c          IF(FREUND(IP).GT.0.0D0.AND.FREUND(IP).LT.1.0D0.AND.
c     +        CC(I,IP+12).GT.0.0D0) THEN
c            SLKS(I,IP,1)=SLKS(I,IP,1)*CC(I,IP+12)**(FREUND(IP)-1.0D0)
c          ENDIF
C
C         ..ADJUST EQUILIBRIUM CONSTANT IF KINETIC SORPTION IS USED SIMULTANEOUSLY
C         WHEN F IS = 1 THEN WE HAVE ALL KINETICS
          SLKS(I,IP,2)=SLKS(I,IP,1)
C         !SAVE ORIGINAL KD FOR MACROPORES
          IF(RK2(IP).GT.0.0D0) THEN
            EK2(I,IP)=FEK2(IP)*SLKS(I,IP,1)
c            EK2(I,IP)=FEK2(IP)*
c     +          FSLKS(SLKS(I,IP,1),CC(I,IP+12),FREUND(IP),
c     +          theta(i),bd(i))
            SLKS(I,IP,1)=(1.0D0-FEK2(IP))*SLKS(I,IP,1)
          ELSE
            EK2(I,IP)=0.0D0
            SLKS(I,IP,1)=SLKS(I,IP,1)
          ENDIF
C Liwang Ma, 7-3-2006
C------------------
          CC(I,IP+12)=CC(I,IP+12)*OKD/(THETA(I)+BD(I)*
     +      FSLKS(SLKS(I,IP,1),CC(I,IP+12),FREUND(IP),theta(i),bd(i)))
C------------------
   10   CONTINUE
   20 CONTINUE
      FIRSTD=.FALSE.
C
      RETURN
      END
C
      SUBROUTINE PEWASH(RAIN,COPLNT,CORES,CDS,RESCOV,IP)
C
C======================================================================
C
C       PURPOSE: THIS SUBROUTINE CALCULATES THE AMOUNT OF PESTICIDE THAT
C              IS WASHED OFF FROM THE PLANT CANOPY ONTO THE RESIDUE
C              COVER AND SOIL SURFACE. THEN IT CALCULATES THE AMOUNT
C              THAT WASHES OFF THE RESIDUE ONTO THE SOIL SURFACE.
C
C       REF:  WILLIS, G.H. W.F. SPENCER, L.L. MCDOWELL. 1980. THE INTERCEPTION
C         OF APPLIED PESTICIDES BY FOLIAGE AND THEIR PERSISTANCE AND RUNOFF
C         POTENTIAL.  PP. 595-606 IN KNISEL, W.G.(ED) 1980 - CREAMS DOC'S.
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       CDS       I/O AMOUNT OF PESTICIDE REACHING THE SOIL
C                 SURFACE AFTER WASHOFF (UG/CC)
C       COEFF  L  THE ACTIVE COEFFICIENT FOR WASHOFF EQU
C       COPLNT     I  THE INITIAL AMOUNT OF CHEMICAL AVAILABLE
C                 FOR DISSIPATION ON THE PLANT CANOPY
C       CORES I/O THE INITIAL AMOUNT OF CHEMICAL AVAILABLE
C                 FOR DISSIPATION ON THE RESIDUE SURFACE
C       PCOEFF     L  EMPIRICAL WASHOFF EQUATION COEFFICIENT
C       POWER  L  THE ACTIVE POWER FOR WASHOFF EQU
C       PPOWER     L  EMPIRICAL WASHOFF EQUATION POWER
C       RAIN   I  AMOUNT OF RAIN USED TO WASHOFF [MM]
C       RCOEFF     I  USER SUPPLIED RESIDUE WASHOFF COEFF
C       RESCOV     I  RESIDUE COVER OF THE SOIL SURFACE; TO
C                 DETERMINE DIRECT SOIL COVER [%/100]
C       RPOWER     I  USER SUPPLIED RESIDUE WASHOFF POWER
C       TRANS  L  AMOUNT OF CHEMICAL IN TRANSIT
C       UCOEFF     I  USER SUPPLIED PLANT WASHOFF EQU COEFF
C       UPOWER     I  USER SUPPLIED PLANT WASHOFF EQU POWER
C
C       COMMENTS:  THIS ROUTINE IS WRITTEN TO HANDLE ONLY ONE PESTICIDE
C              COPLNT AND CORES ARE RETURNED UPDATED FOR LOSSES
C
C       EXTERNAL REFERENCES:
C                 WASHOF
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   97
C
C-----------------------------------------------------------------------
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C   ARRAY DIMENSION VALUES
      PARAMETER(MXPEST=3,CONCMIN=1.0D-15)
C
      COMMON /RZPEST/ HALFF(MXPEST),HALFFP(MXPEST),HALFFB(MXPEST),
     +    HALFR(MXPEST),HALFRP(MXPEST),HALFRB(MXPEST),HALFSSA(MXPEST),
     +    HALFSSV(MXPEST),HALFSSP(MXPEST),HALFSSB(MXPEST),
     +    HALFSA(MXPEST),HALFSN(MXPEST),HALFSB(MXPEST),HALFBD(MXPEST),
     +    XKH(MXPEST),XKOC(MXPEST),XMW(MXPEST),THETAR(MXPEST),
     +    TMPR(MXPEST),EA(MXPEST),EN(MXPEST),WALKERB(MXPEST),
     +    VMAX(MXPEST),DYIELD(4,MXPEST),FREUND(MXPEST),XPKA(MXPEST),
     +    XPKB(MXPEST),REFPH(MXPEST),CATKOC(MXPEST),ANKOC(MXPEST),
     +    PCOEFF(MXPEST),PPOWER(MXPEST),RCOEFF(MXPEST),RPOWER(MXPEST),
     +    XKOW(MXPEST),PBIND(MXPEST),IPCODE(4,MXPEST),IPANC(MXPEST),
     +    IPDEP(MXPEST),IPACTV(MXPEST),NPEST
C
C     ..CALCULATE AMOUNT OF WASHOFF FOR THE PLANT CANOPY
      CDS=0.0D0
      IF(RAIN.GT.0.0D0) THEN
        TRANS=0.0D0
        IF(COPLNT.NE.0.0D0) THEN
          CALL WASHOF(COPLNT,TRANS,RAIN,PCOEFF(IP),PPOWER(IP))
          CORES=CORES+TRANS*RESCOV
          CDS=TRANS*(1.0D0-RESCOV)
          IF(ABS(CDS).LE.CONCMIN) CDS=0.0D0
          IF(ABS(COPLNT).LE.CONCMIN) COPLNT=0.0D0
        ENDIF
C
C       ..CALCULATE AMOUNT OF WASHOFF FOR THE RESIDUE SURFACE
        IF(CORES.NE.0.0D0) THEN
          CALL WASHOF(CORES,TRANS,RAIN,RCOEFF(IP),RPOWER(IP))
          CDS=CDS+TRANS
          IF(ABS(CDS).LE.CONCMIN) CDS=0.0D0
          IF(ABS(CORES).LE.CONCMIN) CORES=0.0D0
C
        ELSE
C       CDS = 0.0D0
        ENDIF
C
C       .. CONVERT FROM SOIL TO PPM WATER
        CDS=CDS/(RAIN*1.0D-1)
C
      ENDIF
C
      RETURN
      END
C
      SUBROUTINE WASHOF(CO,TRANS,RAIN,COEFF,POWER)
C
C======================================================================
C
C       PURPOSE:
C
C       REF:  WILLIS, G.H. W.F. SPENCER, L.L. MCDOWELL. 1980. THE INTERCEPTION
C         OF APPLIED PESTICIDES BY FOLIAGE AND THEIR PERSISTANCE AND RUNOFF
C         POTENTIAL.  PP. 595-606 IN KNISEL, W.G.(ED) 1980 - CREAMS DOC'S.
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       TRANS I/O AMOUNT OF PESTICIDE LEAVING
C       CO        I/O INITIAL AMOUNT OF CHEMICAL
C       COEFF  I  THE ACTIVE COEFFICIENT FOR WASHOFF EQU
C       POWER  I  THE ACTIVE POWER FOR WASHOFF EQU
C       CLEFT  L  PERCENT OF PESTICIDE REMAINING ON THE SURFACE
C       RAIN   I  AMOUNT OF RAIN USED TO WASHOFF [MM]
C       TRANS I/O AMOUNT OF CHEMICAL IN TRANSIT
C
C       CALLED FROM:  PEWASH
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   97
C
C-----------------------------------------------------------------------
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     ..DETERMINE PERCENTAGE REMAINING
C      PRCENT = COEFF * EXP(-POWER * RAIN) * 0.01D0
C      PRCENT = MAX(0.0D0,PRCENT)
C      PRCENT = MIN(1.0D0,PRCENT)
C
C      t1 = CO * prcent
C      CLEFT = t1
C
C      TRANS = CO * (1.0D0 - PRCENT)
C      CO = CO * PRCENT
C
C     ..CALCULATE AMOUNT LEFT AND AMOUNT LEAVING
      CLEFT=((100.0D0-COEFF)*CO+CO*COEFF*EXP(-POWER*RAIN))/100.0D0
      CLEFT=MAX(0.0D0,CLEFT)
      TRANS=CO-CLEFT
      CO=CLEFT
C
      RETURN
      END
C
      SUBROUTINE PEBIND(BD,CC,COBIND,NN,TL,SLKS,THETA)
C
C======================================================================
C
C       PURPOSE: SEQUENCES THROUGH RZWQM PESTICIDES TO FORM THE
C              IRR-REVERSIBLE BOUND POOL OF PESTICIDES
C
C       COBIND    I/O CONCENTRATION OF IRREVERSIBLY BOUND POOL (UG/CC)
C
C       PROGRAMMER:  KEN ROJAS
C
C       CDATE: 05.08.2002
C       MDATE: 05.08.2002
C-----------------------------------------------------------------------
      PARAMETER(MXPEST=3,MXNOD=300,MXCHEM=15)
C
      INTEGER IP,IPACTV,IPANC,IPCODE,IPDEP,NN,NPEST,IEK2
C
      DOUBLE PRECISION ANKOC,BD(MXNOD),CATKOC,CC(MXNOD,MXCHEM),
     +    COBIND(MXNOD,MXPEST),DYIELD,EA,EN,FREUND,HALFBD,HALFF,HALFFB,
     +    HALFFP,HALFR,HALFRB,HALFRP,HALFSA,HALFSB,HALFSN,HALFSSA,
     +    HALFSSB,HALFSSP,HALFSSV,PBIND,PCOEFF,PPOWER,RCOEFF,REFPH,
     +    RPOWER,THETAR,TMPR,VMAX,SLKS(MXNOD,MXPEST,2),WALKERB,XKH,XKOC,
     +    XKOW,XMW,XPKA,XPKB,KD(MXNOD),CONCX2,EK2,RK2,RDIF,F,TL(MXNOD),
     +    THETA(MXNOD),fslks
C
C
      COMMON /KNTCS/ CONCX2(MXNOD,MXPEST),EK2(MXNOD,MXPEST),
     +    RK2(MXPEST),RDIF(MXPEST),FEK2(MXPEST),IEK2(MXPEST)
C
      COMMON /RZPEST/ HALFF(MXPEST),HALFFP(MXPEST),HALFFB(MXPEST),
     +    HALFR(MXPEST),HALFRP(MXPEST),HALFRB(MXPEST),HALFSSA(MXPEST),
     +    HALFSSV(MXPEST),HALFSSP(MXPEST),HALFSSB(MXPEST),
     +    HALFSA(MXPEST),HALFSN(MXPEST),HALFSB(MXPEST),HALFBD(MXPEST),
     +    XKH(MXPEST),XKOC(MXPEST),XMW(MXPEST),THETAR(MXPEST),
     +    TMPR(MXPEST),EA(MXPEST),EN(MXPEST),WALKERB(MXPEST),
     +    VMAX(MXPEST),DYIELD(4,MXPEST),FREUND(MXPEST),XPKA(MXPEST),
     +    XPKB(MXPEST),REFPH(MXPEST),CATKOC(MXPEST),ANKOC(MXPEST),
     +    PCOEFF(MXPEST),PPOWER(MXPEST),RCOEFF(MXPEST),RPOWER(MXPEST),
     +    XKOW(MXPEST),PBIND(MXPEST),IPCODE(4,MXPEST),IPANC(MXPEST),
     +    IPDEP(MXPEST),IPACTV(MXPEST),NPEST
C
C
      DO 20 IP=1,NPEST
        DO 10 J=1,NN
c Liwang Ma, 7-3-2006
            KD(J)=FSLKS(SLKS(J,IP,1),CC(J,IP+12),FREUND(IP),
     +               theta(j),bd(j))
   10   CONTINUE
C
C       ..DETERMINE BINDING RATE AFFECTS
        IF(HALFBD(IP).GT.0.0D0) CALL BINDPOOL(NN,MXNOD,TL,BD,KD,
     +      HALFBD(IP),CC(1,IP+12),CONCX2(1,IP),COBIND(1,IP),THETA,
     +      RK2(IP),EK2(1,IP))
C
   20 CONTINUE
C
      RETURN
      END
C
      SUBROUTINE BINDPOOL(NN,MXNOD,TL,BD,KD,TBLIFE,PSOL,PKIN,PIBIND,
     +    THETA,RK2,EK2)
C======================================================================
C
C       PURPOSE: ESTABLISHES THE IRR-REVERSIBLE BOUND POOL OF PESTICIDES
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O DESCRIPTION
C       --------  --- -----------
C       BD         I  SOIL BULK DENSITY (G/CC-S)
C       EK2           ADSORPTION RATE FOR KINETIC POOL
C       KD            AD/DESORPTION RATE FOR EQUILIBRIUM POOL(CC-W/G-S)
C       MXNOD     MAXIMUM NUMBER OF NUMERICAL NODES
C       NN         I  NUMBER OF ACTUAL NUMERICAL NODES
C       PSOL      PESTICIDE CONC IN SOLUTION (MG/L)
C       PKIN      PESTICIDE CONC IN KINETIC POOL (UG/G)
C       PIBIND     PESTICIDE CONC IN IRREVERSIBLY BOUND POOL (UG/CC)
C       PROPORTION    FRACTION TAKEN AWAY FROM A POOL
C       RK2           DESORPTION RATE FOR KINETIC POOL
C       TBLIFE        HALFLIFE FOR BINDING POOL FORMATION (DAYS)
C       THETA     VOLUMETRIC WATER CONTENT (CC-W/CC-S)
C       TL            THICKNESS OF EACH LAYER (CM)
C
C       PROGRAMMER:  KEN ROJAS
C
C       CDATE: 05.09.2002
C       MDATE: 05.09.2002
C-----------------------------------------------------------------------
C
      INTEGER NN,MXNOD
C
      DOUBLE PRECISION TL(MXNOD),BD(MXNOD),KD(MXNOD),PSOL(MXNOD),
     +    PKIN(MXNOD),PIBIND(MXNOD),TSADS,TSSKN,CONV,KBIND,TBLIFE,
     +    PROPORTION,THETA(MXNOD),TSOL,EK2(MXNOD),RK2
C
C       ..DETERMINE BINDING RATE AFFECTS
      KBIND=0.0D0
      IF(TBLIFE.GT.0.0D0) KBIND=LOG(2.0D0)/TBLIFE
      PROPORTION=1.0D0-EXP(-KBIND)
C
C     ..FORM BINDING POOL(UG/CC) FROM ADSORBED COMPONENTS
      DO 10 I=1,NN
        CONV1=TL(I)*THETA(I)*1.0D-1
        CONV=TL(I)*BD(I)*1.0D-1
C
C       .. TOTAL MASS OF PEST IN THIS LAYER (KG/HA)
        TSOL=PSOL(I)*(CONV1+(KD(I)*CONV))+PIBIND(I)*CONV+PKIN(I)*CONV
C
C       ..DETERMINE MASS IN SOIL PROFILE ADSORBED (KG/HA)
        TSADS=PSOL(I)*KD(I)*CONV*PROPORTION
C
C       ..DETERMINE MASS IN SOIL PROFILE KINETIC POOL (KG/HA)
        IF(RK2.GT.0.0D0) THEN
          TSSKN=(RK2*(EK2(I)*PSOL(I)-(1.0D0+PROPORTION/RK2)*PKIN(I)))*
     +        CONV
C
c Liwang Ma, 8-31-2005
          TBSK=PROPORTION*PKIN(I)*CONV
C         .. DETERMINE CORRECTED MASS IN KINETIC POOL
c          PKIN(I)=(PKIN(I)*CONV+TSSKN)/CONV
          PKIN(I)=(PKIN(I)*CONV-TBSK)/CONV
        ELSE
          TSSKN=0.0D0
        ENDIF
C
C       ..DETERMINE MASS IN BOUND POOL (UG/G)
c tested by Liwang Ma, 8-30-2005
        PIBIND(I)=(PIBIND(I)*CONV+(TSADS+TBSK))/CONV
C
C       .. DETERMINE CORRECTED MASS IN EQUILIBRIUM POOL
        PSOL(I)=(TSOL-(PKIN(I)+PIBIND(I))*CONV)/(CONV1+(KD(I)*CONV))
   10 CONTINUE
C
      RETURN
      END
C
      SUBROUTINE FPMOVAV(FWFPS,TF3,TF6,IP,NDX)
      PARAMETER(MXNOD=300,MXPEST=3)
      PARAMETER(NT3F=MXNOD*MXPEST*3,NT6F=MXNOD*MXPEST*6)
      INTEGER IC(MXPEST),IP,NDX
      DOUBLE PRECISION T3FWF(3,MXPEST,MXNOD),T6FWF(6,MXPEST,MXNOD),
     +    TT3F(3),TT6F(6),FWFPS,TF3,TF6, avemov
      LOGICAL FIRSTP(MXPEST)
      SAVE T3FWF,T6FWF,IC,FIRSTP
      DATA T3FWF /NT3F*0.0D0/,T6FWF /NT6F*0.0D0/,IC /MXPEST*0/,FIRSTP /
     +    MXPEST*.TRUE./
C
C     .. INITILAIZE FOR FIRST TIME THROUGH
      IF(FIRSTP(IP)) THEN
        IC(IP)=1
        FIRSTP(IP)=.FALSE.
      ELSE
        IC(IP)=IC(IP)+1
      ENDIF
C
C     ..PUT SAVED VALUES IN TEMP ARRAYS
      DO 10 J=1,3
        TT3F(J)=T3FWF(J,IP,NDX)
   10 CONTINUE
      DO 20 J=1,6
        TT6F(J)=T6FWF(J,IP,NDX)
   20 CONTINUE
C
C     .. DETERMINE MOVING AVERAGES
      TF3=AVEMOV(TT3F,3,FWFPS,IC(IP))
      TF6=AVEMOV(TT6F,6,FWFPS,IC(IP))
C
C     .. STORE TEMP ARRAYS
      DO 30 J=1,3
        T3FWF(J,IP,NDX)=TT3F(J)
   30 CONTINUE
      DO 40 J=1,6
        T6FWF(J,IP,NDX)=TT6F(J)
   40 CONTINUE
C
      RETURN
      END
C=========================================================
c Function for Freundlich adsorption
C=========================================================
      function fslks(kf,cc,nf,theta,roe)
	double precision fslks,kf,cc,nf,theta,roe,total1,total2
c
         IF(nf.GT.0.0D0.AND.nf.LT.1.0D0.AND.
     +        CC.GT.0.0D0) THEN
         CCX=0.0                                                        
         SE=KF*CC**NF                                                      
C--- CALCULATION OF S-EQUILIBRIUM AT TIME ZERO    --------------        
C                                                                       
      total1=theta*cc+roe*se
      C0E=CC+(ROE/THETA)*SE                                             
      CEX=CC                                                            
C                                                                       
C---  ITERATION FOR S - EQUILIBRIUM -------------------------------     
C                                                                       
      IF(KF.EQ.0.0D0) GO TO 20                                          
      IF(C0E.LT.1.0E-07) GO TO 20                                      
5     CONTINUE                                                          
      DO 11 L=1,30                                                       
          CC=CEX                                                        
          CEX=C0E/(1+KF*(ROE/THETA)*CC**(NF-1.0))                       
11    CONTINUE                                                          
      IF(ABS(CEX-CC).LT.1.0D-07) GO TO 15                               
      GO TO 5                                                           
15    CC=CEX                                                            
      SE=KF*CC**NF                                                      
  20  CONTINUE                                                          
C                                                                       
      total2=theta*cc+roe*se
	if (abs((total1-total2)/total2).le.1.0d-6) then
            fSLKS=kf*CC**(nf-1.0D0)
	    else
	    stop 'problem with Freundlich equation'
	   endif
	else
	      fslks=kf
      ENDIF
      return
	end
C=========================================================
c kinetics from Selim and Ma
C=========================================================
c
      SUBROUTINE Kinetics (KF,NF,CC,ROE,THETA,k1,k2,kirr,DT)                
C                                                                       
      IMPLICIT NONE                                                     
C                                                                       
      INTEGER I,J,K,L,M,N                           
C                                                                       
      double precision X,DT,R,TAU,KF,NF,ROE,THETA,CT1,CT2                    
     &     , C0,C0E,CEX,XX,X1,X2,X3,SORBT2                      
      double precision  S1,S2,S3,SI,SE,CC                                           
      double precision  S1X,S2X,S3X,SIX,CCX                                         
      double precision KIRR,K1,K2,K3,K4,K5,K6,NS1,NS2,H1,H2,CAVG            
C                                                                       
      DIMENSION CT1(105),X(15),CT2(105)        
C                                                                       
      NS1=1.0d0                                                         
      K3=0.0d0                                                          
      K4=0.0d0                                                          
      NS2=1.0d0                                                         
      K5=0.0d0                                                          
      K6=0.0d0                                                          
C                                                                       
C                                                                       
C===========  SET INITIAL CONCENTRAION ====================             
c         CC=(TWATER(MN)-SOIL(MN)*ADMC)*XCI(MN)/TWATER(MN)               
C                                                                       
         CCX=0.0                                                        
         SE=0.0                                                         
         S1=0.0                                                         
         S1X=0.0                                                        
         S2=0.0                                                         
         S2X=0.0                                                        
         S3=0.0                                                         
         S3X=0.0                                                        
         SI=0.0                                                         
         SIX=0.0                                                        
C--- CALCULATION OF S-EQUILIBRIUM AT TIME ZERO    --------------        
C                                                                       
      C0E=CC+(ROE/THETA)*SE                                             
      CEX=CC                                                            
C                                                                       
C---  ITERATION FOR S - EQUILIBRIUM -------------------------------     
C                                                                       
      IF(KF.EQ.0.0D0) GO TO 20                                          
      IF(C0E.LT.10.0E-07) GO TO 20                                      
5     CONTINUE                                                          
      DO 11 L=1,20                                                       
          CC=CEX                                                        
          CEX=C0E/(1+KF*(ROE/THETA)*CC**(NF-1.0))                       
11    CONTINUE                                                          
      IF(ABS(CEX-CC).LT.1.0D-06) GO TO 15                               
      GO TO 5                                                           
15    CC=CEX                                                            
      SE=KF*CC**NF                                                      
  20  CONTINUE                                                          
C                                                                       
C-----------CALCULATIONS AT TIMES GREATER THAN ZERO  ---------------    
C --- EXPLICIT-IMPLICIT FINITE DIFFERENCE CALCULATIONS -----------      
C                                                                       
      DO 30 M=1,15                                                      
C========================== CALCULATE THE CONCENTRATION GOING INTO      
C                           THE SOIL PHASES                             
C                                                                       
      H1=0.0                                                            
      H2=0.0                                                            
      CAVG=(CC+CCX)/2                                                   
      IF (CAVG.LE.0.0) CAVG=0.0                                         
      IF (CAVG.GE.1E-06) THEN                                           
           H1=CAVG**NS1                                                 
           H2=CAVG**NS2                                                 
      ENDIF                                                             
C                                                                       
      X1=DT*(K1*H1-K2*(ROE/THETA)*((S1+S1X)/2))                         
      X2=DT*(K3*H2-K4*(ROE/THETA)*((S2+S2X)/2))                         
      X3=DT*KIRR*CAVG                                                   
      CCX=CC-(X1+X2+X3)                                                 
C                                                                       
C======================== CALCULATE SOLUTES ON THE SOIL PHASE           
C                                                                       
      S1X=S1+X1*(THETA/ROE)                                             
      S2X=S2+X2*(THETA/ROE)+DT*(K6*((S3+S3X)/2)                         
     &                       -K5*((S2+S2X)/2))                          
      S3X=S3+DT*(K5*((S2+S2X)/2)                                        
     &                       -K6*((S3+S3X)/2))                          
      SIX=SI+DT*KIRR*(THETA/ROE)*CAVG                                   
C                                                                       
C ------------    ITERATION FOR S'S CALCULAIONS AFTER CONSIDERING       
C                 THE INFLOW INTO THE SOIL PHASE AT DT INTERVAL         
C                                                                       
      C0E=CCX+(ROE/THETA)*SE                                            
      IF(C0E.LT.10.0E-07) GO TO 50                                      
      CEX=CCX                                                           
35    CONTINUE                                                          
      DO 40 L=1,20                                                       
      CCX=CEX                                                           
40    CEX=C0E/(1+KF*(ROE/THETA)*CCX**(NF-1.0D0))                        
      IF(ABS(CEX-CCX).LT.1.0D-06) GO TO 45                              
      GO TO 35                                                          
45    CCX=CEX                                                           
      SE=KF*CCX**NF                                                     
50    CONTINUE                                                          
C                                                                       
      IF ((SORBT2-(SIX+S1X+S2X+S3X+SE)).LE.1E-05)  GOTO 369                
      SORBT2=SIX+S1X+S2X+S3X+SE                                            
30    CONTINUE                                                          
C ========================== END OF INTERATION AT THE DT TIME INTERVAL  
369   CONTINUE                                                          
      return
	end
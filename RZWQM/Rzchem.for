C $Id: Rzchem.for,v 1.1 2002/08/28 00:00:48 rojas Exp $
C
      SUBROUTINE ACDCOF(A,EK,ECA,ENA,ENH4,EMG,EAL,GAM2,GYPSUM,LIME,
     +    GIBSIT,NUMR,SK)
C
C======================================================================
C
C       PURPOSE:  THIS SUBROUTINE CALCULATES THE PARTIAL DERIVATIVES
C         OF EACH EQUATION IN THE ACID SYSTEM.  THESE VALUES ARE USED
C         TO BUILD THE JACOBIAN MATRIX USED IN THE MATRIX SOLVER.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       A          O    JACOBIAN MATRIX OF PARTIAL DERIVATIVES
C       AL         I    ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALOH       I    ALUMINUM HYDROXIDE CONCENTRATION [MOLES/LITER]
C       ALORG      I    ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALSO4      I    ALUMINUM SULFATE CONCENTRATION [MOLES/LITER]
C       CAI        I    INITIAL CALCIUM CONCENTRATION [MOLES/GRAM SOIL]
C       CAIS       I    INITIAL CALCIUM CONCENTRATION [MOLES/LITER]
C       CO3        I    CARBONATE CONCENTRATION [MOLES/LITER]
C       CO3IS      I    INITIAL CARBONATE CONCENTRATION [MOLES/LITER]
C       EAL        I    EXCHANGABLE IONS FOR ALUMINUM
C       ECA        I    EXCHANGABLE IONS FOR CALCIUM
C       EK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       EMG        I    EXCHANGABLE IONS FOR MAGNESIUM
C       ENA        I    EXCHANGABLE IONS FOR SODIUM
C       ENH4       I    EXCHANGABLE IONS FOR AMMONIA
C       GAM1       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM3       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF THREE
C       H          I    HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3       I    BICARBONATE CONCENTRATION [MOLES/LITER]
C       HCO3IS     I    INITIAL BICARBONATE CONCENTRATION [MOLES/LITER]
C       HIS        I    INITIAL HYDROGEN CONCENTRATION [MOLES/LITER]
C       I          I    COUNTER VARIABLE
C       J          I    COUNTER VARIABLE
C       ORG        I    ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       ORGIS      I    INITIAL O.M. CONCENTRATION [MOLES/LITER]
C       PCO2       I    PARTIAL PRESSURE OF CO2 [ATM]
C       SK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SO4        I    SULFATE CONCENTRATION [MOLES/LITER]
C       SO4I       I    INITIAL SULFATE CONCENTRATION [MOLES/GRAM SOIL]
C       SO4IS      I    INITIAL SULFATE CONCENTRATION [MOLES/LITER]
C       XMG        I    MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGI       I    INITIAL MAGNESIUM CONC [MOLES/GRAM SOIL]
C       XMGIS      I    INITIAL MAGNESIUM CONC [MOLES/LITER]
C       XNA        I    SODIUM CONCENTRATION [MOLES/LITER]
C       XNAI       I    INITIAL SODIUM CONCENTRATION [MOLES/GRAM SOIL]
C       XNAIS      I    INITIAL SODIUM CONCENTRATION [MOLES/LITER]
C       XNASIS     I    INITIAL SODIUM SULFATE CONCENTRATION [MOLES/LITER]
C       XNH4       I    AMMONIA CONCENTRATION [MOLES/LITER]
C       XNH4I      I    INITIAL AMMONIA CONCENTRATION [MOLES/GRAM SOIL]
C       XNH4IS     I    INITIAL AMMONIA CONCENTRATION [MOLES/LITER]
C
C
C       COMMENTS:
C
C       CALLED FROM:
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:  1.0
C
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
      DIMENSION A(NUMR,NUMR)
      LOGICAL LIME,GYPSUM,GIBSIT
C
      SQRTCA=SQRT(CA)
      G2=1.0D0/(GAM2*GAM2)
      C1=4.39D-3*G2
      C2=4.37D-3*G2
      C3=1.905D-1/GAM2
      C16=SK
C     
C-----INITIALIZE JOCOBIAN MATRIX
C     
      DO 20 I=1,NUMR
        DO 10 J=1,NUMR
          A(I,J)=0.0D0
   10   CONTINUE
   20 CONTINUE
C     
C-----EQUATION TO REACT CARBON DIOXIDE WITH WATER AT A FIXED PCO2.
C     
      A(1,4)=HCO3
      A(1,5)=H
C     
C-----EQUATION FOR DISSOCIATION OF HCO3 TO FORM CO3 AND H.
C     
      A(2,3)=H/HCO3
      A(2,4)=CO3/HCO3
      A(2,5)=-CO3*H/HCO3**2.0D0
C     
C-----CHARGE BALANCE EQUATION FOR THIS VARIABLE SET
C-----NOTE THAT THE NET CHANGE IN CHARGE EQUALS ZERO, RATHER THAN
C-----THE NET CHARGE ZERO.  THIS ALLOWS THE INITIAL XNALYSIS TO BE
C-----OUT OF BALANCE AND STILL COMPUTE THE CORRECT CHANGES TO ACHIEVE
C-----EQUILIBRIUM.
C     
      IF(GYPSUM) THEN
        A(3,1)=1.0D0
        A(3,2)=1.5D0
        A(3,3)=-1.0D0
        A(3,4)=0.5D0
        A(3,5)=-0.5D0
        A(3,6)=-0.5D0
        A(3,7)=-0.5D0*XNA/C3-1.0D0
        A(3,8)=1.0D0
        A(3,9)=0.5D0
        A(3,10)=0.5D0-0.5D0*SO4/C3
        A(3,11)=1.0D0
        A(3,12)=0.5D0
        A(3,13)=1.0D0
      ELSE
        A(3,1)=1.0D0
        A(3,2)=1.5D0
        A(3,3)=-1.0D0
        A(3,4)=0.5D0
        A(3,5)=-0.5D0
        A(3,6)=-0.5D0
        A(3,7)=-1.0D0
        A(3,8)=1.0D0
        A(3,9)=0.5D0
        A(3,10)=0.5D0
        A(3,11)=1.0D0
        A(3,12)=0.5D0
        A(3,13)=1.0D0
      ENDIF
C     
C-----IF THIS IS NOT A GYPSUM NOR LIME SYSTEM, USE THE FOLLOWING
C-----EQUATION.  OTHERWISE, TEST TO SEE IF LIME IS ABSENT, BUT
C-----GYPSUM IS PRESENT.
C     
      IF(.NOT.LIME.AND..NOT.GYPSUM) THEN
C       
C-----EQUATION FOR CALCIUM MASS BALANCE WHEN LIME AND GYPSUM ARE
C-----ABSENT.
C       
        A(4,1)=C16
        A(4,14)=EK*0.5D0
      ELSEIF(.NOT.LIME.AND.GYPSUM) THEN
C       
C-----EQUATION FOR CALCIUM MASS BALANCE WHEN LIME IS ABSENT, BUT
C-----GYPSUM IS PRESENT.
C       
        A(4,1)=C16
        A(4,7)=-C16-C16*XMG/C2-C16*XNA/C2
        A(4,8)=-C16*SO4/C2
        A(4,9)=-C16
        A(4,10)=-C16*SO4/C2
        A(4,14)=EK*0.5D0
      ELSE
C       
C-----USE THIS EQUATION FOR A LIME SYSTEM
C       
        A(4,1)=CO3
        A(4,3)=CA
      ENDIF
C     
C-----IF GYPSUM IS ABSENT, USE THIS EQUATION, OTHERWISE USE
C-----SOLUBILITY PRODUCT FOR GYPSUM.
C     
      IF(.NOT.GYPSUM.AND..NOT.LIME) THEN
C       
C-----EQUATION FOR SULFATE MASS BALANCE IN THE ABSENCE OF GYPSUM.
C       
        A(5,7)=C16
        A(5,9)=C16
      ELSEIF(.NOT.GYPSUM.AND.LIME) THEN
        A(5,1)=C16*SO4/C1
        A(5,7)=C16+C16*CA/C1
        A(5,9)=C16
      ELSE
C       
C-----SOLUBILITY PRODUCT RELATIONSHIP FOR GYPSUM.
C       
        A(5,1)=SO4
        A(5,7)=CA
      ENDIF
C     
C-----MASS BALANCE EQUATION FOR MAGNESIUM.
C     
      IF(.NOT.GYPSUM) THEN
        A(6,8)=C16
        A(6,16)=EK*0.5D0
      ELSE
        A(6,7)=C16*XMG/C2
        A(6,8)=C16+C16*SO4/C2
        A(6,16)=EK*0.5D0
      ENDIF
C     
C-----MASS BALANCE EQUATION FOR SODIUM.
C     
      IF(.NOT.GYPSUM) THEN
        A(7,10)=C16
        A(7,15)=EK
      ELSE
        A(7,7)=C16*XNA/C3
        A(7,10)=C16+C16*SO4/C3
        A(7,15)=EK
      ENDIF
C     
C-----MASS BALANCE EQUATION FOR NH4.
C     
      A(8,12)=C16
      A(8,17)=EK
C     
C-----EQUATION FOR ALOH ION PAIR
C     
      A(9,2)=1.0D0/(H*ALOH)
      A(9,4)=-AL/(H**2.0D0*ALOH)
      A(9,11)=-AL/(H*ALOH**2.0D0)
C     
C-----EQUATION FOR ALSO4 ION PAIR
C     
      A(10,2)=SO4/ALSO4
      A(10,7)=AL/ALSO4
      A(10,9)=-AL*SO4/ALSO4**2.0D0
C     
C-----EQUATION FOR GIBBSITE SOLUBILITY
C     
      IF(GIBSIT) THEN
        A(11,2)=1.0D0/H**3.0D0
        A(11,4)=-3.0D0*AL/H**4.0D0
      ELSE
        A(11,2)=C16
        A(11,9)=C16
        A(11,11)=C16
        A(11,13)=C16
        A(11,18)=EK/3.0D0
      ENDIF
C     
C-----IF BOTH LIME AND GIBBSITE ARE ABSENT, USE THIS EQUATION.
C-----NOTE THAT IN THIS CASE THE PRESENCE OR ABSENCE OF GYPSUM MAKES
C-----NO DIFFERENCE AND WE STILL USE THE SAME EQUATION.
C     
C-----AS WRITTEN THIS EQUATION IGNORES THE HCO3 ION PAIRS.
C     
      IF(LIME) THEN
        IF(GYPSUM.AND.GIBSIT) THEN
          A(12,1)=-1.0D0
          A(12,2)=-1.5D0
          A(12,3)=1.0D0
          A(12,4)=-0.5D0
          A(12,5)=0.5D0
          A(12,7)=1.0D0+XMG/C2+XNA/C3
          A(12,8)=SO4/C2
          A(12,9)=-0.5D0
          A(12,10)=SO4/C3
          A(12,11)=-1.5D0
          A(12,13)=-1.5D0
          A(12,14)=-0.5D0*EK/SK
          A(12,18)=-EK/SK*0.5D0
        ELSEIF(GYPSUM.AND..NOT.GIBSIT) THEN
          A(12,1)=-1.0D0
          A(12,3)=1.0D0
          A(12,4)=-0.5D0
          A(12,5)=0.5D0
          A(12,7)=1.0D0+XMG/C2+XNA/C3
          A(12,8)=SO4/C2
          A(12,9)=1.0D0
          A(12,10)=SO4/C3
          A(12,14)=-0.5D0*EK/SK
        ELSEIF(.NOT.GYPSUM.AND.GIBSIT) THEN
          A(12,1)=-1.0D0-SO4/C1
          A(12,2)=-1.5D0
          A(12,3)=1.0D0
          A(12,4)=-0.5D0
          A(12,5)=0.5D0
          A(12,7)=-CA/C1
          A(12,9)=-1.5D0
          A(12,11)=-1.5D0
          A(12,13)=-1.5D0
          A(12,14)=-0.5D0*EK/SK
          A(12,18)=-EK/SK*0.5D0
        ELSE
          A(12,1)=-1.0D0-SO4/C1
          A(12,3)=1.0D0
          A(12,4)=-0.5D0
          A(12,5)=0.5D0
          A(12,7)=-CA/C1
          A(12,14)=-0.5D0*EK/SK
        ENDIF
      ELSE
        IF(GIBSIT) THEN
          A(12,2)=-1.5D0
          A(12,3)=1.0D0
          A(12,4)=-0.5D0
          A(12,5)=0.5D0
          A(12,9)=-1.5D0
          A(12,11)=-1.5D0
          A(12,13)=-1.5D0
          A(12,18)=-EK/SK*0.5D0
        ELSE
          A(12,3)=1.0D0
          A(12,4)=-0.5D0
          A(12,5)=0.5D0
        ENDIF
      ENDIF
C     
      A(13,2)=ORG/ALORG
      A(13,6)=AL/ALORG
      A(13,13)=-AL*ORG/ALORG**2.0D0
C     
C-----EQUATION FOR CA-NA ION EXCHANGE.
C     
      A(14,1)=0.5D0/(SQRTCA*XNA)*ENA/ECA
      A(14,14)=-SQRTCA/XNA*ENA/ECA**2.0D0
      A(14,15)=SQRTCA/(XNA*ECA)
      A(14,10)=-SQRTCA/XNA**2.D0*ENA/ECA
C     
C-----EQUATION FOR CA-MG ION EXCHANGE
C     
      A(15,1)=EMG/(XMG*ECA)
      A(15,14)=-CA/XMG*EMG/ECA**2.D0
      A(15,16)=CA/(XMG*ECA)
      A(15,8)=-CA/XMG**2.D0*EMG/ECA
C     
C-----EQUATION FOR NA-NH4 ION EXCHANGE.
C     
      A(16,15)=-XNA/XNH4*ENH4/ENA**2.D0
      A(16,17)=XNA/(XNH4*ENA)
      A(16,10)=ENH4/(XNH4*ENA)
      A(16,12)=-XNA/XNH4**2.D0*ENH4/ENA
C     
C-----EQUATION FOR CA-AL ION EXCHANGE
C     
      A(17,1)=3.0D0*CA**2.D0*EAL**2.D0/(AL**2.D0*ECA**2.D0)
      A(17,2)=-2.0D0*CA**3.D0*EAL**2.D0/(AL**3.D0*ECA**2.D0)
      A(17,14)=-2.0D0*CA**3.D0*EAL**2.D0/(AL**2.D0*ECA**3.D0)
      A(17,18)=2.0D0*CA**3.D0*EAL/(AL**2.D0*ECA**2.D0)
C     
C-----MASS BALANCE EQUATION FOR EXCHANGEABLE IONS.
C     
      A(18,14)=1.0D0
      A(18,15)=1.0D0
      A(18,16)=1.0D0
      A(18,17)=1.0D0
      A(18,18)=1.0D0
C     
      RETURN
      END
C
      SUBROUTINE ACDEXC(AL,CA,EAL,ECA,EMG,ENA,ENH4,GAM1,GAM2,GAM3,XMG,
     +    XNA,XNH4,EPS,ABORT)
C======================================================================
C
C       PURPOSE:  THIS SUBROUTINE CALCULATES THE EXCHANGABLE IONS FOR
C         CA, NA, MG, NH4, AND AL USED IN CALCULATIONS FOR THE ACID
C         SYSTEM.  THE VALUES ARE CALCULATED USING A NEWTON-RAPHSON
C         ITERATIVE METHOD.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       AL         I    ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALPHA           TEMPORARY STORAGE OF COEFFICIENTS
C       BETA            TEMPORARY STORAGE OF COEFFICIENTS
C       C1              TEMPORARY STORAGE OF COEFFICIENTS
C       C2              TEMPORARY STORAGE OF COEFFICIENTS
C       CA         I    CALCIUM CONCENTRATION [MOLES/LITER]
C       DENA            COMPUTED DELTA EXCHANGABLE SODIUM
C       DF              COMPUTED DERIVATIVE OF THE FUNCTION
C       EAL        O    EXCHANGABLE IONS OF ALUMINUM
C       ECA        O    EXCHANGABLE IONS OF CALCIUM
C       EMG        O    EXCHANGABLE IONS OF MAGNESIUM
C       ENA        O    EXCHANGABLE IONS OF SODIUM
C       ENH4       O    EXCHANGABLE IONS OF AMMONIA
C       EPS        I    THRESHHOLD FOR CONVERGENCE
C       F               COMPUTED FUNCTIONAL VALUE
C       GAM1       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM2       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       GAM3       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF THREE
C       I               COUNTER VARIABLE
C       N0              MAXIMUM NUMBER OF ITERATIONS
C       OLDENA          PREVIOUS VALUE OF EXCHANGABLE SODIUM
C       XMG        I    MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XNA        I    SODIUM CONCENTRATION [MOLES/LITER]
C
C
C       COMMENTS:
C
C       EXTERNAL REFERENCES:
C
C       CALLED FROM: SCINIT, SCACD
C
C       PROGRAMMER: KENR
C
C       VERSION:
C
C======================================================================
C
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL ABORT
C
C     --CALCULATE CONSTANTS
C
      ABORT=.FALSE.
      N0=200
      C1=0.707D0/SQRT(GAM2)*GAM1
      C2=0.676D0/GAM2**3.0D0*GAM3**2.0D0
      SQRTCA=SQRT(CA)
      ALPHA=SQRTCA/(XNA*C1)+1.0D0+XMG*0.67D0/CA*SQRTCA/(XNA*C1)+XNH4*
     +    0.220D0/XNA
      BETA=SQRT(AL**2.0D0*C2/CA**3.0D0*(SQRTCA/(XNA*C1))**3.0D0)
C     
C     --USE NEWTON-RAPHSON TO SOLVE FOR ENA
C     
      DO 10 I=1,N0
        F=BETA*ENA**1.50D0+ALPHA*ENA-1.0D0
        DF=1.5D0*BETA*SQRT(ENA)+ALPHA
        DENA=-F/DF
        OLDENA=ENA
        ENA=ENA+DENA
C       
        IF(DABS(DENA/OLDENA).LT.EPS) GOTO 20
C     
   10 CONTINUE
C     
C     --COULD NOT SOLVE FOR ENA
C     
      ABORT=.TRUE.
      PRINT*
      PRINT*,'======================================================'
      PRINT*,'SKIP CHEM EQUIL BECAUSE NO SOLUTION FOR EXCHANGABLE NA'
      PRINT*,'======================================================'
      PRINT*
      GOTO 30
C     
C     --BACK-SOLVE FOR OTHER EXCHANGABLE IONS
C     
   20 ECA=ENA*SQRTCA/(XNA*C1)
      EMG=ENA*XMG*0.670D0/CA*SQRTCA/(XNA*C1)
      ENH4=ENA*XNH4*0.220D0/XNA
      EAL=SQRT(AL**2.0D0*ECA**3.0D0*C2/CA**3.0D0)
C     
   30 RETURN
      END
C
      SUBROUTINE ACDUPD(ECA,ENA,EMG,ENH4,EAL,X,GYPSUM,LIME,GAM2,NUMR)
C
C======================================================================
C
C       PURPOSE:  THIS SUBROUTINE TESTS AND CORRECTS SOLUTION ESTIMATES
C         FOR MASS BALANCE UNDERRUNS.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       AL        I/O   ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALOH      I/O   ALUMINUM HYDROXIDE CONCENTRATION [MOLES/LITER]
C       ALORG     I/O   ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALSO4     I/O   ALUMINUM SULFATE CONCENTRATION [MOLES/LITER]
C       CA        I/O   CALCIUM CONCENTRATION [MOLES/LITER]
C       CASO4     I/O  CALCIUM SULFATE CONC [MOLES/LITER]
C       CO3       I/O   CARBONATE CONCENTRATION [MOLES/LITER]
C       EAL       I/O   EXCHANGABLE IONS FOR ALUMINUM
C       ECA       I/O   EXCHANGABLE IONS FOR CALCIUM
C       EMG       I/O   EXCHANGABLE IONS FOR MAGNESIUM
C       ENA       I/O   EXCHANGABLE IONS FOR SODIUM
C       ENH4      I/O   EXCHANGABLE IONS FOR AMMONIA
C       GAM2       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       GYPSUM     I    LOGICAL FLAG INDICATING IF SOLID GYPSUM PRESENT
C       H         I/O   HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3      I/O   BICARBONATE CONCENTRATION [MOLES/LITER]
C       I          I    COUNTER VARIABLE
C       LIME       I    LOGICAL FLAG INDICATING IF SOLID LIME PRESENT
C       ORG       I/O   ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       OVER            FLAG USED FOR MASS UNDERRUN
C       SO4       I/O   SULFATE CONCENTRATION [MOLES/LITER]
C       X          I    SOLUTION VECTOR OF CONCENTRATION CORRECTIONS
C       XMG       I/O   MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGSO4    I/O   MAGNESIUM SULFATE CONC [MOLES/LITER]
C       XNA       I/O   SODIUM CONCENTRATION [MOLES/LITER]
C       XNASO4    I/O   SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4      I/O   AMMONIA CONCENTRATION [MOLES/LITER]
C
C
C       COMMENTS:
C
C       MASS STORAGE FILES:
C
C
C       EXTERNAL REFERENCES:
C
C
C       CALLED FROM:
C
C       PROGRAMMER:
C
C       VERSION:
C
C======================================================================
C
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
      LOGICAL GYPSUM,LIME,OVER
      DIMENSION X(NUMR)
C
      OVER=.FALSE.
C     
C-----TEST AND CORRECT SOLUTION ESTIMATES FOR MASS BALANCE
C-----OVERRUNS.  KEEP ALL THE CONCENTRATIONS POSITIVE.
C     
   10 CONTINUE
C     
      IF(CA+X(1).LE.0.0D0.AND.X(1).LT.0.0D0) OVER=.TRUE.
      IF(AL+X(2).LE.0.0D0.AND.X(2).LT.0.0D0) OVER=.TRUE.
      IF(CO3+X(3).LE.0.0D0.AND.X(3).LT.0.0D0) OVER=.TRUE.
      IF(H+X(4).LE.0.0D0.AND.X(4).LT.0.0D0) OVER=.TRUE.
      IF(HCO3+X(5).LE.0.0D0.AND.X(5).LT.0.0D0) OVER=.TRUE.
      IF(ORG+X(6).LE.0.0D0.AND.X(6).LT.0.0D0) OVER=.TRUE.
      IF(SO4+X(7).LE.0.0D0.AND.X(7).LT.0.0D0) OVER=.TRUE.
      IF(XMG+X(8).LE.0.0D0.AND.X(8).LT.0.0D0) OVER=.TRUE.
      IF(ALSO4+X(9).LE.0.0D0.AND.X(9).LT.0.0D0) OVER=.TRUE.
      IF(XNA+X(10).LE.0.0D0.AND.X(10).LT.0.0D0) OVER=.TRUE.
      IF(ALOH+X(11).LE.0.0D0.AND.X(11).LT.0.0D0) OVER=.TRUE.
      IF(XNH4+X(12).LE.0.0D0.AND.X(12).LT.0.0D0) OVER=.TRUE.
      IF(ALORG+X(13).LE.0.0D0.AND.X(13).LT.0.0D0) OVER=.TRUE.
      IF(ECA+X(14).LE.0.0D0.AND.X(14).LT.0.0D0) OVER=.TRUE.
      IF(ENA+X(15).LE.0.0D0.AND.X(15).LT.0.0D0) OVER=.TRUE.
      IF(EMG+X(16).LE.0.0D0.AND.X(16).LT.0.0D0) OVER=.TRUE.
      IF(ENH4+X(17).LE.0.0D0.AND.X(17).LT.0.0D0) OVER=.TRUE.
      IF(EAL+X(18).LE.0.0D0.AND.X(18).LT.0.0D0) OVER=.TRUE.
C     
C     --IF WE HAVE MASS UNDERFLOW THEN HALF THE CORRECTION
C     
      IF(OVER) THEN
        DO 20 I=1,18
          X(I)=X(I)*0.50D0
   20   CONTINUE
        OVER=.FALSE.
        GOTO 10
C     
C     --UPDATE EACH CONSTITUENT
C     
      ELSE
        CA=CA+X(1)
        AL=AL+X(2)
        CO3=CO3+X(3)
        H=H+X(4)
        HCO3=HCO3+X(5)
        ORG=ORG+X(6)
        SO4=SO4+X(7)
        XMG=XMG+X(8)
        ALSO4=ALSO4+X(9)
        XNA=XNA+X(10)
        ALOH=ALOH+X(11)
        XNH4=XNH4+X(12)
        ALORG=ALORG+X(13)
        ECA=ECA+X(14)
        ENA=ENA+X(15)
        EMG=EMG+X(16)
        ENH4=ENH4+X(17)
        EAL=EAL+X(18)
      ENDIF
C     
C     --IF IN GYPSUM SYSTEM THEN UPDATE CATION IONIC PAIRS
C     
      IF(GYPSUM) THEN
        CASO4=CA*SO4/(4.39D-3/GAM2**2.0D0)
        XNASO4=XNA*SO4/(1.905D-1/GAM2)
        XMGSO4=XMG*SO4/(4.37D-3/GAM2**2.0D0)
      ENDIF
C     
C     --IF IN LIME SYSTEM AND NOT IN GYPSUM THEN UPDATE CALCIUM SULFATE
C     
      IF(LIME.AND..NOT.GYPSUM) THEN
        CASO4=CA*SO4/(4.39D-3/GAM2**2.0D0)
      ENDIF
C     
      RETURN
      END
C
C
      SUBROUTINE ACDVTR(B,ECA,ENA,ENH4,EMG,EAL,GAM1,GAM2,GAM3,SK,EK,
     +    PCO2,LIME,GYPSUM,GIBSIT,NUMR)
C
C======================================================================
C
C       PURPOSE:  COMPUTE FUNCTIONAL SOLUTION VECTOR FOR THE ACID
C         SYSTEM.  THIS SUBROUTINE IS ONLY ENTERED WITH A PH LESS
C         THAN 6.0.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       AL         I    ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALI        I    INITIAL ALUMINUM CONCENTRATION [MOLES/GRAM SOIL]
C       ALIS       I    INITIAL ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALOH       I    ALUMINUM HYDROXIDE CONCENTRATION [MOLES/LITER]
C       ALOHI      I    INITIAL ALOH CONCENTRATION [MOLES/GRAM SOIL]
C       ALOHIS     I    INITIAL ALOH CONCENTRATION [MOLES/LITER]
C       ALORG      I    ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALORIS     I    INITIAL ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALSO4      I    ALUMINUM SULFATE CONCENTRATION [MOLES/LITER]
C       ALSO4I     I    INITIAL ALSO4 CONCENTRATION [MOLES/GRAM SOIL]
C       ALSOIS     I    INITIAL ALSO4 CONCENTRATION [MOLES/LITER]
C       B          O    SOLUTION VECTOR
C       CA         I    CALCIUM CONCENTRATION [MOLES/LITER]
C       CAI        I    INITIAL CALCIUM CONCENTRATION [MOLES/GRAM SOIL]
C       CAIS       I    INITIAL CALCIUM CONCENTRATION [MOLES/LITER]
C       CASO4      I    CALCIUM SULFATE CONC [MOLES/LITER]
C       CO3        I    CARBONATE CONCENTRATION [MOLES/LITER]
C       CO3I       I    INITIAL CARBONATE CONCENTRATION [MOLES/GRAM SOIL]
C       CO3IS      I    INITIAL CARBONATE CONCENTRATION [MOLES/LITER]
C       EAL        I    EXCHANGABLE IONS OF ALUMINUM
C       ECA        I    EXCHANGABLE IONS OF CALCIUM
C       EK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       EMG        I    EXCHANGABLE IONS OF MAGNESIUM
C       ENA        I    EXCHANGABLE IONS OF SODIUM
C       ENH4       I    EXCHANGABLE IONS OF AMMONIA
C       GAM1       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM2       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       GAM3       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF THREE
C       GIBSIT     I    LOGICAL FLAG IF GIBBSITE PRESENT
C       GYPSUM     I    LOGICAL FLAG IF GYPSUM PRESENT
C       H          I    HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3       I    BICARBONATE CONCENTRATION [MOLES/LITER]
C       HCO3I      I    INITIAL BICARB CONCENTRATION [MOLES/GRAM SOIL]
C       HCO3IS     I    INITIAL BICARBONATE CONCENTRATION [MOLES/LITER]
C       HI         I    INITIAL HYDROGEN CONCENTRATION [MOLES/GRAM SOIL]
C       HIS        I    INITIAL HYDROGEN CONCENTRATION [MOLES/LITER]
C       LIME       I    LOGICAL FLAG IF LIME PRESENT
C       ORG        I    ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       ORGI       I    INITIAL O.M. CONCENTRATION [MOLES/GRAM SOIL]
C       ORGIS      I    INITIAL O.M. CONCENTRATION [MOLES/LITER]
C       PCO2       I    PARTIAL PRESSURE OF CO2 [ATM]
C       SK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SO4        I    SULFATE CONCENTRATION [MOLES/LITER]
C       SO4I       I    INITIAL SULFATE CONCENTRATION [MOLES/GRAM SOIL]
C       SO4IS      I    INITIAL SULFATE CONCENTRATION [MOLES/LITER]
C       XMG        I    MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGI       I    INITIAL MAGNESIUM CONC [MOLES/GRAM SOIL]
C       XMGIS      I    INITIAL MAGNESIUM CONC [MOLES/LITER]
C       XMGSO4     I    MAGNESIUM SULFATE CONC [MOLES/LITER]
C       XNA        I    SODIUM CONCENTRATION [MOLES/LITER]
C       XNAI       I    INITIAL SODIUM CONCENTRATION [MOLES/GRAM SOIL]
C       XNAIS      I    INITIAL SODIUM CONCENTRATION [MOLES/LITER]
C       XNAORG     I    ORGANIC SODIUM CONCENTRATION [MOLES/LITER]
C       XNASIS     I    INITIAL SODIUM SULFATE CONCENTRATION [MOLES/LITER]
C       XNASO4     I    SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4       I    AMMONIA CONCENTRATION [MOLES/LITER]
C       XNH4I      I    INITIAL AMMONIA CONCENTRATION [MOLES/GRAM SOIL]
C
C
C       COMMENTS:
C
C
C       EXTERNAL REFERENCES:
C
C       CALLED FROM: SCACD
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:   1.0
C
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
      LOGICAL LIME,GYPSUM,GIBSIT
      DIMENSION B(NUMR)
C
C-----EQUATION FOR CALCIUM SULFATE ION PAIR.
C
      G1=1.0D0/(GAM2*GAM2)
      C1=4.39D-3*G1
      C2=4.37D-3*G1
      C3=1.905D-1/GAM2
      C4=1.2589D-8/GAM1**2.0D0*PCO2
      C5=5.012D-11/GAM2
      C6=CAIS
      C7=SO4IS
      C8=XMGIS
      C9=XNAIS
      C10=XNASIS
      C11=HCO3IS
      C12=HIS
      C13=CO3IS
      C14=XNH4IS
      C15=ORGIS
      C16=SK
      C17=EK
      C18=CAI
      C19=0.676D0/GAM2**3.D0*GAM3**2.D0
      C20=8.7D-9*G1
      C21=SO4I
      C22=2.29D-5*G1
      C23=XMGI
      C24=XNAI
      C25=1.047D5/GAM3*GAM1*GAM2
      C26=XNH4I
      C27=6.31D-4/GAM3/GAM2*GAM1
      C28=1.0965D8*GAM1**3.D0/GAM3
      C29=1.0D6/GAM3/GAM1*GAM2
C     
C-----EQUATION TO REACT CARBON DIOXIDE WITH WATER AT A FIXED PCO2.
C     
      B(1)=HCO3*H-C4
C     
C-----EQUATION FOR DISSICIATION OF HCO3 TO FORM CO3 AND H.
C     
      B(2)=CO3*H/HCO3-C5
C     
C-----CHARGE BALANCE EQUATION FOR THIS VARIABLE SET
C-----NOTE THAT THE NET CHANGE IN CHARGE EQUALS ZERO, RATHER THAN
C-----THE NET CHARGE ZERO.  THIS ALLOWS THE INITIAL XNALYSIS TO BE
C-----OUT OF BALANCE AND STILL COMPUTE THE CORRECT CHANGES TO ACHIEVE
C-----EQUILIBRIUM.
C     
      IF(GYPSUM) THEN
        B(3)=(CA-C6)-(SO4-C7)+(XMG-C8)+0.5D0*(XNA-C9)-0.5D0*((XNA*SO4/C3
     +      )-C10)-0.5D0*(HCO3-C11)+0.5D0*(H-C12)-(CO3-C13)+0.5D0*(XNH4-
     +      C14)-0.5D0*(ORG-C15)+1.5D0*(AL-ALIS)+(ALOH-ALOHIS)+0.5D0*(
     +      ALSO4-ALSOIS)+(ALORG-ALORIS)
      ELSE
        B(3)=(CA-C6)-(SO4-C7)+(XMG-C8)+0.5D0*(XNA-C9)-0.5D0*(XNASO4-C10)
     +      -0.5D0*(HCO3-C11)+0.5D0*(H-C12)-(CO3-C13)+0.5D0*(XNH4-C14)-
     +      0.5D0*(ORG-C15)+1.5D0*(AL-ALIS)+(ALOH-ALOHIS)+0.5D0*(ALSO4-
     +      ALSOIS)+(ALORG-ALORIS)
      ENDIF
C     
C-----IF THIS IS NOT A GYPSUM NOR LIME SYSTEM, USE THE FOLLOWING
C-----EQUATION.  OTHERWISE, TEST TO SEE IF LIME IS ABSENT, BUT
C-----GYPSUM IS PRESENT.
C     
      IF(.NOT.LIME.AND..NOT.GYPSUM) THEN
C       
C-----EQUATION FOR CALCIUM MASS BALANCE WHEN LIME AND GYPSUM ARE
C-----ABSENT.
C       
        B(4)=C16*(CA+CASO4)+C17*0.5D0*ECA-C18
      ELSEIF(.NOT.LIME.AND.GYPSUM) THEN
C       
C-----EQUATION FOR CALCIUM MASS BALANCE WHEN LIME IS ABSENT, BUT
C-----GYPSUM IS PRESENT.
C       
        B(4)=C16*(CA-SO4-(XMG*SO4/C2)-(XNA*SO4/C2)-ALSO4)+C17*0.5D0*ECA+
     +      C21-C18
      ELSE
C       
C-----USE THIS EQUATION FOR A LIME SYSTEM
C       
        B(4)=CA*CO3-C20
      ENDIF
C     
C-----IF GYPSUM IS ABSENT, USE THIS EQUATION, OTHERWISE USE
C-----SOLUBILITY PRODUCT FOR GYPSUM.
C     
      IF(.NOT.GYPSUM.AND..NOT.LIME) THEN
C       
C-----EQUATION FOR SULFATE MASS BALANCE IN THE ABSENCE OF GYPSUM.
C       
        B(5)=C16*(SO4+ALSO4+CASO4+XNASO4+XMGSO4)-C21
C     
      ELSEIF(.NOT.GYPSUM.AND.LIME) THEN
C       
        B(5)=C16*(SO4+(CA*SO4/C1)+ALSO4+XNASO4+XMGSO4)-C21
      ELSE
C       
C-----SOLUBILITY PRODUCT RELATIONSHIP FOR GYPSUM.
C       
        B(5)=CA*SO4-C22
      ENDIF
C     
C-----MASS BALANCE EQUATION FOR MAGNESIUM.
C     
      IF(.NOT.GYPSUM) THEN
C       
        B(6)=C16*(XMG+(XMG*SO4/C2))+C17*0.5D0*EMG-C23
C     
      ELSE
C       
        B(6)=C16*(XMG+XMGSO4)+C17*0.5D0*EMG-C23
C     
      ENDIF
C     
C-----MASS BALANCE EQUATION FOR SODIUM.
C     
      IF(.NOT.GYPSUM) THEN
C       
        B(7)=C16*(XNA+XNASO4+XNAORG)+C17*ENA-C24
C     
      ELSE
C       
        B(7)=C16*(XNA+(XNA*SO4/C3)+XNAORG)+C17*ENA-C24
C     
      ENDIF
C     
C-----MASS BALANCE EQUATION FOR NH4.
C     
      B(8)=C16*XNH4+C17*ENH4-C26
C     
C-----EQUATION FOR ALOH ION PAIR
C     
      B(9)=AL/(H*ALOH)-C25
C     
C-----EQUATION FOR ALSO4 ION PAIR
C     
      B(10)=AL*SO4/ALSO4-C27
C     
C-----EQUATION FOR GIBBSITE SOLUBILITY
C     
      IF(GIBSIT) THEN
        B(11)=AL/H**3.0D0-C28
      ELSE
        B(11)=C16*(AL+ALOH+ALSO4+ALORG)+C17/3.0D0*EAL-ALI
      ENDIF
C     
C-----CALCULATE MASS BALANCE FOR HCO3, CO3, AND H.
C     
      IF(LIME) THEN
        IF(GYPSUM.AND.GIBSIT) THEN
C         
          B(12)=(CO3-C13)-(((CA+(CA*SO4/C1)+ECA*0.5D0*C17/C16)-C18/C16)-
     +        (SO4+(CA*SO4/C1)+(XMG*SO4/C2)+(XNA*SO4/C3)+ALSO4-C21/C16))
     +        -((H-C12)+3.0D0*(AL+ALSO4+ALORG+ALOH+EAL/3.0D0*C17/C16-ALI
     +        /C16))*0.5D0+(HCO3-C11)*0.5D0
C       
        ELSEIF(GYPSUM.AND..NOT.GIBSIT) THEN
C         
          B(12)=(CO3-C13)-(((CA+(CA*SO4/C1)+ECA*0.5D0*C17/C16)-C18/C16)-
     +        (SO4+(CA*SO4/C1)+(XMG*SO4/C2)+(XNA*SO4/C3)+ALSO4-C21/C16))
     +        -(H-C12)*0.5D0+(HCO3-C11)*0.5D0
C       
        ELSEIF(.NOT.GYPSUM.AND.GIBSIT) THEN
C         
          B(12)=(CO3-C13)-((CA+(CA*SO4/C1)+ECA*0.5D0*C17/C16)-C18/C16)-(
     +        (H-C12)+3.D0*(AL+ALSO4+ALORG+ALOH+EAL/3.0D0*C17/C16-ALI/
     +        C16))*0.5D0+(HCO3-C11)*0.5D0
C       
        ELSE
C         
          B(12)=(CO3-C13)-((CA+(CA*SO4/C1)+ECA*0.5D0*C17/C16)-C18/C16)-(
     +        H-C12)*0.5D0+(HCO3-C11)*0.5D0
        ENDIF
C     
      ELSE
C       
        IF(GIBSIT) THEN
          B(12)=(CO3-C13)-((H-C12)+3.0D0*(AL+ALSO4+ALORG+ALOH+EAL/3.0D0*
     +        C17/C16-ALI/C16))*0.5D0+(HCO3-C11)*0.5D0
C       
        ELSE
C         
          B(12)=(CO3-C13)-(H-C12)*0.5D0+(HCO3-C11)*0.5D0
        ENDIF
C     
      ENDIF
C     
C-----EQUATION FOR ALUMINUM ORGANIC COMPLEX
C     
      B(13)=AL*ORG/ALORG-C29
C     
C-----EQUATION FOR CA-NA ION EXCHANGE.
C     
      B(14)=SQRT(CA)/XNA*ENA/ECA-0.707D0/SQRT(GAM2)*GAM1
C     
C-----EQUATION FOR CA-MG ION EXCHANGE
C     
      B(15)=CA/XMG*EMG/ECA-0.67D0
C     
C-----EQUATION FOR NA-NH4 ION EXCHANGE.
C     
      B(16)=XNA/XNH4*ENH4/ENA-0.22D0
C     
C-----EQUATION FOR CA-AL ION EXCHANGE
C     
      B(17)=CA**3.0D0*EAL**2.0D0/(AL**2.0D0*ECA**2.0D0)-C19
C     
C-----MASS BALANCE EQUATION FOR EXCHANGABLE IONS.
C     
      B(18)=ECA+EMG+ENA+ENH4+EAL-1.0D0
C     
      RETURN
      END
C
      SUBROUTINE ALKCOF(A,EK,ECA,ENA,ENH4,EMG,GYPSUM,LIME,NUMR,SK)
C
C======================================================================
C
C       PURPOSE:  THIS SUBROUTINE CALCULATES THE PARTIAL DERIVATIVES
C         OF EACH EQUATION IN THE ALKALINE SYSTEM.  THESE VALUES ARE
C         USED TO BUILD THE JACOBIAN MATRIX USED IN THE MATRIX SOLVER.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       A          O    JACOBIAN MATRIX OF PARTIAL DERIVATIVES
C       CA         I    CALCIUM CONCENTRATION [MOLES/LITER]
C       CASO4      I    CALCIUM SULFATE CONC [MOLES/LITER]
C       CO3        I    CARBONATE CONCENTRATION [MOLES/LITER]
C       ECA        I    EXCHANGABLE IONS FOR CALCIUM
C       EK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       EMG        I    EXCHANGABLE IONS FOR MAGNESIUM
C       ENA        I    EXCHANGABLE IONS FOR SODIUM
C       ENH4       I    EXCHANGABLE IONS FOR AMMONIA
C       GYPSUM     I    LOGICAL FLAG INDICATING IF SOLID GYPSUM PRESENT
C       H          I    HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3       I    BICARBONATE CONCENTRATION [MOLES/LITER]
C       I          I    COUNTER VARIABLE
C       J          I    COUNTER VARIABLE
C       LIME       I    LOGICAL FLAG INDICATING IF SOLID LIME PRESENT
C       NUMR       I    NUMBER OF ALKALINE CONSTITUENTS AND EQ. EQUATIONS
C       ORG        I    ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       SK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SO4        I    SULFATE CONCENTRATION [MOLES/LITER]
C       XMG        I    MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGSO4     I    MAGNESIUM SULFATE CONC [MOLES/LITER]
C       XNA        I    SODIUM CONCENTRATION [MOLES/LITER]
C       XNAORG     I    SODIUM ORGANIC COMPLEX CONCENTRATION [MOLES/LITER]
C       XNASO4     I    SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4       I    AMMONIA CONCENTRATION [MOLES/LITER]
C
C
C       COMMENTS:
C
C       MASS STORAGE FILES:
C
C
C       EXTERNAL REFERENCES:
C
C
C       CALLED FROM:
C
C       PROGRAMMER:
C
C       VERSION:
C
C======================================================================
C
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
      DIMENSION A(NUMR,NUMR)
      LOGICAL LIME,GYPSUM
C
      SQRTCA=SQRT(CA)
C     
C     ..INITIALIZE JOCOBIAN MATRIX
C     
      DO 20 I=1,NUMR
        DO 10 J=1,NUMR
          A(I,J)=0.0D0
   10   CONTINUE
   20 CONTINUE
C     
      A(1,1)=SO4/CASO4
      A(1,7)=CA/CASO4
      A(1,2)=-CA*SO4/CASO4**2.0D0
C     
      A(2,7)=XMG/XMGSO4
      A(2,8)=SO4/XMGSO4
      A(2,9)=-XMG*SO4/XMGSO4**2.0D0
C     
      A(3,7)=XNA/XNASO4
      A(3,10)=SO4/XNASO4
      A(3,11)=-XNA*SO4/XNASO4**2.0D0
C     
      A(4,5)=H
      A(4,4)=HCO3
C     
      A(5,4)=CO3/HCO3
      A(5,5)=-CO3*H/HCO3**2.0D0
      A(5,3)=H/HCO3
C     
      A(6,1)=1.D0
      A(6,3)=-1.0D0
      A(6,4)=0.5D0
      A(6,5)=-0.5D0
      A(6,6)=-0.5D0
      A(6,7)=-1.0D0
      A(6,8)=1.0D0
      A(6,10)=0.5D0
      A(6,11)=-0.5D0
      A(6,12)=0.5D0
C     
      IF(.NOT.LIME.AND..NOT.GYPSUM) THEN
C       
C-----CALCIUM MASS BALANCE DERIVATIVES FOR A NON-LIME, NON-
C-----GYPSUM SYSTEM.
C       
        A(7,1)=SK
        A(7,2)=SK
        A(7,14)=EK*0.5D0
C     
C-----CALCIUM MASS BALANCE FOR A GYPSUM SYSTEM
C     
      ELSEIF(.NOT.LIME.AND.GYPSUM) THEN
        A(7,1)=SK
        A(7,7)=-SK
        A(7,11)=-SK
        A(7,9)=-SK
        A(7,14)=EK*0.5D0
      ELSE
        A(7,1)=CO3
        A(7,3)=CA
      ENDIF
C     
      IF(.NOT.GYPSUM) THEN
        A(8,2)=SK
        A(8,9)=SK
        A(8,11)=SK
        A(8,7)=SK
      ELSE
        A(8,1)=SO4
        A(8,7)=CA
      ENDIF
C     
C-----MAGNESIUM MASS BALANCE DERIVATIVES.
C     
      A(9,8)=SK
      A(9,9)=SK
      A(9,16)=EK*0.5D0
C     
C-----SODIUM MASS BALANCE DERIVATIVES.
C     
      A(10,13)=SK
      A(10,11)=SK
      A(10,10)=SK
      A(10,15)=EK
C     
C-----NH4 MASS BALANCE DERIVATIVES.
C     
      A(11,12)=SK
      A(11,17)=EK
C     
      IF(LIME.AND.GYPSUM) THEN
        A(12,1)=-1.0D0
        A(12,3)=1.0D0
        A(12,4)=-0.5D0
        A(12,5)=0.5D0
        A(12,7)=1.0D0
        A(12,9)=1.0D0
        A(12,11)=1.0D0
        A(12,14)=-0.5D0*EK/SK
      ELSEIF(LIME.AND..NOT.GYPSUM) THEN
        A(12,1)=-1.0D0
        A(12,2)=-1.0D0
        A(12,3)=1.0D0
        A(12,4)=-0.5D0
        A(12,5)=0.5D0
        A(12,14)=-0.5D0*EK/SK
      ELSE
        A(12,3)=1.0D0
        A(12,4)=-0.5D0
        A(12,5)=0.5D0
      ENDIF
C     
      A(13,6)=XNA/XNAORG
      A(13,10)=ORG/XNAORG
      A(13,13)=-XNA*ORG/XNAORG**2.0D0
C     
C-----BEGIN DERIVATIVES FOR ION EXCHANGE EQUATIONS.
C     
      A(14,1)=0.5D0*ENA/(SQRTCA*XNA*ECA)
      A(14,10)=-SQRTCA*ENA/(ECA*XNA**2.0D0)
      A(14,15)=SQRTCA/(XNA*ECA)
      A(14,14)=-SQRTCA*ENA/(ECA**2.0D0*XNA)
C     
      A(15,1)=EMG/(XMG*ECA)
      A(15,8)=-CA*EMG/(ECA*XMG**2.0D0)
      A(15,16)=CA/(XMG*ECA)
      A(15,14)=-CA*EMG/(ECA**2.0D0*XMG)
C     
      A(16,10)=ENH4/(XNH4*ENA)
      A(16,17)=XNA/(XNH4*ENA)
      A(16,12)=-XNA*ENH4/(ENA*XNH4**2.0D0)
      A(16,15)=-XNA/XNH4*ENH4/ENA**2.0D0
C     
      A(17,14)=1.0D0
      A(17,15)=1.0D0
      A(17,16)=1.0D0
      A(17,17)=1.0D0
C     
      RETURN
      END
C
      SUBROUTINE ALKEXC(CA,ECA,ENA,EMG,ENH4,EAL,GAM1,GAM2,XNA,XMG,XNH4)
C
C======================================================================
C
C       PURPOSE:  THIS FUNCTION CALCULATES EXCHANGABLE IONS OF CA,NA,
C         MG, AND NH4 USED IN CALCULATIONS FOR THE ALKALINE SYSTEM. THE
C         VALUES ARE EXPLICITELY CALCULATED ON EVERY ITERATION STEP.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       CA         I    CALCIUM CONCENTRATION [MOLES/LITER]
C       EAL        O    EXCHANGABLE IONS OF ALUMINUM
C       ECA        O    EXCHANGABLE IONS OF CALCIUM
C       EMG        O    EXCHANGABLE IONS OF MAGNESIUM
C       ENA        O    EXCHANGABLE IONS OF SODIUM
C       ENA1            TEMPORARY STORAGE OF COEFFICIENTS
C       ENA2            TEMPORARY STORAGE OF COEFFICIENTS
C       ENA3            TEMPORARY STORAGE OF COEFFICIENTS
C       ENH4            EXCHANGABLE IONS OF AMMONIA
C       GAM1       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM2       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       XMG        I    MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XNA        I    SODIUM CONCENTRATION [MOLES/LITER]
C
C
C       COMMENTS:
C
C       EXTERNAL REFERENCES:
C
C
C       CALLED FROM:  SCINIT, SCALK
C
C       PROGRAMMER:
C
C       VERSION:
C
C======================================================================
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C     ..DEFINE CONSTANTS
      C1=0.707D0*GAM1/SQRT(GAM2)
      SQRTCA=SQRT(CA)
      ENA1=SQRTCA/(XNA*C1)
      ENA2=0.67D0*XMG/(SQRTCA*XNA*C1)
      ENA3=0.22D0*XNH4/XNA
C     
C     ..SOLVE FOR EXCHANGABLE IONS FOR SODIUM
      ENA=1.0D0/(ENA1+ENA2+ENA3+1.0D0)
C     
C     ..SOLVE FOR THE OTHER EXCHANGABLE IONS IN THE ALKALINE SYSTEM
      ECA=ENA1*ENA
      EMG=ENA2*ENA
      ENH4=ENA3*ENA
      EAL=1.0D-15
C     
      RETURN
      END
C
      SUBROUTINE ALKUPD(ECA,ENA,EMG,ENH4,X,NUMR)
C
C======================================================================
C
C       PURPOSE:  THIS SUBROUTINE TESTS AND CORRECTS SOLUTION ESTIMATES
C         FOR MASS BALANCE UNDERRUNS.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       CA        I/O   CALCIUM CONCENTRATION [MOLES/LITER]
C       CASO4     I/O   CALCIUM SULFATE CONC [MOLES/LITER]
C       CO3       I/O   CARBONATE CONCENTRATION [MOLES/LITER]
C       ECA        I    EXCHANGABLE IONS FOR CALCIUM
C       EMG        I    EXCHANGABLE IONS FOR MAGNESIUM
C       ENA        I    EXCHANGABLE IONS FOR SODIUM
C       ENH4       I    EXCHANGABLE IONS FOR AMMONIA
C       H         I/O   HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3      I/O   BICARBONATE CONCENTRATION [MOLES/LITER]
C       I               COUNTER VARIABLE
C       ORG       I/O   ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       OVER            FLAG USED FOR UNDERRUN
C       SO4       I/O   SULFATE CONCENTRATION [MOLES/LITER]
C       X          I    SOLUTION VECTOR OF CONCENTRATION CORRECTIONS
C       XMG       I/O   MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGSO4    I/O   MAGNESIUM SULFATE CONC [MOLES/LITER]
C       XNA       I/O   SODIUM CONCENTRATION [MOLES/LITER]
C       XNAORG    I/O   SODIUM ORGANIC COMPLEX CONCENTRATION [MOLES/LITER]
C       XNASO4    I/O   SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4      I/O   AMMONIA CONCENTRATION [MOLES/LITER]
C
C
C       COMMENTS:
C
C       MASS STORAGE FILES:
C
C
C       EXTERNAL REFERENCES:
C
C
C       CALLED FROM:
C
C       PROGRAMMER:
C
C       VERSION:
C
C======================================================================
C
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
      DIMENSION X(NUMR)
      LOGICAL OVER
C
      OVER=.FALSE.
C     
C-----TEST AND CORRECT SOLUTION ESTIMATES FOR MASS BALANCE
C-----OVERRUNS.  KEEP ALL THE CONCENTRATIONS POSITIVE.
C     
   10 CONTINUE
C     
      IF(CA+X(1).LE.0.0D0.AND.X(1).LT.0.0D0) OVER=.TRUE.
      IF(CASO4+X(2).LE.0.0D0.AND.X(2).LT.0.0D0) OVER=.TRUE.
      IF(CO3+X(3).LE.0.0D0.AND.X(3).LT.0.0D0) OVER=.TRUE.
      IF(H+X(4).LE.0.0D0.AND.X(4).LT.0.0D0) OVER=.TRUE.
      IF(HCO3+X(5).LE.0.0D0.AND.X(5).LT.0.0D0) OVER=.TRUE.
      IF(ORG+X(6).LE.0.0D0.AND.X(6).LT.0.0D0) OVER=.TRUE.
      IF(SO4+X(7).LE.0.0D0.AND.X(7).LT.0.0D0) OVER=.TRUE.
      IF(XMG+X(8).LE.0.0D0.AND.X(8).LT.0.0D0) OVER=.TRUE.
      IF(XMGSO4+X(9).LE.0.0D0.AND.X(9).LT.0.0D0) OVER=.TRUE.
      IF(XNA+X(10).LE.0.0D0.AND.X(10).LT.0.0D0) OVER=.TRUE.
      IF(XNASO4+X(11).LE.0.0D0.AND.X(11).LT.0.0D0) OVER=.TRUE.
      IF(XNH4+X(12).LE.0.0D0.AND.X(12).LT.0.0D0) OVER=.TRUE.
      IF(XNAORG+X(13).LE.0.0D0.AND.X(13).LT.0.0D0) OVER=.TRUE.
      IF(ECA+X(14).LE.0.0D0.AND.X(14).LT.0.0D0) OVER=.TRUE.
      IF(ENA+X(15).LE.0.0D0.AND.X(15).LT.0.0D0) OVER=.TRUE.
      IF(EMG+X(16).LE.0.0D0.AND.X(16).LT.0.0D0) OVER=.TRUE.
      IF(ENH4+X(17).LE.0.0D0.AND.X(17).LT.0.0D0) OVER=.TRUE.
C     
      IF(OVER) THEN
        DO 20 I=1,17
          X(I)=X(I)*0.50D0
   20   CONTINUE
        OVER=.FALSE.
        GOTO 10
      ELSE
        CA=CA+X(1)
        CASO4=CASO4+X(2)
        CO3=CO3+X(3)
        H=H+X(4)
        HCO3=HCO3+X(5)
        ORG=ORG+X(6)
        SO4=SO4+X(7)
        XMG=XMG+X(8)
        XMGSO4=XMGSO4+X(9)
        XNA=XNA+X(10)
        XNASO4=XNASO4+X(11)
        XNH4=XNH4+X(12)
        XNAORG=XNAORG+X(13)
        ECA=ECA+X(14)
        ENA=ENA+X(15)
        EMG=EMG+X(16)
        ENH4=ENH4+X(17)
      ENDIF
C     
      RETURN
      END
C
      SUBROUTINE ALKVTR(B,ECA,ENA,ENH4,EMG,GAM1,GAM2,SK,EK,PCO2,LIME,
     +    GYPSUM,NUMR)
C
C======================================================================
C
C       PURPOSE:  COMPUTE FUNCTIONAL SOLUTION VECTOR FOR THE ALKALINE
C         SYSTEM.  THIS SUBROUTINE IS ONLY ENTERED WITH A PH GREATER
C         THAN OR EQUAL TO 6.0
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       ALSO4      I    ALUMINUM SULFATE CONCENTRATION [MOLES/LITER]
C       B          O    FUNCTIONAL SOLUTION VECTOR
C       CA         I    CALCIUM CONCENTRATION [MOLES/LITER]
C       CAI        I    INITIAL CALCIUM CONCENTRATION [MOLES/GRAM SOIL]
C       CAIS       I    INITIAL CALCIUM CONCENTRATION [MOLES/LITER]
C       CASO4      I    CALCIUM SULFATE CONC [MOLES/LITER]
C       CO3        I    CARBONATE CONCENTRATION [MOLES/LITER]
C       CO3I       I    INITIAL CARBONATE CONCENTRATION [MOLES/GRAM SOIL]
C       CO3IS      I    INITIAL CARBONATE CONCENTRATION [MOLES/LITER]
C       ECA        I    EXCHANGABLE IONS FOR CALCIUM
C       EK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       EMG        I    EXCHANGABLE IONS FOR MAGNESIUM
C       ENA        I    EXCHANGABLE IONS FOR SODIUM
C       ENH4       I    EXCHANGABLE IONS FOR AMMONIA
C       GAM1       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM2       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       GYPSUM     I    LOGICAL FLAG INDICATING IF SOLID GYPSUM PRESENT
C       H          I    HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3       I    BICARBONATE CONCENTRATION [MOLES/LITER]
C       HCO3I      I    INITIAL BICARB CONCENTRATION [MOLES/GRAM SOIL]
C       HCO3IS     I    INITIAL BICARBONATE CONCENTRATION [MOLES/LITER]
C       HI         I    INITIAL HYDROGEN CONCENTRATION [MOLES/GRAM SOIL]
C       HIS        I    INITIAL HYDROGEN CONCENTRATION [MOLES/LITER]
C       LIME       I    LOGICAL FLAG INDICATING IF SOLID LIME PRESENT
C       ORG        I    ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       ORGIS      I    INITIAL O.M. CONCENTRATION [MOLES/LITER]
C       PCO2       I    PARTIAL PRESSURE OF CO2 [ATM]
C       SK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SO4        I    SULFATE CONCENTRATION [MOLES/LITER]
C       SO4I       I    INITIAL SULFATE CONCENTRATION [MOLES/GRAM SOIL]
C       SO4IS      I    INITIAL SULFATE CONCENTRATION [MOLES/LITER]
C       XMG        I    MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGI       I    INITIAL MAGNESIUM CONC [MOLES/GRAM SOIL]
C       XMGIS      I    INITIAL MAGNESIUM CONC [MOLES/LITER]
C       XMGSO4     I    MAGNESIUM SULFATE CONC [MOLES/LITER]
C       XNA        I    SODIUM CONCENTRATION [MOLES/LITER]
C       XNAI       I    INITIAL SODIUM CONCENTRATION [MOLES/GRAM SOIL]
C       XNAIS      I    INITIAL SODIUM CONCENTRATION [MOLES/LITER]
C       XNAORG     I    SODIUM ORGANIC COMPLEX CONCENTRATION [MOLES/LITER]
C       XNASIS     I    INITIAL SODIUM SULFATE CONCENTRATION [MOLES/LITER]
C       XNASO4     I    SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4       I    AMMONIA CONCENTRATION [MOLES/LITER]
C       XNH4I      I    INITIAL AMMONIA CONCENTRATION [MOLES/GRAM SOIL]
C       XNH4IS     I    INITIAL AMMONIA CONCENTRATION [MOLES/LITER]
C
C
C       COMMENTS:
C
C       CALLED FROM:
C
C       PROGRAMMER:   KEN ROJAS
C
C       VERSION:  1.0
C
C======================================================================
C
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
      LOGICAL LIME,GYPSUM
      DIMENSION B(NUMR)
C
C-----EQUATION FOR CALCIUM SULFATE ION PAIR.
C
      C1=4.39D-3/GAM2**2.0D0
      C2=4.37D-3/GAM2**2.0D0
      C3=1.905D-1/GAM2
      C4=1.2589D-8/GAM1**2.0D0*PCO2
      C5=5.012D-11/GAM2
      C6=CAIS
      C7=SO4IS
      C8=XMGIS
      C9=XNAIS
      C10=XNASIS
      C11=HCO3IS
      C12=HIS
      C13=CO3IS
      C14=XNH4IS
      C15=ORGIS
      C16=SK
      C17=EK
      C18=CAI
      C19=8.7D-9/GAM2**2.0D0
      C20=SO4I
      C21=2.29D-5/GAM2**2.0D0
      C22=XMGI
      C23=XNAI
      C24=XNH4I
C     
      B(1)=CA*SO4/CASO4-C1
C     
C-----EQUATION FOR MAGNESIUM SULFATE ION PAIR.
C     
      B(2)=XMG*SO4/XMGSO4-C2
C     
C-----EQUATION FOR SODIUM SULFATE ION PAIR.
C     
      B(3)=XNA*SO4/XNASO4-C3
C     
C-----EQUATION TO REACT CARBON DIOXIDE WITH WATER AT A FIXED PCO2.
C     
      B(4)=HCO3*H-C4
C     
C-----EQUATION FOR DISSICIATION OF HCO3 TO FORM CO3 AND H.
C     
      B(5)=CO3*H/HCO3-C5
C     
C-----CHARGE BALANCE EQUATION FOR THIS VARIABLE SET
C-----NOTE THAT THE NET CHANGE IN CHARGE EQUALS ZERO, RATHER THAN
C-----THE NET CHARGE ZERO.  THIS ALLOWS THE INITIAL XNALYSIS TO BE
C-----OUT OF BALANCE AND STILL COMPUTE THE CORRECT CHANGES TO ACHIEVE
C-----EQUILIBRIUM.
C     
      B(6)=(CA-C6)-(SO4-C7)+(XMG-C8)+0.5D0*(XNA-C9)-0.5D0*(XNASO4-C10)-
     +    0.5D0*(HCO3-C11)+0.5D0*(H-C12)-(CO3-C13)+0.5D0*(XNH4-C14)-
     +    0.5D0*(ORG-C15)
C     
C-----IF THIS IS NOT A GYPSUM NOR LIME SYSTEM, USE THE FOLLOWING
C-----EQUATION.  OTHERWISE, TEST TO SEE IF LIME IS ABSENT, BUT
C-----GYPSUM IS PRESENT.
C     
      IF(.NOT.LIME.AND..NOT.GYPSUM) THEN
C       
C-----EQUATION FOR CALCIUM MASS BALANCE WHEN LIME AND GYPSUM ARE
C-----ABSENT.
C       
        B(7)=C16*(CA+CASO4)+C17*0.5D0*ECA-C18
      ELSEIF(.NOT.LIME.AND.GYPSUM) THEN
C       
C-----EQUATION FOR CALCIUM MASS BALANCE WHEN LIME IS ABSENT, BUT
C-----GYPSUM IS PRESENT.
C       
        B(7)=C16*(CA-SO4-XMGSO4-XNASO4-ALSO4)+C17*0.5D0*ECA+C20-C18
      ELSE
C       
C-----USE THIS EQUATION FOR A LIME SYSTEM
C       
        B(7)=CA*CO3-C19
      ENDIF
C     
C-----IF GYPSUM IS ABSENT, USE THIS EQUATION, OTHERWISE USE
C-----SOLUBILITY PRODUCT FOR GYPSUM.
C     
      IF(.NOT.GYPSUM) THEN
C       
C-----EQUATION FOR SULFATE MASS BALANCE IN THE ABSENCE OF GYPSUM.
C       
        B(8)=C16*(SO4+CASO4+XMGSO4+XNASO4+ALSO4)-C20
      ELSE
C       
C-----SOLUBILITY PRODUCT RELATIONSHIP FOR GYPSUM.
C       
        B(8)=CA*SO4-C21
      ENDIF
C     
C-----MASS BALANCE EQUATION FOR MAGNESIUM.
C     
      B(9)=C16*(XMG+XMGSO4)+C17*0.5D0*EMG-C22
C     
C-----MASS BALANCE EQUATION FOR SODIUM.
C     
      B(10)=C16*(XNA+XNASO4+XNAORG)+C17*ENA-C23
C     
C-----MASS BALANCE EQUATION FOR NH4.
C     
      B(11)=C16*XNH4+C17*ENH4-C24
C     
C-----SOLVE FOR HCO3, CO3, AND H MASS BALANCE
C-----NOTE THAT IN THIS CASE THE PRESENCE OR ABSENCE OF GYPSUM MAKES
C-----NO DIFFERENCE AND WE STILL USE THE SAME EQUATION.
C     
      IF(LIME.AND.GYPSUM) THEN
        B(12)=(CO3-CO3I)-(((CA+CASO4+ECA*0.5D0*C17/C16)-C18/C16)-(SO4+
     +      CASO4+XMGSO4+XNASO4+ALSO4-C20/C16))-(H-HI)*0.5D0+(HCO3-HCO3I
     +      )*0.5D0
      ELSEIF(LIME.AND..NOT.GYPSUM) THEN
        B(12)=(CO3-CO3I)-((CA+CASO4+ECA*0.5D0*C17/C16)-C18/C16)-(H-HI)*
     +      0.5D0+(HCO3-HCO3I)*0.5D0
      ELSE
        B(12)=(CO3-CO3I)-(H-HI)*0.5D0+(HCO3-HCO3I)*0.5D0
      ENDIF
C     
C-----SOLVE FOR SODIUM AND ORGANIC MATTER COMPLEX
C     
      B(13)=XNA*ORG/XNAORG-1.0D6/GAM1**2.0D0
C     
C-----EQUATION FOR CA-NA ION EXCHANGE.
C     
      B(14)=SQRT(CA)/XNA*ENA/ECA-0.707D0*GAM1/SQRT(GAM2)
C     
C-----EQUATION FOR CA-MG ION EXCHANGE
C     
      B(15)=CA/XMG*EMG/ECA-0.67D0
C     
C-----EQUATION FOR NA-NH4 ION EXCHANGE.
C     
      B(16)=XNA/XNH4*ENH4/ENA-0.22D0
C     
C-----MASS BALANCE EQUATION FOR EXCHANGEABLE IONS.
C     
      B(17)=ECA+EMG+ENA+ENH4-1.0D0
C     
      RETURN
      END
C
      SUBROUTINE CHEM(NN,STREN,CC,BD,PCO2,TWC,RFDD,TLT)
C
C======================================================================
C
C       PURPOSE:  MAIN DRIVER ROUTINE FOR THE EQUILIBRIUM SOIL CHEMISTRY
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C
C
C       COMMENTS:
C
C
C       EXTERNAL REFERENCES:
C
C       CALLED FROM:
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION: 1.0
C
C======================================================================
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      PARAMETER(MXNOD=300,MXCHEM=15)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
      COMMON /SCHMIN/ ECA(MXNOD),ENA(MXNOD),EMG(MXNOD),ENH4(MXNOD),
     +    EAL(MXNOD),SCL(MXNOD,3),CEC(MXNOD)
C     
      SAVE AALOH,AALSO4,ACASO4,AMGSO4,ANASO4,ANAORG,AALORG,SCFLAG,AORG,
     +    IDAY
C
      LOGICAL LIME,GYPSUM,GIBSIT
      LOGICAL SCL,SCFLAG,ABORT
C
      DIMENSION AALOH(MXNOD),AALSO4(MXNOD),ACASO4(MXNOD),AMGSO4(MXNOD),
     +    ANASO4(MXNOD),ANAORG(MXNOD),AALORG(MXNOD),AORG(MXNOD),
     +    STREN(MXNOD),PCO2(MXNOD)
      DIMENSION CC(MXNOD,MXCHEM),BD(NN),TWC(NN),TLT(NN)
C
      DATA AALOH /MXNOD*0.0D0/,AALSO4 /MXNOD*0.0D0/,ACASO4 /MXNOD*0.0D0
     +    /,AMGSO4 /MXNOD*0.0D0/,ANASO4 /MXNOD*0.0D0/,ANAORG /MXNOD*
     +    0.0D0/,AALORG /MXNOD*0.0D0/,AORG /MXNOD*0.0D0/
      DATA SCFLAG /.TRUE./,IDAY /0/
C
C   --CYCLE THROUGH ALL THE SOIL LAYERS
      IF(RFDD.GT.0.0D0.OR.SCFLAG.OR.IDAY.EQ.5) THEN
        NNT=NN
        IDAY=0
C     OPEN (92, FILE='chem.out', STATUS='unknown')
      ELSE
        DO 10 I=1,NN
          IF(TLT(I).LE.300.0D0) NNT=I
   10   CONTINUE
        IDAY=IDAY+1
      ENDIF
      DO 20 I=1,NNT
C       PRINT*,I
        ALOH=AALOH(I)
        ALSO4=AALSO4(I)
        CASO4=ACASO4(I)
        XMGSO4=AMGSO4(I)
        XNASO4=ANASO4(I)
        XNAORG=ANAORG(I)
        ALORG=AALORG(I)
        ORG=AORG(I)
        AECA=ECA(I)
        AENA=ENA(I)
        AEMG=EMG(I)
        AENH4=ENH4(I)
        AEAL=EAL(I)
        H=CC(I,1)
        CA=CC(I,2)
        XNA=CC(I,3)
        XMG=CC(I,4)
        CL=CC(I,5)
        HCO3=CC(I,6)
        SO4=CC(I,7)
        AL=CC(I,8)
        XNO3=CC(I,9)
        XNH4=CC(I,10)
        CO3=CC(I,11)
        LIME=SCL(I,1)
        GYPSUM=SCL(I,2)
        GIBSIT=SCL(I,3)
        U=STREN(I)
        PPCO2=PCO2(I)
        CCEC=CEC(I)
C       
C       
C       IF (I.EQ.1)
C       + write(92,102)'init',ALOH,ALSO4,CASO4,XMGSO4,XNASO4,XNAORG,ALORG,
C       +  ORG,AECA,AENA,AEMG,AENH4,AEAL,H,CA,XNA,XMG,CL,HCO3,SO4,AL,XNO3,
C       +  XNH4,CO3,U,PPCO2,LIME,GYPSUM,GIBSIT 
C102    format(1x,a4,1x,26g9.2,3l6)
C       
        TH=TWC(I)
        RHOB=BD(I)
C       
C=====================================================
C       GET THE NEW EQUILIBRIUM VALUES FOR THIS SOIL LAYER
C=====================================================
C       
        CALL SCMAIN(RHOB,CCEC,CL,GIBSIT,GYPSUM,LIME,PPCO2,SCFLAG,PH,TH,
     +      XNO3,U,AECA,AENA,AEMG,AENH4,AEAL,ABORT)
C       
C       IF (I.EQ.1)
C       + write(92,102)'solv',ALOH,ALSO4,CASO4,XMGSO4,XNASO4,XNAORG,ALORG,
C       +  ORG,AECA,AENA,AEMG,AENH4,AEAL,H,CA,XNA,XMG,CL,HCO3,SO4,AL,XNO3,
C       +  XNH4,CO3,U,PPCO2,LIME,GYPSUM,GIBSIT 
C       
C       ..RETURN THE NEW EQUILIBRIATED VALUES
        IF(.NOT.ABORT) THEN
          CC(I,1)=H
          CC(I,2)=CA
          CC(I,3)=XNA
          CC(I,4)=XMG
          CC(I,5)=CL
          CC(I,6)=HCO3
          CC(I,7)=SO4
          CC(I,8)=AL
          CC(I,10)=XNH4
          CC(I,11)=CO3
          AALOH(I)=ALOH
          AALSO4(I)=ALSO4
          ACASO4(I)=CASO4
          AMGSO4(I)=XMGSO4
          ANASO4(I)=XNASO4
          ANAORG(I)=XNAORG
          AALORG(I)=ALORG
          AORG(I)=ORG
          ECA(I)=AECA
          ENA(I)=AENA
          EMG(I)=AEMG
          ENH4(I)=AENH4
          EAL(I)=AEAL
          STREN(I)=U
        ELSE
          ANAORG(I)=XNAORG
          AALORG(I)=ALORG
          AORG(I)=ORG
          STREN(I)=U
C       WRITE(79,1001) I
C1001   FORMAT(1X,//' LAYER # ',I4,/'INITIAL VALUES')
C       WRITE(79,1000)CC(I,1),CC(I,2)/40080.0D0,CC(I,3)/23000.0D0,
C       +        CC(I,4)/24300.0D0,CC(I,5)/35450.0D0,CC(I,6)/61018.0D0,
C       +        CC(I,7)/96060.0D0,CC(I,8)/26980.0D0,CC(I,9)/62000.0D0,
C       +        CC(I,10)/14000.0D0,CC(I,11)/60018.0D0
C       WRITE(79,1000)AALOH(I),AALSO4(I), ACASO4(I), AMGSO4(I), 
C       +        ANASO4(I), ANAORG(I), AALORG(I),AORG(I)
C       XCA   = CA   / 40080.0D0
C       XXNA  = XNA  / 23000.0D0
C       XXMG  = XMG  / 24300.0D0
C       XCL   = CL   / 35450.0D0
C       XHCO3 = HCO3 / 61018.0D0
C       XSO4  = SO4  / 96060.0D0
C       XAL   = AL   / 26980.0D0
C       XXNO3 = XNO3 / 62000.0D0
C       XXNH4 = XNH4 / 14000.0D0
C       XCO3  = CO3  / 60018.0D0
C       WRITE(79,1002)
C       WRITE(79,1000) H,XCA,XXNA,XXMG,XCL,XHCO3,XSO4,XAL,XXNH4,XXNO3,
C       +        XCO3
C       WRITE(79,1000)ALOH,ALSO4,CASO4,XMGSO4,XNASO4,XNAORG,ALORG,ORG
C1002   FORMAT(1X,/'RESULTANT VALUES')
C1000   FORMAT(11(1X,E10.4))
C       
        ENDIF
   20 CONTINUE
C     
      SCFLAG=.FALSE.
      RETURN
      END
C
      SUBROUTINE SCACD(ECA,ENA,EMG,ENH4,EAL,EK,GAM1,GAM2,GAM3,GYPSUM,
     +    GIBSIT,LIME,PCO2,SK,B,ABORT)
C
C======================================================================
C
C       PURPOSE:  MAIN DRIVING ROUTINE FOR THE ACID SUBSYSTEM.  THIS
C         SYSTEM IS ONLY ENTERED WHEN PH IS LESS THAN 6.0
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       A               JACOBIAN MATRIX OF PARTIAL DERIVATIVES
C       AUG             AUGMENTED MATRIX USED FOR TEMPORARY STORAGE
C       B          O    FUNCTIONAL SOLUTION VECTOR
C       EAL        I    EXCHANGABLE IONS FOR ALUMINUM
C       ECA        I    EXCHANGABLE IONS FOR CALCIUM
C       EK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       EMG        I    EXCHANGABLE IONS FOR MAGNESIUM
C       ENA        I    EXCHANGABLE IONS FOR SODIUM
C       ENH4       I    EXCHANGABLE IONS FOR AMMONIA
C       GAM1       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM2       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       GAM3       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF THREE
C       GIBSIT     I    LOGICAL FLAG INDICATING IF SOLID GIBBSITE PRESENT
C       GYPSUM     I    LOGICAL FLAG INDICATING IF SOLID GYPSUM PRESENT
C       LIME       I    LOGICAL FLAG INDICATING IF SOLID LIME PRESENT
C       PCO2       I    PARTIAL PRESSURE OF CO2 [ATM]
C       SK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SOLVE           LOGICAL FLAG INDICATING ERROR IN MATRIX SOLVER
C       X               SOLUTION VECTOR OF CONCENTRATION CORRECTIONS
C       Z               TEMPORARY STORAGE OF JOCABIAN MATRIX
C
C       COMMENTS:
C
C       EXTERNAL REFERENCES:
C
C       CALLED FROM:
C
C       PROGRAMMER:
C
C       VERSION:
C
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER(NUMR=18)
C
      DIMENSION A(NUMR,NUMR),B(NUMR),X(NUMR),AUG(NUMR,NUMR+1)
      LOGICAL SOLVE,LIME,GYPSUM,GIBSIT,ABORT
C
C     ..SET UP DETERMINANT VECTOR
      CALL ACDVTR(B,ECA,ENA,ENH4,EMG,EAL,GAM1,GAM2,GAM3,SK,EK,PCO2,LIME,
     +    GYPSUM,GIBSIT,NUMR)
C     
C     ..SET UP THE COEFFICIENT MATRIX CONTAINING PARTIAL DERIVATIVES
      CALL ACDCOF(A,EK,ECA,ENA,ENH4,EMG,EAL,GAM2,GYPSUM,LIME,GIBSIT,
     +    NUMR,SK)
C     CALL SHOWME(A,NUMR,NUMR)
C     WRITE (25,101) LIME,GYPSUM,GIBSIT
C     101   FORMAT(//1X,'LIME =',L4,'    GYPSUM =',L4,'    GIBSIT =',L4)
C     
C     ..SOLVE THE SIMULTANEOUS EQUATIONS
      SOLVE=.TRUE.
      CALL SOLVSL(AUG,A,B,X,NUMR,SOLVE)
C     
C     ..CHECK FOR SINGULAR SOLUTION
      IF(SOLVE) THEN
C       
C       ..CHECK FOR MASS BALANCE OVERRUNS AND UPDATE CONSTITUENTS
C       WRITE(2,1000)ECA,ENA,EMG,ENH4,EAL
C       WRITE(2,1000)CA,AL,CO3,H,HCO3,ORG,SO4,XMG,
C       1             ALSO4,XNA,ALOH,XNH4,ALORG
C       WRITE(2,1000)(B(I),I=1,NUMR)
C       WRITE(2,1000)(X(I),I=1,NUMR)
C       
        CALL ACDUPD(ECA,ENA,EMG,ENH4,EAL,X,GYPSUM,LIME,GAM2,NUMR)
C     
C     WRITE(3,1000)((A(I,J),J=1,NUMR),I=1,NUMR)
C     WRITE(2,1000)(X(I),I=1,NUMR)
C     WRITE(2,'(//)')
C     WRITE(3,'(//)')
      ELSE
C       
C       ..SKIP EXECUTION BECAUSE THE SOLUTION WAS SINGULAR
        PRINT*
        PRINT*,'=============================================='
        PRINT*,'ACID SYSTEM MATRIX IS SINGULAR SKIP CHEM EQUIL'
        PRINT*,'=============================================='
        PRINT*
        ABORT=.TRUE.
      ENDIF
C     
      RETURN
C     1000 FORMAT (13(1X,G9.3))
      END
C
      SUBROUTINE SCALK(ECA,ENA,EMG,ENH4,EK,GAM1,GAM2,GYPSUM,LIME,PCO2,
     +    SK,B,ABORT)
C
C======================================================================
C
C       PURPOSE: MAIN DRIVER ROUTINE FOR ALKALINE SUBSYSTEM.  THIS
C          SUBROUTINE IS ENTERED ONLY WHEN THE PH IS GREATER THAN OR
C          EQUAL TO 6.0
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       A               JACOBIAN MATRIX OF PARTIAL DERIVATIVES
C       AUG             AUGMENTED MATRIX USED FOR TEMP STORAGE.
C       B          O    FUNCTIONAL SOLUTION VECTOR
C       ECA        I    EXCHANGABLE IONS FOR CALCIUM
C       EK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       EMG        I    EXCHANGABLE IONS FOR MAGNESIUM
C       ENA        I    EXCHANGABLE IONS FOR SODIUM
C       ENH4       I    EXCHANGABLE IONS FOR AMMONIA
C       GAM1       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM2       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       GYPSUM     I    LOGICAL FLAG INDICATING IF SOLID GYPSUM PRESENT
C       LIME       I    LOGICAL FLAG INDICATING IF SOLID LIME PRESENT
C       PCO2       I    PARTIAL PRESSURE OF CO2 [ATM]
C       SK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SOLVE           LOGICAL FLAG INDICATING ERROR IN MATRIX SOLVER
C       X               SOLUTION VECTOR OF CONCENTRATION CORRECTIONS
C       Z               TEMPORARY STORAGE FOR THE JACOBIAN MATRIX
C
C       COMMENTS:
C
C       EXTERNAL REFERENCES:
C
C       CALLED FROM:
C
C       PROGRAMMER: KEN ROJAS
C
C       VERSION: 1.0
C
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER(NUMR=17)
C
      DIMENSION A(NUMR,NUMR),B(NUMR),X(NUMR),AUG(NUMR,NUMR+1)
      LOGICAL SOLVE,LIME,GYPSUM,ABORT
C
C     ..SET UP DETERMINANT VECTOR
      CALL ALKVTR(B,ECA,ENA,ENH4,EMG,GAM1,GAM2,SK,EK,PCO2,LIME,GYPSUM,
     +    NUMR)
C     
C     ..SET UP THE COEFFICIENT MATRIX CONTAINING PARTIAL DERIVATIVES
      CALL ALKCOF(A,EK,ECA,ENA,ENH4,EMG,GYPSUM,LIME,NUMR,SK)
C     WRITE (25,101) LIME,GYPSUM
C     101   FORMAT(//1X,'LIME =',L4,'    GYPSUM =',L4)
C     
C     ..SOLVE THE SIMULTANEOUS EQUATIONS
      SOLVE=.TRUE.
      CALL SOLVSL(AUG,A,B,X,NUMR,SOLVE)
C     
C     ..CHECK FOR SINGULAR SOLUTION
      IF(SOLVE) THEN
C       
C       ..CHECK FOR OVERRUNS IN MASS BALANCE AND UPDATE CONSTITUENTS
C       WRITE(2,1000)ECA,ENA,EMG,ENH4
C       WRITE(2,1000)CA,CASO4,CO3,H,HCO3,ORG,SO4,XMG,
C       1             XMGSO4,XNA,XNASO4,XNH4,XNAORG
C       WRITE(2,1000)(B(I),I=1,NUMR)
C       WRITE(2,1000)(X(I),I=1,NUMR)
C       
C       CALL TIMER('ALKUPD',0)
        CALL ALKUPD(ECA,ENA,EMG,ENH4,X,NUMR)
C     CALL TIMER('ALKUPD',1)
C     
C     WRITE(3,1000)((A(I,J),J=1,NUMR),I=1,NUMR)
C     WRITE(2,1000)(X(I),I=1,NUMR)
C     WRITE(2,'(//)')
C     WRITE(3,'(//)')
      ELSE
C       
C       ..SKIP EXECUTION, BECAUSE THE SOLUTION WAS SINGULAR
        PRINT*
        PRINT*,'=================================================='
        PRINT*,'ALKALINE SYSTEM MATRIX IS SINGULAR SKIP CHEM EQUIL'
        PRINT*,'=================================================='
        PRINT*
        ABORT=.TRUE.
      ENDIF
C     
      RETURN
C     1000 FORMAT (13(1X,G9.3))
      END
C
      SUBROUTINE SCCON(BD,CEC,CL,EK,PCO2,PPCO2,SCFLAG,SK,THETA,XNO3)
C
C======================================================================
C
C       PURPOSE:  USER SUPPLIED CONCENTRATIONS [MG/LITER] OF EACH
C         CONSTITUENT IS CONVERTED TO [MOLES/GRAM]
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       AL        I/O   ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALOH       O    ALUMINUM HYDROXIDE CONCENTRATION [MOLES/LITER]
C       ALORG      O    ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALSO4      O    ALUMINUM SULFATE CONCENTRATION [MOLES/LITER]
C       BD         I    BULK DENSITY OF THE SOIL [GRAM/CM^3]
C       CA        I/O   CALCIUM CONCENTRATION [MOLES/LITER]
C       CASO4      O    CALCIUM SULFATE CONC [MOLES/LITER]
C       CEC        I    CATION EXCHANGE CAPACITY [MEQ/100 GRAMS SOIL]
C       CL        I/O   CHLORIDE CONCENTRATION [MOLES/LITER]
C       CO3        O    CARBONATE CONCENTRATION [MOLES/LITER]
C       EK         O    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       H          O    HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3      I/O   BICARBONATE CONCENTRATION [MOLES/LITER]
C       ORG        O    ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       PCO2       O    PARTIAL PRESSURE OF CO2 [ATM]
C       PH         I    PH [STANDARD UNITS]
C       PPCO2      I    USER PROVIDED PARTIAL PRESSURE OF CO2 [ATM]
C       SCFLAG     I    FLAG USED INDICATING INITIALIZATION CYCLE
C       SK         O    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SO4       I/O   SULFATE CONCENTRATION [MOLES/LITER]
C       THETA      I    WATER HOLDING CAP OF SOIL [CM^2 H2O/M^3 SOIL]
C       XMG       I/O   MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGSO4     O    MAGNESIUM SULFATE CONC [MOLES/LITER]
C       XNA       I/O   SODIUM CONCENTRATION [MOLES/LITER]
C       XNAORG     O    ORGANIC SODIUM CONCENTRATION [MOLES/LITER]
C       XNASO4     O    SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4      I/O   AMMONIA CONCENTRATION [MOLES/LITER]
C
C       COMMENTS:  FIRST TIME ESTIMATES OF NON USER SUPPLIED CONC-
C         ENTRATIONS ARE ESTIMATED FOR COMPUTATIONAL USE.
C
C       EXTERNAL REFERENCES:
C
C       CALLED FROM:
C
C       PROGRAMMER:   KEN ROJAS
C
C       VERSION:  1.0
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER(SMLNBR=1.0D-60)
      LOGICAL SCFLAG
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
C     ..SET UP CONVERSION FACTORS TO SWITCH FROM MOLES/LITER OF EXTRACT
C       TO MOLES/GM OF SOIL
      SK=0.001D0*THETA/BD
      EK=CEC*1.0D-5
C     PCO2 = PPCO2 * 1.0D-3  OLD VERSION
      PCO2=PPCO2
C     
C     --CONVERT FROM MG/L TO MOLES/LITER
      AL=AL/26980.0D0
      XNA=XNA/23000.0D0
      CA=CA/40080.0D0
      CL=CL/35450.0D0
      XNO3=XNO3/14000.0D0
      XMG=XMG/24300.0D0
      XNH4=XNH4/14000.0D0
      HCO3=HCO3/61018.0D0
      CO3=CO3/60018.0D0
      SO4=SO4/96060.0D0
C     
C     --IF FIRST TIME THRU THEN GET ESTIMATE OF CONSTITUENTS
C     
      IF(SCFLAG) THEN
        ALOH=AL*1.0D-3
        ALSO4=MIN(AL,SO4)*1.0D-3
        XMGSO4=MIN(XMG,SO4)*1.0D-3
        XNASO4=MIN(XNA,SO4)*1.0D-3
        CASO4=MIN(CA,SO4)*1.0D-3
        ORG=MIN(AL,XNA)*1.0D-3
        XNAORG=XNA*ORG/1.0D6
        ALORG=AL*ORG/1.0D6
      ENDIF
CC    
C     --CHECK FOR ZERO CONCENTRATIONS AND REPLACE WITH A SMALL NUMBER
C     
      AL=MAX(AL,SMLNBR)
      CA=MAX(CA,SMLNBR)
      XNA=MAX(XNA,SMLNBR)
      XMG=MAX(XMG,SMLNBR)
      SO4=MAX(SO4,SMLNBR)
      ALOH=MAX(ALOH,SMLNBR)
      ALSO4=MAX(ALSO4,SMLNBR)
      XMGSO4=MAX(XMGSO4,SMLNBR)
      XNASO4=MAX(XNASO4,SMLNBR)
      CASO4=MAX(CASO4,SMLNBR)
      CL=MAX(CL,SMLNBR)
      CO3=MAX(CO3,SMLNBR)
      HCO3=MAX(HCO3,SMLNBR)
      XNH4=MAX(XNH4,SMLNBR)
      ORG=MAX(ORG,SMLNBR)
      XNAORG=MAX(XNAORG,SMLNBR)
      ALORG=MAX(ALORG,SMLNBR)
C     
C     
C     --MAINTAIN MASS BALANCE
C     
      SAL=AL
      SSO4=SO4
      SCA=CA
      SXNA=XNA
      SXMG=XMG
      SORG=ORG
C     
   10 AL=AL-ALOH-ALSO4-ALORG
      SO4=SO4-XMGSO4-XNASO4-CASO4-ALSO4
      CA=CA-CASO4
      XNA=XNA-XNASO4-XNAORG
      XMG=XMG-XMGSO4
      ORG=ORG-XNAORG-ALORG
C     
C     CHECK FOR ZEROS
C     
      IF(AL.LE.0.0D0.OR.CA.LE.0.0D0.OR.XNA.LE.0.0D0.OR.XMG.LE.0.0D0.OR.
     +    SO4.LE.0.0D0.OR.ORG.LE.0.0D0) THEN
C       
        AL=SAL
        SO4=SSO4
        CA=SCA
        XNA=SXNA
        XMG=SXMG
        ORG=SORG
C       
        ALOH=AL*1.0D-3
        ALSO4=MIN(AL,SO4)*1.0D-3
        XMGSO4=MIN(XMG,SO4)*1.0D-3
        XNASO4=MIN(XNA,SO4)*1.0D-3
        CASO4=MIN(CA,SO4)*1.0D-3
        ORG=MIN(AL,XNA)*1.0D-3
        XNAORG=XNA*ORG/1.0D6
        ALORG=AL*ORG/1.0D6
        GOTO 10
      ENDIF
C     
C     AL = MAX (AL, SMLNBR)
C     CA = MAX (CA, SMLNBR)
C     XNA = MAX (XNA, SMLNBR)
C     XMG = MAX (XMG, SMLNBR)
C     SO4 = MAX (SO4, SMLNBR)
C     ORG = MAX (ORG, SMLNBR)
C     
      RETURN
      END
C
      SUBROUTINE SCDBHK(CL,GAM1,GAM2,GAM3,XNO3,U)
C
C======================================================================
C
C       PURPOSE:  CALCULATE THE DEBYE-HUCKEL ACTIVITY COEFFICIENTS.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       AL         I    ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALOH       I    ALUMINUM HYDROXIDE CONCENTRATION [MOLES/LITER]
C       ALORG      I    ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALSO4      I    ALUMINUM SULFATE CONCENTRATION [MOLES/LITER]
C       CA         I    CALCIUM CONCENTRATION [MOLES/LITER]
C       CL         I    CHLORIDE CONCENTRATION [MOLES/LITER]
C       CO3        I    CARBONATE CONCENTRATION [MOLES/LITER]
C       GAM1       O    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM2       O    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       GAM3       O    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF THREE
C       H          I    HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3       I    BICARBONATE CONCENTRATION [MOLES/LITER]
C       ORG        I    ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       SO4        I    SULFATE CONCENTRATION [MOLES/LITER]
C       U          L    IONIC STRENGTH
C       XMG        I    MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XNA        I    SODIUM CONCENTRATION [MOLES/LITER]
C       XNASO4     I    SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4       I    AMMONIA CONCENTRATION [MOLES/LITER]
C
C       COMMENTS:
C
C       CALLED FROM: SCMAIN
C
C       PROGRAMMER: KEN ROJAS
C
C       VERSION:  1.0
C
C======================================================================
C
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
C     ..DETERMINE IONIC STRENGTH
      U=0.5D0*(4.0D0*(CA+XMG+SO4+CO3+ALOH+ALORG)+HCO3+ORG+XNASO4+XNA+CL+
     +    XNO3+H+XNH4+ALSO4+9.0D0*AL)
C     
C     ..COMPUTE DEBYE-HUCKEL ACTIVITY COEFFICIENTS
      XKWR=SQRT(U)/(1.0D0+SQRT(U))-0.3D0*U
C     
C     --ACTIVITY COEFFICIENT FOR VALENCY OF ONE
      GAM1=10.0D0**(-0.509D0*XKWR)
C     
C     --ACTIVITY COEFFICIENT FOR VALENCY OF TWO
      GAM2=10.0D0**(-0.509D0*4.0D0*XKWR)
C     
C     --ACTIVITY COEFFICIENT FOR VALENCY OF THREE
      GAM3=10.0D0**(-0.509D0*9.0D0*XKWR)
C     
      RETURN
C     
      END
C
C
      SUBROUTINE SCINIT(ECA,ENA,EMG,ENH4,EAL,EK,EPS,GAM1,GAM2,GAM3,PH,
     +    SK,ABORT)
C
C======================================================================
C
C       PURPOSE: SET TOTAL INITIAL VALUES IN MOLES/GRAM SOIL, AND STORE
C                THE INITIAL VALUES IN SOLUTION FOR LATER USE IN CHARGE
C                BALANCE EQUATIONS.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       AL         I    ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALI        O    INITIAL ALUMINUM CONCENTRATION [MOLES/GRAM SOIL]
C       ALIS       O    INITIAL ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALOH       I    ALUMINUM HYDROXIDE CONCENTRATION [MOLES/LITER]
C       ALOHI      O    INITIAL ALOH CONCENTRATION [MOLES/GRAM SOIL]
C       ALOHIS     O    INITIAL ALOH CONCENTRATION [MOLES/LITER]
C       ALORG      I    ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALORIS     O    INITIAL ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALSO4      I    ALUMINUM SULFATE CONCENTRATION [MOLES/LITER]
C       ALSO4I     O    INITIAL ALSO4 CONCENTRATION [MOLES/GRAM SOIL]
C       ALSOIS     O    INITIAL ALSO4 CONCENTRATION [MOLES/LITER]
C       CA         I    CALCIUM CONCENTRATION [MOLES/LITER]
C       CAI        O    INITIAL CALCIUM CONCENTRATION [MOLES/GRAM SOIL]
C       CAIS       O    INITIAL CALCIUM CONCENTRATION [MOLES/LITER]
C       CASO4      I    CALCIUM SULFATE CONC [MOLES/LITER]
C       CO3        I    CARBONATE CONCENTRATION [MOLES/LITER]
C       CO3I       O    INITIAL CARBONATE CONCENTRATION [MOLES/GRAM SOIL]
C       CO3IS      O    INITIAL CARBONATE CONCENTRATION [MOLES/LITER]
C       EAL        O    EXCHANGABLE IONS OF ALUMINUM
C       ECA        O    EXCHANGABLE IONS OF CALCIUM
C       EK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       EMG        O    EXCHANGABLE IONS OF MAGNESIUM
C       ENA        O    EXCHANGABLE IONS OF SODIUM
C       ENH4       O    EXCHANGABLE IONS OF AMMONIA
C       EPS        I    THRESHHOLD CONVERGENCE VALUE
C       GAM1       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF ONE
C       GAM2       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF TWO
C       GAM3       I    DEBYE-HUCKEL COEFFICIENT FOR VALENCY OF THREE
C       H          I    HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3       I    BICARBONATE CONCENTRATION [MOLES/LITER]
C       HCO3I      O    INITIAL BICARB CONCENTRATION [MOLES/GRAM SOIL]
C       HCO3IS     O    INITIAL BICARBONATE CONCENTRATION [MOLES/LITER]
C       HI         O    INITIAL HYDROGEN CONCENTRATION [MOLES/GRAM SOIL]
C       HIS        O    INITIAL HYDROGEN CONCENTRATION [MOLES/LITER]
C       ORG        I    ORGANIC MATTER CONCENTRATION [MOLES/LITER]
C       ORGI       O    INITIAL O.M. CONCENTRATION [MOLES/GRAM SOIL]
C       ORGIS      O    INITIAL O.M. CONCENTRATION [MOLES/LITER]
C       PH         I    PH [STANDARD UNITS]
C       SK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SO4        I    SULFATE CONCENTRATION [MOLES/LITER]
C       SO4I       O    INITIAL SULFATE CONCENTRATION [MOLES/GRAM SOIL]
C       SO4IS      O    INITIAL SULFATE CONCENTRATION [MOLES/LITER]
C       XMG        I    MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGI       O    INITIAL MAGNESIUM CONC [MOLES/GRAM SOIL]
C       XMGIS      O    INITIAL MAGNESIUM CONC [MOLES/LITER]
C       XMGSO4     I    MAGNESIUM SULFATE CONC [MOLES/LITER]
C       XNA        I    SODIUM CONCENTRATION [MOLES/LITER]
C       XNAI       O    INITIAL SODIUM CONCENTRATION [MOLES/GRAM SOIL]
C       XNAIS      O    INITIAL SODIUM CONCENTRATION [MOLES/LITER]
C       XNAORG     I    ORGANIC SODIUM CONCENTRATION [MOLES/LITER]
C       XNASIS     O    INITIAL SODIUM SULFATE CONCENTRATION [MOLES/LITER]
C       XNASO4     I    SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4       I    AMMONIA CONCENTRATION [MOLES/LITER]
C       XNH4I      O    INITIAL AMMONIA CONCENTRATION [MOLES/GRAM SOIL]
C
C       COMMENTS:
C
C       EXTERNAL REFERENCES:  ALKEXC, ACDEXC
C
C       CALLED FROM: SCMAIN
C
C       PROGRAMMER:   KENR
C
C       VERSION:
C
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
      LOGICAL ABORT
C
C     --DETERMINE EXCHANGABLE IONS
      IF(ECA.EQ.0.0D0.AND.ENA.EQ.0.0D0.AND.EMG.EQ.0.0D0.AND.ENH4.EQ.
     +    0.0D0.AND.EAL.EQ.0.0D0) THEN
        ENA=0.01D0
        IF(PH.GE.6.0D0) THEN
          CALL ALKEXC(CA,ECA,ENA,EMG,ENH4,EAL,GAM1,GAM2,XNA,XMG,XNH4)
        ELSE
          CALL ACDEXC(AL,CA,EAL,ECA,EMG,ENA,ENH4,GAM1,GAM2,GAM3,XMG,XNA,
     +        XNH4,EPS,ABORT)
        ENDIF
      ENDIF
C     
C     ..COMPUTE TOTAL INITIAL VALUES IN MOLES/GRAM SOIL.
      CAI=SK*(CA+CASO4)+EK*0.50D0*ECA
      XNAI=SK*(XNA+XNASO4+XNAORG)+EK*ENA
      XMGI=SK*(XMG+XMGSO4)+EK*0.50D0*EMG
      SO4I=SK*(SO4+CASO4+XMGSO4+XNASO4+ALSO4)
      ALI=SK*(AL+ALSO4+ALOH+ALORG)+EK/3.0D0*EAL
      XNH4I=SK*XNH4+EK*ENH4
      HCO3I=HCO3
      HI=H
      CO3I=CO3
      ORGI=SK*(ORG+XNAORG+ALORG)
C     
C     ..STORE INITIAL VALUES FOR USE IN CHARGE BALANCE EQUATIONS
      ORGIS=ORG
      CAIS=CA
      XMGIS=XMG
      XNAIS=XNA
      XNH4IS=XNH4
      ALIS=AL
      HIS=H
      SO4IS=SO4
      HCO3IS=HCO3
      CO3IS=CO3
      ALOHIS=ALOH
      ALSOIS=ALSO4
      XNASIS=XNASO4
      ALORIS=ALORG
C     
      RETURN
      END
C
      SUBROUTINE SCMAIN(BD,CEC,CL,GIBSIT,GYPSUM,LIME,PPCO2,SCFLAG,PH,
     +    THETA,XNO3,U,AECA,AENA,AEMG,AENH4,AEAL,ABORT)
C
C======================================================================
C
C       PURPOSE:  MAIN PROGRAM FOR SOLUTION OF THE SOIL CHEMISTRY
C         MODEL.  THIS MODULE SOLVES TWO DISTINCT SYSTEMS BASED ON
C         SOIL PH.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C
C       COMMENTS:
C
C       EXTERNAL REFERENCES:
C
C       CALLED FROM:
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION:  1.0
C
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
      PARAMETER(NUMK=17,NUMC=18)
      LOGICAL CONVRG,SCFLAG,LIME,GYPSUM,GIBSIT,ABORT
      DIMENSION XK(NUMK),XC(NUMC)
C
C     ..SET INITILIZATION FLAG FOR FIRST TIME RUN.
      ECA=AECA
      ENA=AENA
      EMG=AEMG
      ENH4=AENH4
      EAL=AEAL
      EPS=1.0D-9
      ICOUNT=0
      ABORT=.FALSE.
C     
C     ==========================
C     INITIALIZE LOCAL VARIABLES
C     ==========================
C     
C     ..FIND INITIAL CONCENTRATIONS
      CALL SCCON(BD,CEC,CL,EK,PCO2,PPCO2,SCFLAG,SK,THETA,XNO3)
C     
C     ..SOLVE FOR THE DEBYE-HUCKEL COEFFICIENT
      CALL SCDBHK(CL,GAM1,GAM2,GAM3,XNO3,U)
C     
C     ..FIND CONCENTRATION OF HYDROGEN FROM PH
C     IF (SCFLAG) THEN
C     H = 10.0D0 ** (-PH) / GAM1
C     CALL SCDBHK (CL,GAM1,GAM2,GAM3,XNO3,U)
C     ENDIF
C     
C     ..SET INITITAL VALUES FOR CHARGE AND MASS BALANCE CALCULATIONS
      PH=-LOG10(H*GAM1)
      CALL SCINIT(ECA,ENA,EMG,ENH4,EAL,EK,EPS,GAM1,GAM2,GAM3,PH,SK,ABORT
     +    )
      IF(.NOT.ABORT) THEN
C       
C       ==================================
C       START ITERATING UNTIL CONVERGENCE
C       ==================================
C       
C       WRITE(79,444)ORG,XNAORG,ALORG
   10   CONTINUE
        ICOUNT=ICOUNT+1
C       
        IF(PH.GE.6.0D0) THEN
C         
C         ..ALKALINE SYSTEM
C         
          CALL SCALK(ECA,ENA,EMG,ENH4,EK,GAM1,GAM2,GYPSUM,LIME,PCO2,SK,
     +        XK,ABORT)
C         
C         ..DETERMINE CONVERGENCE
C         
          AMX=0.0D0
          DO 20 I=1,NUMK
            AMX=MAX(AMX,DABS(XK(I)))
   20     CONTINUE
          IF(AMX.GE.EPS) THEN
            CONVRG=.FALSE.
          ELSE
            CONVRG=.TRUE.
          ENDIF
C         
C         ..SOLVE FOR PH IN THIS ITERATION
          PH=-LOG10(H*GAM1)
          IF(PH.LT.6.0D0) THEN
            ALORG=AL*ORG/(1.0D6*GAM2/(GAM3*GAM1))
C         CALL ACDEXC(AL,CA,EAL,ECA,EMG,ENA,ENH4,GAM1,GAM2,GAM3,XMG,
C         1                  XNA,XNH4,EPS,ABORT)
          ENDIF
C       PRINT*,'MAX FUNCTION VALUE (ALKALINE SYSTEM) = ',AMX,ICOUNT,ph
C       
        ELSE
C         
C         ..ACID SYSTEM
C         
          CALL SCACD(ECA,ENA,EMG,ENH4,EAL,EK,GAM1,GAM2,GAM3,GYPSUM,
     +        GIBSIT,LIME,PCO2,SK,XC,ABORT)
C         
C         ..DETERMINE CONVERGENCE
C         
          AMX=0.0D0
          DO 30 I=1,NUMC
            AMX=MAX(AMX,DABS(XC(I)))
   30     CONTINUE
          IF(AMX.GE.EPS) THEN
            CONVRG=.FALSE.
          ELSE
            CONVRG=.TRUE.
          ENDIF
C         
C         ..SOLVE FOR PH IN THIS ITERATION
          PH=-LOG10(H*GAM1)
          IF(PH.GE.6.0D0) THEN
            AL=AL+EK/3.0D0*EAL
            EAL=1.0D-15
            XNAORG=XNA*ORG/(1.0D6/GAM1**2.0D0)
C         CALL ALKEXC(CA,ECA,ENA,EMG,ENH4,EAL,GAM1,GAM2,XNA,XMG,XNH4)
          ENDIF
C       PRINT*,'MAX FUNCTION VALUE (ACID SYSTEM) = ',AMX,ICOUNT,ph
C       PRINT*,XC
C       
        ENDIF
C       
C       ..DETERMINE DEBYE-HUCKEL ACTIVITY COEFFICIENTS
        CALL SCDBHK(CL,GAM1,GAM2,GAM3,XNO3,U)
C       write(92,102)'NDEP',ALOH,ALSO4,CASO4,XMGSO4,XNASO4,XNAORG,ALORG,
C       +  ORG,AECA,AENA,AEMG,AENH4,AEAL,H,CA,XNA,XMG,CL,HCO3,SO4,AL,XNO3,
C       +  XNH4,CO3,U,PPCO2,LIME,GYPSUM,GIBSIT 
C102    format(1x,a4,1x,26g11.4,3l6)
C       
C       
C       ..CHECK FOR CONVERGENCE, IF NOT, SOLVE AGAIN
        IF(.NOT.CONVRG.AND.ICOUNT.LE.100.AND..NOT.ABORT) GOTO 10
C       
        IF(ICOUNT.EQ.101) THEN
          ORG=MIN(AL,XNA)*1.0D-3
          XNAORG=XNA*ORG/1.0D6
          ALORG=AL*ORG/1.0D6
          ABORT=.TRUE.
        ENDIF
      ENDIF
C     
C     ..SOLVE FOR FINAL VALUES AND CONVERT TO COMMON UNITS
C9999 CALL SCSOLV (CL,XNO3,EK,SK,ECA,ENA,EMG,ENH4,EAL)
      CALL SCSOLV(CL,XNO3)
C     
C     PRINT*,' NUMBER OF ITERATION STEPS ',ICOUNT
C     PRINT*,ECA,ENA,EMG,ENH4,EAL
C     PRINT*,'LIME:',LIME,'   GYPSUM:',GYPSUM,'   GIBSIT:',GIBSIT
C     PRINT*
      AECA=ECA
      AENA=ENA
      AEMG=EMG
      AENH4=ENH4
      AEAL=EAL
C     
      RETURN
C     1000 FORMAT (3(1X,E15.9))
      END
C
      SUBROUTINE SCSOLV(CL,XNO3)
C      SUBROUTINE SCSOLV (CL,XNO3,EK,SK,ECA,ENA,EMG,ENH4,EAL)
C
C======================================================================
C
C       PURPOSE:  THIS FUNCTION CALCULATES THE FINAL CONCENTRATIONS OF
C         CONSTITUENTS.  THE UNITS ARE CONVERTED FROM MOLES/LITER TO
C         MG/LITER.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       AL        I/O   ALUMINUM CONCENTRATION [MOLES/LITER]
C       ALI        I    INITIAL ALUMINUM CONCENTRATION [MOLES/GRAM SOIL]
C       ALOH       I    ALUMINUM HYDROXIDE CONCENTRATION [MOLES/LITER]
C       ALORG      I    ALUMINUM ORGANIC COMPLEX [MOLES/LITER]
C       ALSO4      I    ALUMINUM SULFATE CONCENTRATION [MOLES/LITER]
C       CA        I/O   CALCIUM CONCENTRATION [MOLES/LITER]
C       CAI        I    INITIAL CALCIUM CONCENTRATION [MOLES/GRAM SOIL]
C       CASO4      I    CALCIUM SULFATE CONC [MOLES/LITER]
C       CL        I/O   CHLORIDE CONCENTRATION [MOLES/LITER]
C       CO3        I    CARBONATE CONCENTRATION [MOLES/LITER]
C       CO3I       I    INITIAL CARBONATE CONCENTRATION [MOLES/GRAM SOIL]
C       EAL        I    EXCHANGABLE IONS FOR ALUMINUM
C       ECA        I    EXCHANGABLE IONS FOR CALCIUM
C       EK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       EMG        I    EXCHANGABLE IONS FOR MAGNESIUM
C       ENA        I    EXCHANGABLE IONS FOR SODIUM
C       ENH4       I    EXCHANGABLE IONS FOR AMMONIA
C       H          I    HYDROGEN CONCENTRATION [MOLES/LITER]
C       HCO3      I/O   BICARBONATE CONCENTRATION [MOLES/LITER]
C       HCO3I      I    INITIAL BICARB CONCENTRATION [MOLES/GRAM SOIL]
C       HI         I    INITIAL HYDROGEN CONCENTRATION [MOLES/GRAM SOIL]
C       PH         I    PH [STANDARD UNITS]
C       SK         I    CONVERSION FACTOR TO MOLES/GRAM OF SOIL
C       SO4       I/O   SULFATE CONCENTRATION [MOLES/LITER]
C       SO4I       I    INITIAL SULFATE CONCENTRATION [MOLES/GRAM SOIL]
C       XMG       I/O   MAGNESIUM CONCENTRATION [MOLES/LITER]
C       XMGI       I    INITIAL MAGNESIUM CONC [MOLES/GRAM SOIL]
C       XMGSO4     I    MAGNESIUM SULFATE CONC [MOLES/LITER]
C       XNA       I/O   SODIUM CONCENTRATION [MOLES/LITER]
C       XNAI       I    INITIAL SODIUM CONCENTRATION [MOLES/GRAM SOIL]
C       XNAORG     I    SODIUM ORGANIC COMPLEX CONCENTRATION [MOLES/LITER]
C       XNASO4     I    SODIUM SULFATE CONC [MOLES/LITER]
C       XNH4      I/O   AMMONIA CONCENTRATION [MOLES/LITER]
C       XNH4I      I    INITIAL AMMONIA CONCENTRATION [MOLES/GRAM SOIL]
C       XNO3      I/O   NITRATE CONCENTRATION
C
C       COMMENTS:
C
C       CALLED FROM:   SCMAIN
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION: 1.0
C
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      COMMON /SCCONC/ AL,ALOH,ALSO4,CA,CASO4,H,HCO3,SO4,XMG,XMGSO4,XNA,
     +    XNASO4,XNAORG,XNH4,ORG,CO3,ALORG,ALI,ALOHI,ALSO4I,CAI,CO3I,HI,
     +    HCO3I,ORGI,SO4I,XMGI,XNAI,XNH4I,ALIS,ALOHIS,ALSOIS,CAIS,CO3IS,
     +    HIS,HCO3IS,ORGIS,SO4IS,XMGIS,XNAIS,XNASIS,XNH4IS,ALORIS
C
C     ..FIND FINAL SOLUTIONS
      CA=CA+CASO4
      SO4=SO4+CASO4+XMGSO4+XNASO4+ALSO4
      XNA=XNA+XNASO4+XNAORG
      XMG=XMG+XMGSO4
      AL=AL+ALSO4+ALOH+ALORG
      ORG=ORG+XNAORG+ALORG
C     
C     --CALCULATE STATISTICS OF EACH SPECIES
C     CAF = SK*CA + EK*0.5D0*ECA
C     XMGF = SK*XMG + EK*0.5D0*EMG
C     XNAF = SK*XNA + EK*ENA
C     ALF = SK*AL + EK/3.0D0*EAL
C     XNH4F = SK*XNH4 + EK*ENH4
C     PCSO4 = CASO4  /SO4*100.0D0
C     PMSO4 = XMGSO4 /SO4*100.0D0
C     PNSO4 = XNASO4 /SO4*100.0D0
C     PASO4 = ALSO4  /SO4*100.0D0
C     PALOH = ALOH /AL*100.0D0
C     PALSO4 =  ALSO4/AL*100.0D0
C     PALORG =  ALORG/AL*100.0D0
C     PAL = 100.0D0 - PALSO4 - PALOH - PALORG
C     HCO3F = HCO3
C     FSO4 = 100.0D0 - PCSO4 - PMSO4 - PNSO4 - PASO4
C     CO3F = CO3
C     HF = H
C     PRINT*,'ORG BALANCE=>',ORGI-SK*ORG
C     
C     ..CONVERT TO COMMON UNITS
      AL=AL*26980.0D0
      XNA=XNA*23000.0D0
      CA=CA*40080.0D0
      CL=CL*35450.0D0
      XNO3=XNO3*14000.0D0
      XMG=XMG*24300.0D0
      XNH4=XNH4*14000.0D0
      HCO3=HCO3*61018.0D0
      CO3=CO3*60018.0D0
      SO4=SO4*96060.0D0
C     
C     WRITE (1,301) PCSO4,PMSO4,PNSO4,PASO4,FSO4,PALOH,PALSO4,
C     1              PALORG,PAL
C     301  FORMAT (//1X,'ION PAIR PERCENTAGES',/5X,'SULFATE SUMMARY',
C     1/7X,F5.2,1X,'PERCENT CASO4',/7X,F5.2,1X,'PERCENT MGSO4',
C     2/7X,F5.2,1X,'PERCENT NASO4',/7X,F5.2,1X,'PERCENT ALSO4',
C     3/7X,F6.2,1X,'PERCENT FREE SO4',//5X,'ALUMINUM SUMMARY',/7X,
C     6 F5.2,1X,'PERCENT ALOH2',/7X,F5.2,1X,'PERCENT ALSO4',
C     7/7X,F5.2,1X,'PERCENT ALORG',/7X,F6.2,1X,'FREE AL')
C     WRITE(1,303) CAI,XNAI,XMGI,CAF,XNAF,XMGF,CAI-CAF,XNAI-XNAF,
C     1XMGI-XMGF,SO4I,ALI,XNH4I,SO4F,ALF,XNH4F,SO4I-SO4F,ALI-ALF,
C     2XNH4I-XNH4F,HCO3I,HI,CO3I,HCO3F,HF,CO3F,HCO3I-HCO3F,HI-HF,
C     3CO3I-CO3F
C     303  FORMAT(//5X,'MASS BALANCE SUMMARY',/7X,'CAI= ',G9.3,3X,'XNAI= ',
C     1G9.3,3X,'XMGI= ',G9.3,/7X,'CAF= ',G9.3,3X,'XNAF= ',G9.3,3X,
C     2'XMGF= ',G9.3,/' DELTAS:',4X,G9.3,2(9X,G9.3),
C     3//7X,'SO4I= ',G9.3,3X,'ALI= ',G9.3,3X,'XNH4I= ',
C     3G9.3,/7X,'SO4F= ',G9.3,3X,'ALF= ',G9.3,3X,'XNH4F= ',G9.3,
C     4/' DELTAS:',5X,G9.3,8X,G9.3,10X,G9.3,
C     4//7X,'HCO3I= ',G9.3,3X,'HI= ',G9.3,3X,'CO3I= ',G9.3,/7X
C     5,'HCO3F= ',G9.3,3X,'HF= ',G9.3,3X,'CO3F= ',G9.3,
C     5/' DELTAS:',6X,G9.3,7X,G9.3,9X,G9.3)
C     WRITE (1,202) CA, XMG, XNA, HCO3, SO4, XNH4, AL, XNO3,PH
C     202  FORMAT (//1X,'CA= ',G12.4,2X,'MG= ',G12.4,2X,'NA= ',G12.4,2X,
C     1'HCO3= ',G12.4,/2X,'SO4= ',G12.4,2X,'NH4= ',G12.4,2X,'AL= ',
C     1G12.4,2X,'NO3= ',G12.4,/5X,'PH= ',F6.2)
C     
      RETURN
      END
C
      SUBROUTINE SOLVSL(A,Z,B,X,N,SOLVE)
C
C======================================================================
C
C       PURPOSE:  THIS FUNCTION IS THE MATRIX SOLVER FOR THE SOIL
C         CHEMISTRY SYSTEM.  THE METHOD USED IS A MAXIMUM PIVOT
C         SOLUTION TECHNIQUE.
C
C       REF:
C
C       VARIABLE DEFINITIONS:
C       VARIABLE  I/O   DESCRIPTION
C       --------  ---   -----------
C       A          I    JACOBIAN MATRIX OF PARTIAL DERIVATIVES
C       B          I    FUNCTIONAL SOLUTION VECTOR
C       CK              FLAG USED FOR MAXIMUM PIVOT ROTATION
C       LOC             FLAG USED FOR MAXIMUM PIVOT ROTATION
C       X          O    SOLUTION VECTOR OF CONCENTRATION CORRECTIONS
C
C       COMMENTS:
C
C       EXTERNAL REFERENCES:
C
C       CALLED FROM:
C
C       PROGRAMMER:  KEN ROJAS
C
C       VERSION: 1.0
C
C======================================================================
C
C     ..DECLARE VARIABLES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION Z(N,N),B(N),X(N),A(N,N+1),LOC(20)
      LOGICAL CK(20),SOLVE
C
C     ..INITIALIZE TEMPORARY MATRIX
      M=N+1
      DO 20 I=1,N
        DO 10 J=1,N
          A(I,J)=Z(I,J)
   10   CONTINUE
   20 CONTINUE
C     
C     ..CREATE AUGMENTED MATRIX WITH SOLUTION VECTOR
C     INCLUDED IN COLUMN M
      DO 30 I=1,N
        A(I,M)=-B(I)
        CK(I)=.TRUE.
   30 CONTINUE
C     
      DO 70 I=1,N
        IP=I+1
        AMAX=0.0D0
C       
C       ..FIND MAX PIVOT POINT FOR ITH COLUMN
        DO 40 K=1,N
          IF(DABS(A(K,I)).GT.AMAX.AND.CK(K)) THEN
            LOC(I)=K
            AMAX=DABS(A(K,I))
          ENDIF
   40   CONTINUE
C       
C       ..CHECK PIVOT VALUE IF TOO SMALL, BECAUSE MATRIX MAY BE SINGULAR
        IF(AMAX.GT.1.0D-60) THEN
          L=LOC(I)
          CK(L)=.FALSE.
C         
C         ..SOLVE SYSTEM FOR CURRENT POINT VALUE
          DO 60 J=1,N
            IF(J.NE.L) THEN
              F=-A(J,I)/A(L,I)
              DO 50 K=IP,M
                A(J,K)=A(J,K)+F*A(L,K)
   50         CONTINUE
            ENDIF
   60     CONTINUE
        ELSE
          SOLVE=.FALSE.
        ENDIF
   70 CONTINUE
C     
C     ..SOLVE FOR SOLUTION VECTOR
      DO 80 I=1,N
        L=LOC(I)
        X(I)=A(L,N+1)/A(L,I)
   80 CONTINUE
C     
      RETURN
      END
